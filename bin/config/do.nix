# dotfiles/bin/config/do.nix â® https://github.com/quackhack-mcblindy/dotfiles
{ # ğŸ¦† says â® Quack Powered natural language processing engine written in Nix & Bash - translates text to Shell commands
  self,
  lib,
  config,
  pkgs,
  sysHosts,
  cmdHelpers,
  ...
} : let
  cfg = config.yo;
  # ğŸ¦† says â® grabbinâ€™ all da scripts for ez listin'  
  scripts = config.yo.scripts; 
  scriptNames = builtins.attrNames scripts; # ğŸ¦† says â® just names - we never name one
  # ğŸ¦† says â® only scripts with known intentions
  scriptNamesWithIntents = builtins.filter (scriptName:
    let # ğŸ¦† says â® a intent iz kinda ..
      intent = generatedIntents.${scriptName};
      # ğŸ¦† says â® .. pointless if it haz no sentence data ..
      hasSentences = builtins.any (data: data ? sentences && data.sentences != []) intent.data;
    in # ğŸ¦† says â® .. so datz how we build da scriptz!
#      builtins.hasAttr scriptName generatedIntents && hasSentences
#  ) scriptNames; # ğŸ¦† says â® datz quackin' cool huh?!
      builtins.hasAttr scriptName generatedIntents && hasSentences
  ) (builtins.attrNames scriptsWithVoice);

#  scriptsWithVoice = lib.filterAttrs (_: script: script.voice != null) config.yo.scripts;
  # ğŸ¦† says â® only scripts with voice enabled and non-null voice config
  scriptsWithVoice = lib.filterAttrs (_: script: 
    script.voice != null && (script.voice.enabled or true)
  ) config.yo.scripts;  
  
  # ğŸ¦† says â® generate intents
  generatedIntents = lib.mapAttrs (name: script: {
    priority = script.voice.priority or 3;
    data = [{
      inherit (script.voice) sentences lists;
    }];
  }) scriptsWithVoice;

  # ğŸ¦† says â® helpz pass Nix path 4 intent data 2 Bash 
  intentBasePath = "${config.this.user.me.dotfilesDir}#nixosConfigurations.${config.this.host.hostname}.config.yo.scripts";
  
  # ğŸ¦† says â® QUACK! da duck take a list of listz and duck make all da possible combinationz
  cartesianProductOfLists = lists:
    # ğŸ¦† says â® if da listz iz empty .. 
    if lists == [] then
      [ [] ] # ğŸ¦† says â® .. i gib u empty listz of listz yo got it?
    else # ğŸ¦† says â® ELSE WAT?!
      let # ğŸ¦† says â® sorry.. i gib u first list here u go yo
        head = builtins.head lists;
        # ğŸ¦† says â® remaining listz for u here u go bro!
        tail = builtins.tail lists;
        # ğŸ¦† says â® calculate combinations for my tail - yo calc wher u at?!
        tailProduct = cartesianProductOfLists tail;
      in # ğŸ¦† says â® for everyy x in da listz ..
        lib.concatMap (x:
          # ğŸ¦† says â® .. letz combinez wit every tail combinationz ..  
          map (y: [x] ++ y) tailProduct
        ) head; # ğŸ¦† says â® dang! datz a DUCK COMBO alright!  
         
  # ğŸ¦† says â® here i duckie help yo out! makin' yo life eazy sleazy' wen declarative sentence yo typin'    
  expandOptionalWords = sentence: # ğŸ¦† says â® qucik & simple sentences we quacky & hacky expandin'
    let # ğŸ¦† says â® CHOP CHOP! Rest in lil' Pieceez bigg sentence!!1     
      tokens = lib.splitString " " sentence;      
      # ğŸ¦† says â® definin' dem wordz in da (braces) taggin' dem' wordz az (ALTERNATIVES) lettin' u choose one of dem wen triggerin' 
      isRequiredGroup = t: lib.hasPrefix "(" t && lib.hasSuffix ")" t;
      # ğŸ¦† says â® puttin' sentence wordz in da [bracket] makin' em' [OPTIONAL] when bitchin' u don't have to be pickin' woooho 
      isOptionalGroup = t: lib.hasPrefix "[" t && lib.hasSuffix "]" t;   
      expandToken = token: # ğŸ¦† says â® dis gets all da real wordz out of one token (yo!)
        if isRequiredGroup token then
          let # ğŸ¦† says â® thnx 4 lettin' ducklin' be cleanin' - i'll be removin' dem "()" 
            clean = lib.removePrefix "(" (lib.removeSuffix ")" token);
            alternatives = lib.splitString "|" clean; # ğŸ¦† says â® use "|" to split (alternative|wordz) yo 
          in  # ğŸ¦† says â® dat's dat 4 dem alternativez
            alternatives
        else if isOptionalGroup token then
          let # ğŸ¦† says â® here we be goin' again - u dirty and i'll be cleanin' dem "[]"
            clean = lib.removePrefix "[" (lib.removeSuffix "]" token);
            alternatives = lib.splitString "|" clean; # ğŸ¦† says â® i'll be stealin' dat "|" from u 
          in # ğŸ¦† says â® u know wat? optional means we include blank too!
            alternatives ++ [ "" ]
        else # ğŸ¦† says â® else i be returnin' raw token for yo
          [ token ];      
      # ğŸ¦† says â® now i gib u generatin' all dem combinationz yo
      expanded = cartesianProductOfLists (map expandToken tokens);      
      # ğŸ¦† says â® clean up if too much space, smush back into stringz for ya
      trimmedVariants = map (tokenList:
        let # ğŸ¦† says â® join with spaces then trim them suckers
          raw = lib.concatStringsSep " " tokenList;
          # ğŸ¦† says â® remove ALL extra spaces
          cleaned = lib.replaceStrings ["  "] [" "] (lib.strings.trim raw);
        in # ğŸ¦† says â® wow now they be shinin'
          cleaned 
      ) expanded; # ğŸ¦† says â® and they be multiplyyin'!      
      # ğŸ¦† says â® throwin' out da empty and cursed ones yo
      nonEmpty = lib.filter (s: s != "") trimmedVariants;
      hasFixedText = v: builtins.match ".*[^\\{].*" v != null; # ğŸ¦† says â® no no no, no nullin'
      validVariants = lib.filter hasFixedText nonEmpty;
    in # ğŸ¦† says â® returnin' all unique variantz of da sentences â€“ holy duck dat'z fresh 
      lib.unique validVariants;
  
  # ğŸ¦† says â® we be doin' sorta da same wit dem listz
  expandListInputVariants = value: 
    let # ğŸ¦† says â® first we choppy choppy - break up da list into word tokenz
      tokens = lib.splitString " " value;
      # ğŸ¦† says â® checkin' if a token be wrapped like [diz] = optional, ya feel?
      isOptional = t: lib.hasPrefix "[" t && lib.hasSuffix "]" t;
      # ğŸ¦† says â® now ducklin' expandz each token â€” either real or optional wit options
      expandToken = token:
        if isOptional token then
          let # ğŸ¦† says â® time 2 clean dat square junk up 4 yo bro
            clean = lib.removePrefix "[" (lib.removeSuffix "]" token);
             # ğŸ¦† says â® u know da drill - splittin' on da "|" to find alt optionalz
            alternatives = lib.splitString "|" clean;
          in
            alternatives
        else # ğŸ¦† says â® not optional? just be givin' back da token as iz
          [ token ];
      expanded = cartesianProductOfLists (map expandToken tokens);
      variants = map (tokenList:
        lib.replaceStrings [ "  " ] [ " " ] (lib.concatStringsSep " " tokenList)
      ) expanded;  # ğŸ¦† says â® only da fresh unique non-emptiez stayinâ€™ in da pond
    in lib.unique (lib.filter (s: s != "") variants);

  # ğŸ¦† says â® optimized pattern expansion
  expandToRegex = sentence: data:
    let
      # ğŸ¦† says â® helper function to convert patterns to regex
      convertPattern = token:
        if lib.hasPrefix "(" token then
          let
            clean = lib.removePrefix "(" (lib.removeSuffix ")" token);
            alternatives = lib.splitString "|" clean;
            escaped = map lib.escapeRegex alternatives;
          in "(?:" + lib.concatStringsSep "|" escaped + ")"
        else if lib.hasPrefix "[" token then
          let
            clean = lib.removePrefix "[" (lib.removeSuffix "]" token);
            alternatives = lib.splitString "|" clean;
            escaped = map lib.escapeRegex alternatives;
          in "(?:" + lib.concatStringsSep "|" escaped + ")?"
        else
          lib.escapeRegex token;
      
      # ğŸ¦† says â® split into tokens while preserving special groups
      tokenize = s:
        let
          groups = builtins.match "([^{]*)(\{[^}]*\})?(.*)" s;
        in
          if groups == null then [s]
          else let
            prefix = builtins.elemAt groups 0;
            param = builtins.elemAt groups 1;
            rest = builtins.elemAt groups 2;
            tokens = if prefix != "" then [prefix] else [];
            tokensWithParam = if param != null then tokens ++ [param] else tokens;
          in tokensWithParam ++ tokenize rest;
      
      # ğŸ¦† says â® process tokens into regex parts
      tokens = tokenize sentence;
      regexParts = map (token:
        if lib.hasPrefix "{" token then
          let
            param = lib.removePrefix "{" (lib.removeSuffix "}" token);
            isWildcard = data.lists.${param}.wildcard or false;
          in if isWildcard then "(.*)" else "\\b([^ ]+)\\b"
        else
          convertPattern token
      ) tokens;
      
      # ğŸ¦† says â® combine parts into final regex
      regex = "^" + lib.concatStrings regexParts + "$";
    in
      regex; 

  # ğŸ¦† says â® take each value like "yo|hey" and map it to its 'out' â€“ buildinâ€™ da translation matrix yo!
  makeEntityResolver = data: listName: # ğŸ¦† says â® i like ducks
    lib.concatMapStrings (entity:
      let 
        variants = expandListInputVariants entity."in"; # ğŸ¦† says â® "in" must always be quoted in Nix. never forget yo
      in # ğŸ¦† says â® otherwize itz an in like this one!
        lib.concatMapStrings (variant: ''
          "${variant}") echo "${entity.out}";;
        '') variants # ğŸ¦† says â® all of them yo!
    ) data.lists.${listName}.values; # ğŸ¦† says â® maps each "in" value to an echo of its "out"
  
  # ğŸ¦† says â® where da magic dynamic regex iz at 
  makePatternMatcher = scriptName: let
    dataList = generatedIntents.${scriptName}.data;    
  in '' # ğŸ¦† says â® diz iz how i pick da script u want 
    match_${scriptName}() { # ğŸ¦† says â® shushin' da caps â€“ lowercase life 4 cleaner dyn regex zen âœ¨
      local input="$(echo "$1" | tr '[:upper:]' '[:lower:]')" 
      # ğŸ¦† says â® always show input in debug mode
      # ğŸ¦† says â® watch the fancy stuff live in action  
      dt_debug "Trying to match for script: ${scriptName}" >&2
      dt_debug "Input: $input" >&2
      # ğŸ¦† says â® duck presentin' - da madnezz 
      ${lib.concatMapStrings (data:
        lib.concatMapStrings (sentence:
          lib.concatMapStrings (sentenceText: let
            # ğŸ¦† says â® now sentenceText is one of the expanded variants!
            parts = lib.splitString "{" sentenceText; # ğŸ¦† says â® diggin' out da goodies from curly nests! Gimme dem {param} nuggets! 
            firstPart = lib.escapeRegex (lib.elemAt parts 0); # ğŸ¦† says â® gotta escape them weird chars 
            restParts = lib.drop 1 parts;  # ğŸ¦† says â® now we in the variable zone quack?  
            # ğŸ¦† says â® process each part to build regex and params
            regexParts = lib.imap (i: part:
              let
                split = lib.splitString "}" part; # ğŸ¦† says â® yeah yeah curly close that syntax shell
                param = lib.elemAt split 0; # ğŸ¦† says â® name of the param in da curly â€“ ex: {user}
                after = lib.concatStrings (lib.tail split); # ğŸ¦† says â® anything after the param in this chunk
                # ğŸ¦† says â® Wildcard mode! anything goes - duck catches ALL the worms! (.*)
                isWildcard = data.lists.${param}.wildcard or false;
                regexGroup = if isWildcard then "(.*)" else "\\b([^ ]+)\\b"; # 82%
                # regexGroup = if isWildcard then "(.*)" else "([^ ]+)";
                # ğŸ¦† says â® ^ da regex that gon match actual input text
              in {
                regex = regexGroup + lib.escapeRegex after;
                param = param;
              }
            ) restParts;

            fullRegex = let
              clean = lib.strings.trim (firstPart + lib.concatStrings (map (v: v.regex) regexParts));
            in "^${clean}$"; # ğŸ¦† says â® mash all regex bits 2gether
            paramList = map (v: v.param) regexParts; # ğŸ¦† says â® the squad of parameters 
          in ''
            local regex='^${fullRegex}$'
            dt_debug "REGEX: $regex"
            if [[ "$input" =~ $regex ]]; then  # ğŸ¦† says â® DANG DANG â€“ regex match engaged 
              ${lib.concatImapStrings (i: paramName: ''
                # ğŸ¦† says â® extract match group #i+1 â€“ param value, come here plz 
                param_value="''${BASH_REMATCH[${toString (i+1)}]}"
                # ğŸ¦† says â® if param got synonym, apply the duckfilter 
                if [[ -n "''${param_value:-}" && -v substitutions["$param_value"] ]]; then
                  subbed="''${substitutions["$param_value"]}"
                  if [[ -n "$subbed" ]]; then
                    param_value="$subbed"
                  fi
                fi           
                ${lib.optionalString (
                  data.lists ? ${paramName} && !(data.lists.${paramName}.wildcard or false)
                ) ''
                  # ğŸ¦† says â® apply substitutions before case matchin'
                  if [[ -v substitutions["$param_value"] ]]; then
                    param_value="''${substitutions["$param_value"]}"
                  fi
                  case "$param_value" in
                    ${makeEntityResolver data paramName}
                    *) ;;
                  esac
                ''} # ğŸ¦† says â® declare global param â€“ duck want it everywhere! (for bash access)
                declare -g "_param_${paramName}"="$param_value"            
                declare -A params=()
                params["${paramName}"]="$param_value"
                matched_params+=("$paramName")
              '') paramList} # ğŸ¦† says â® set dat param as a GLOBAL VAR yo! every duck gotta know 
              # ğŸ¦† says â® build cmd args: --param valu
              cmd_args=()
              ${lib.concatImapStrings (i: paramName: ''
                value="''${BASH_REMATCH[${toString i}]}"
                cmd_args+=(--${paramName} "$value")
              '') paramList}
              dt_debug "REMATCH 1: ''${BASH_REMATCH[1]}"
              dt_debug "REMATCH 2: ''${BASH_REMATCH[2]}"
              dt_debug "REMATCH 3: ''${BASH_REMATCH[3]}"
              dt_debug "MATCHED SCRIPT: ${scriptName}"
              dt_debug "ARGS: ''${cmd_args[@]}"
              return 0
            fi
          '') (expandOptionalWords sentence)
        ) data.sentences
      ) dataList}
      return 1
    }
  ''; # ğŸ¦† says â® dat was fun! let'z do it again some time

  # ğŸ¦† says â® quack and scan, match bagan
  makeFuzzyPatternMatcher = scriptName: let
    dataList = generatedIntents.${scriptName}.data;
  in '' # ğŸ¦† says â® fuzz in code, waddle mode
    match_fuzzy_${scriptName}() {
      local input="$(echo "$1" | tr '[:upper:]' '[:lower:]')"
      local matched_sentence="$2"
      # ğŸ¦† says â® skip regex! dat shit iz crazy - use aligned wordz yo
      declare -A params=()
      local input_words=($input)
      local sentence_words=($matched_sentence)     
      # ğŸ¦† says â® extract params by aligning words cool huh
      for i in ''${!sentence_words[@]}; do
        local word="''${sentence_words[$i]}"
        if [[ "$word" == \{*\} ]]; then
          local param_name="''${word:1:-1}"
          params["$param_name"]="''${input_words[$i]}"
        fi
      done
      # ğŸ¦† says â® apply subs to params yo
      for param in "''${!params[@]}"; do
        local value="''${params[$param]}"
        if [[ -v substitutions["$value"] ]]; then
          params["$param"]="''${substitutions["$value"]}"
        fi
      done
      # ğŸ¦† says â® build da paramz
      cmd_args=()
      for param in "''${!params[@]}"; do
        cmd_args+=(--"$param" "''${params[$param]}")
      done
      return 0
    }
  '';
  
  # ğŸ¦† says â® matcher to json yao
  matchers = lib.mapAttrsToList (scriptName: data:
    let
      matcherCode = makePatternMatcher scriptName;
    in {
      name = scriptName;
      value = pkgs.writeText "${scriptName}-matcher" matcherCode;
    }
  ) generatedIntents;

  # ğŸ¦† says â® one shell script dat sourcez dem allz
  matcherSourceScript = pkgs.writeText "matcher-loader.sh" (
    lib.concatMapStringsSep "\n" (m: "source ${m.value}") matchers
  );

  # ğŸ¦† says â® oh duck... dis is where speed goes steroids yo iz diz cachin'? - no more nix evaluatin' lettin' jq takin' over
  intentDataFile = pkgs.writeText "intent-entity-map4.json" # ğŸ¦† says â® change name to force rebuild of file
    (builtins.toJSON ( # ğŸ¦† says â® packin' all our knowledges into a JSON duck-pond for bash to swim in!
      lib.mapAttrs (_scriptName: intentList:
        let
          allData = lib.flatten (map (d: d.lists or {}) intentList.data);
          # ğŸ¦† says â® collect all sentences for diz intent
          sentences = lib.concatMap (d: d.sentences or []) intentList.data;      
          # ğŸ¦† says â® expand all sentence variants
          expandedSentences = lib.unique (lib.concatMap expandOptionalWords sentences);
          # ğŸ¦† says â® "in" > "out" for dem' subz 
          substitutions = lib.flatten (map (lists: # ğŸ¦† says â® iterate through entity lists
            lib.flatten (lib.mapAttrsToList (_listName: listData: # ğŸ¦† says â® process each list definition
              if listData ? values then # ğŸ¦† says â® check for values existence
                lib.flatten (map (item: # ğŸ¦† says â® process each entity value
                  let # ğŸ¦† says â® clean and split input patterns
                    rawIn = item."in";
                    value = item.out;
                    # ğŸ¦† says â® handle cases like: "[foo|bar baz]" > ["foo", "bar baz"]
                    cleaned = lib.removePrefix "[" (lib.removeSuffix "]" rawIn);
                    variants = lib.splitString "|" cleaned;     
                in map (v: let # ğŸ¦† says â® juzt in case - trim dem' spaces and normalize whitespace         
                  cleanV = lib.replaceStrings ["  "] [" "] (lib.strings.trim v);
                in {   
                  pattern = if builtins.match ".* .*" cleanV != null
                            then cleanV         # ğŸ¦† says â® multi word == "foo bar"
                            else "(${cleanV})"; # ğŸ¦† says â® single word == \b(foo)\b
                  value = value;
                }) variants
              ) listData.values)
            else [] # ğŸ¦† says â® no listz defined - sorry dat gives empty list
          ) lists)
        ) allData);
      in { # ğŸ¦† says â® final per script structure
        inherit substitutions;
        sentences = expandedSentences;
      }
    ) generatedIntents
  ));

  # ğŸ¦† says â® quack! now we preslicin' dem sentences wit their fuzzynutty signatures for bitchin' fast fuzz-lookup!
  fuzzyIndex = lib.mapAttrsToList (scriptName: intent:
    lib.concatMap (data: # ğŸ¦† says â® dive into each intent entryz like itz bread crumbs
      lib.concatMap (sentence: # ğŸ¦† says â® grab all dem raw sentence templates
        map (expanded: { # ğŸ¦† says â® ayy, time to expand theze feathers
          script = scriptName; # ğŸ¦† says â® label diz bird wit itz intent script yo
          sentence = expanded; # ğŸ¦† says â® this da expanded sentence duck gon' match against
          # ğŸ¦† says â® precompute signature for FAAASTEERRr matching - quicky quacky snappy matchin' yo! 
          signature = let
            words = lib.splitString " " (lib.toLower expanded); # ğŸ¦† says â® lowercase & split likez stale rye
            sorted = lib.sort (a: b: lib.hasPrefix a b) words; # ğŸ¦† says â® duck sort dem quackz alphabetically-ish quack quack
          in builtins.concatStringsSep "|" sorted;  # ğŸ¦† says â® make a fuzzy-flyinâ€™ signature string, pipe separated - yo' know it 
        }) (expandOptionalWords sentence) # ğŸ¦† says â® diz iz where optional wordz becomez reality
      ) data.sentences # ğŸ¦† says â® waddlin' through all yo' sentencez
    ) intent.data # ğŸ¦† says â® scoopin' from every intentz
  ) generatedIntents; # ğŸ¦† says â® diz da sacred duck scripture â€” all yo' intents livez here boom  
  fuzzyIndexFile = pkgs.writeText "fuzzy-index.json" (builtins.toJSON fuzzyIndex);
  matcherDir = pkgs.linkFarm "yo-matchers" (
    map (m: { name = "${m.name}.sh"; path = m.value; }) matchers
  ); # ğŸ¦† says â® export da nix store path to da intent data - could be useful yo
  environment.variables."YO_INTENT_DATA" = intentDataFile; 
  environment.variables."á»²O_FUZZY_INDEX" = fuzzyIndexFile;   
  environment.variables."MATCHER_DIR" = matcherDir;
  environment.variables."MATCHER_SOURCE" = matcherSourceScript;
    
  # ğŸ¦† says â® priority system 4 runtime optimization
  scriptRecordsWithIntents = 
    let # ğŸ¦† says â® calculate priority
      calculatePriority = scriptName:
        generatedIntents.${scriptName}.priority or 3; # Default medium
      # ğŸ¦† says â® create script records metadata
      makeRecord = scriptName: rec {
        name = scriptName;
        priority = calculatePriority scriptName;
        hasComplexPatterns = 
          let 
            intent = generatedIntents.${scriptName};
            patterns = lib.concatMap (d: d.sentences) intent.data;
          in builtins.any (p: lib.hasInfix "{" p || lib.hasInfix "[" p) patterns;
      };    
    in lib.sort (a: b:
        # ğŸ¦† says â® primary sort: lower number = higher priority
        a.priority < b.priority 
        # ğŸ¦† says â® secondary sort: simple patterns before complex ones
        || (a.priority == b.priority && !a.hasComplexPatterns && b.hasComplexPatterns)
        # ğŸ¦† says â® third sort: alphabetical for determinism
        || (a.priority == b.priority && a.hasComplexPatterns == b.hasComplexPatterns && a.name < b.name)
      ) (map makeRecord scriptNamesWithIntents);
  # ğŸ¦† says â® generate optimized processing order
  processingOrder = map (r: r.name) scriptRecordsWithIntents;

  # ğŸ¦† duck say â® quacky hacky helper 2 escape md special charizardz yo
  escapeMD = str: let
    replacements = [
      [ "\\" "\\\\" ]
      [ "*" "\\*" ]
      [ "`" "\\`" ]
      [ "_" "\\_" ]
      [ "[" "\\[" ]
      [ "]" "\\]" ]
    ];
  in
    lib.foldl (acc: r: lib.replaceStrings [ (builtins.elemAt r 0) ] [ (builtins.elemAt r 1) ] acc) str replacements;


 
  # ğŸ¦† says â® conflict detection - no bad voice intentz quack!  
  assertionCheckForConflictingSentences = let
    # ğŸ¦† says â® collect all expanded sentences with their script originz
    allExpandedSentences = lib.flatten (lib.mapAttrsToList (scriptName: intent:
      lib.concatMap (data:
        lib.concatMap (sentence:
          map (expanded: {
            inherit scriptName;
            sentence = expanded;
            original = sentence;
            # ğŸ¦† says â® extract parameter positionz & count da fixed words
            hasWildcardAtEnd = lib.hasSuffix " {search}" (lib.toLower expanded) || 
                              lib.hasSuffix " {param}" (lib.toLower expanded) ||
                              (lib.hasInfix " {" expanded && 
                               !(lib.hasInfix "} " expanded)); # ğŸ¦† says â® wildcard at end if no } followed by space
            fixedWordCount = let
              words = lib.splitString " " expanded;
              nonParamWords = lib.filter (word: 
                !(lib.hasPrefix "{" word) && !(lib.hasSuffix "}" word)
              ) words;
            in lib.length nonParamWords;
          }) (expandOptionalWords sentence)
        ) data.sentences
      ) intent.data
    ) generatedIntents);
    # ğŸ¦† says â® check for prefix conflictz
    checkPrefixConflicts = sentences:
      let
        sortedSentences = lib.sort (a: b: 
          lib.stringLength a.sentence < lib.stringLength b.sentence
        ) sentences;
        conflicts = lib.foldl (acc: shorterItem:
          let
            shorter = shorterItem.sentence;
            shorterScript = shorterItem.scriptName;
            shorterHasWildcard = shorterItem.hasWildcardAtEnd;
          in
            acc ++ (lib.foldl (innerAcc: longerItem:
              let
                longer = longerItem.sentence;
                longerScript = longerItem.scriptName;
              in
                if shorterScript != longerScript then
                  if lib.hasPrefix (shorter + " ") longer && shorterHasWildcard then
                    innerAcc ++ [{
                      type = "PREFIX_CONFLICT";
                      shorter = shorter;
                      longer = longer;
                      scripts = [shorterScript longerScript];
                      reason = "Shorter pattern '${shorter}' (ends with wildcard) is a prefix of '${longer}'";
                    }]
                  else
                    innerAcc
                else
                  innerAcc
            ) [] sortedSentences)
        ) [] sortedSentences;
      in
        conflicts;
    # ğŸ¦† says â® find prefix conflictz!
    sentencesByText = lib.groupBy (item: item.sentence) allExpandedSentences;
    exactConflicts = lib.filterAttrs (sentence: items:
      let 
        uniqueScripts = lib.unique (map (item: item.scriptName) items);
      in 
        lib.length uniqueScripts > 1
    ) sentencesByText; 
    # ğŸ¦† says â® find duplicatez!
    exactConflictList = lib.mapAttrsToList (sentence: items:
      let
        scripts = lib.unique (map (item: item.scriptName) items);
      in { # ğŸ¦†  says â® format exact conflictz dawg
        type = "EXACT_CONFLICT";
        sentence = sentence;
        scripts = scripts;
        reason = "Exact pattern match in scripts: ${lib.concatStringsSep ", " scripts}";
      }
    ) exactConflicts;   
    # ğŸ¦†  says â® find prefix conflictz
    prefixConflicts = checkPrefixConflicts allExpandedSentences;    
    # ğŸ¦†  says â® letz put dem conflictz together okay?
    allConflicts = exactConflictList ++ prefixConflicts;
    hasConflicts = allConflicts != [];    
    # ğŸ¦†  says â® find da prefix conflictz  
  in {
    assertion = !hasConflicts;
    message = 
      if hasConflicts then
        let
          conflictMsgs = map (conflict:
            if conflict.type == "EXACT_CONFLICT" then
              ''
              ğŸ¦† says â® CONFLICT! 
                Pattern "${conflict.sentence}"
                In scripts: ${lib.concatStringsSep ", " conflict.scripts}
              ''
            else if conflict.type == "PREFIX_CONFLICT" then
              ''
              ğŸ¦† says â® CONFLICT!
                Shorter: "${conflict.shorter}" (ends with wildcard)
                Longer:  "${conflict.longer}"
                Scripts: ${lib.concatStringsSep ", " conflict.scripts}
                Reason:  ${conflict.reason}
              ''
            else
              ""
          ) allConflicts;
        in
          "Sentence conflicts detected in voice definition:\n\n" +
          lib.concatStringsSep "\n" conflictMsgs +
          "\n\nğŸ¦† says â® fix da conflicts before rebuildin' yo!"
      else
        "No sentence conflicts found.";
  };

  # ğŸ¦† says â® category based helper with actual names instead of {param}
  voiceSentencesHelpFile = pkgs.writeText "voice-sentences-help.md" (
    let
      scriptsWithVoice = lib.filterAttrs (_: script: 
        script.voice != null && script.voice.sentences != [] && (script.voice.enabled or true)
      ) config.yo.scripts;
      
      # ğŸ¦† says â® replace {param} with actual values from voice lists
      replaceParamsWithValues = sentence: voiceData:
        let
          # ğŸ¦† says â® find all {param} placeholders in the sentence
          paramMatches = builtins.match ".*(\\{([^}]+)\\}).*" sentence;
          processToken = token:
            if lib.hasPrefix "{" token && lib.hasSuffix "}" token then
              let
                paramName = lib.removePrefix "{" (lib.removeSuffix "}" token);
                listData = voiceData.lists.${paramName} or null;
              in
                if listData != null then
                  if listData.wildcard or false then
                    "ANYTHING"
                  else
                    let
                      # ğŸ¦† says â® get all possible input values
                      values = map (v: v."in") listData.values;
                      # ğŸ¦† says â® expand any optional patterns like [foo|bar]
                      expandedValues = lib.concatMap expandListInputVariants values;
                      # ğŸ¦† says â® take first few examples for display
                      examples = lib.take 3 (lib.unique expandedValues);
                    in
                      if examples == [] then "ANYTHING"
                      else "(" + lib.concatStringsSep "|" examples + 
                           (if lib.length examples < lib.length expandedValues then "|...)" else ")")
                else
                  "ANYTHING" # ğŸ¦† says â® fallback if param not found
            else
              token;
          
          # ğŸ¦† says â® split sentence and process each token
          tokens = lib.splitString " " sentence;
          processedTokens = map processToken tokens;
        in
          lib.concatStringsSep " " processedTokens;
      
      # ğŸ¦† says â® group by category
      groupedScripts = lib.groupBy (script: script.category or "ğŸ§© Miscellaneous") 
        (lib.attrValues scriptsWithVoice);
      
      # ğŸ¦† says â® generate category sections with param replacement
      categorySections = lib.mapAttrsToList (category: scripts:
        let
          scriptLines = map (script:
            let
              # ğŸ¦† says â® replace params in each sentence
              sentenceLines = lib.concatMapStrings (sentence: 
                let processedSentence = replaceParamsWithValues sentence script.voice;
                in "    - \"${escapeMD processedSentence}\"\n"
              ) script.voice.sentences;
            in
              "  **${escapeMD script.name}**:\n${sentenceLines}"
          ) (lib.sort (a: b: a.name < b.name) scripts);
        in
          "# ${category}\n\n${lib.concatStringsSep "\n" scriptLines}"
      ) groupedScripts;
      
      # ğŸ¦† says â® statistics
      totalScripts = lib.length (lib.attrNames config.yo.scripts);
      voiceScripts = lib.length (lib.attrNames scriptsWithVoice);
      totalPatterns = config.yo.generatedPatterns;
      totalPhrases = config.yo.understandsPhrases;    
      stats = ''  
  # ----â”€â”€â”€â”€----â”€â”€â‹†â‹…â˜†â˜†â˜†â‹…â‹†â”€â”€â”€â”€â”€----â”€ #
  # Total:  
  - **Scripts with voice enabled**: ${toString voiceScripts} / ${toString totalScripts}
  - **Generated patterns**: ${toString totalPatterns}
  - **Understandable phrases**: ${toString totalPhrases}
      '';
    in
      "# ğŸ¦† Voice Commands\nOne-of required words are marked (say|one)\nOptional words are marked [no|have|to]\n\n${lib.concatStringsSep "\n\n" categorySections}\n\n${stats}"
  );

  # ğŸ¦† duck say â® constructs GitHub "blob" URL based on `config.this.user.me.repo` 
  githubBaseUrl = let # ğŸ¦† duck say â® pattern match to extract username and repo name
    matches = builtins.match ".*github.com[:/]([^/]+)/([^/\\.]+).*" config.this.user.me.repo;
  in if matches != null then # ğŸ¦† duck say â® if match - construct
    "https://github.com/${builtins.elemAt matches 0}/${builtins.elemAt matches 1}/blob/main"
  else ""; # ğŸ¦† duck say â® no match? empty string


 # ğŸ¦† duck say â® we build da scripts again but diz time for the READNE and diz time script names > links 
  helpTextFile = pkgs.writeText "yo-helptext.md" helpText;
  # ğŸ¦† duck say â® markdown help text
  helpText = let 
    # ğŸ¦† duck say â® URL escape helper for GitHub links
    escapeURL = str: builtins.replaceStrings [" "] ["%20"] str;
  
    # ğŸ¦† duck say â® categorize scripts - ONLY VOICE READY SCRIPTS
    visibleScripts = lib.filterAttrs (_: script: script.visibleInReadme && script.voiceReady) cfg.scripts;
    groupedScripts = lib.groupBy (script: script.category) (lib.attrValues visibleScripts);
    sortedCategories = lib.sort (a: b: 
      # ğŸ¦† duck wants â® system management to be listed first yo
      if a == "ğŸ–¥ï¸ System Management" then true
      else if b == "ğŸ–¥ï¸ System Management" then false
      else a < b # ğŸ¦† duck say â® after dat everything else quack quack
    ) (lib.attrNames groupedScripts);
  
    # ğŸ¦† duck say â® create table rows with category separatorz 
    rows = lib.concatMap (category:
      let # ğŸ¦† duck say â® sort from A to Ã–  
        scripts = lib.sort (a: b: a.name < b.name) groupedScripts.${category};
      in
        [ # ğŸ¦† duck say â® add **BOLD** header table row for category
          "| **${escapeMD category}** | | |"
        ] 
        # ğŸ¦† duck say â® each yo script goes into a table row
        ++ (map (script:
          let  # ğŸ¦† duck say â® format list of aliases
            aliasList = if script.aliases != [] then
              lib.concatStringsSep ", " (map escapeMD script.aliases)
            else "";
            # ğŸ¦† duck say â® generate CLI parameter hints, with [] for optional/defaulted
            paramHint = lib.concatStringsSep " " (map (param:
              if param.optional || param.default != null
              then "[--${param.name}]"
              else "--${param.name}"
            ) script.parameters);
            # ğŸ¦† duck say â® render yo script name as link + parameters as plain text
            syntax = 
              if githubBaseUrl != "" then
                "[yo ${escapeMD script.name}](${githubBaseUrl}/${escapeURL script.filePath}) ${paramHint}"
              else
                "yo ${escapeMD script.name} ${paramHint}";
          in 
            # ğŸ¦† duck say â® only voice ready scripts shown (all of them now)
            "| ${syntax} | ${aliasList} | ${escapeMD script.description} |"
        ) scripts)
    ) sortedCategories;
  
  in lib.concatStringsSep "\n" rows;

  hej = builtins.readFile voiceSentencesHelpFile;
# ğŸ¦† says â® expose da magic! dis builds our NLP
in { # ğŸ¦† says â® YOOOOOOOOOOOOOOOOOO    
  file."sentences/README.md" = ''
    ## ğŸ¦†âœ¨ COMMANDS! yo  
    
    ğŸ¦†ğŸ   HOME via îœ˜ via ğŸ v3.12.10 
    11:37:13 â¯ yo -h
    Optional parameters marked [optional]
    | Command Syntax               | Description                |
    |------------------------------|----------------------------|
    ${helpText}

    ## ğŸ¦†ğŸš€ SENTENCES! qwack    
    ğŸ¦†ğŸ   HOME via îœ˜ via ğŸ v3.12.10 
    11:38:13 â¯ yo do -h
    ${hej}
  '';

  yo.scripts = { # ğŸ¦† says â® quack quack quack quack quack.... qwack 
    do = { # ğŸ¦† says â® wat ='( 
      description = "Natural language to Shell script translator with dynamic regex matching and automatic parameter resolutiion";
      aliases = [ "d" ];
      category = "âš™ï¸ Configuration"; # ğŸ¦† says â® duckgorize iz zmart wen u hab many scriptz i'd say!
      logLevel = "INFO";
      autoStart = false;
      parameters = [
        { name = "input"; description = "Text to parse into a yo command"; optional = false; }
        { name = "fuzzyThreshold"; type = "int"; description = "Minimum procentage for considering fuzzy matching sucessful. (1-100)"; default = 15; }
      ]; 
      helpFooter = ''
        cat ${voiceSentencesHelpFile}
      '';
      code = ''
        set +u  
        ${cmdHelpers} # ğŸ¦† says â®load required bash helper functions 
        FUZZY_THRESHOLD=$fuzzyThreshold
        intent_data_file="${intentDataFile}" # ğŸ¦† says â® cache dat JSON wisdom, duck hates slowridez
        YO_FUZZY_INDEX="${fuzzyIndexFile}" # for fuzzy nutty duckz
        text="$input" # ğŸ¦† says â® for once - i'm lettin' u doin' da talkin'
        match_result_flag=$(mktemp)
        trap 'rm -f "$match_result_flag"' EXIT
        echo "waiting" > "$match_result_flag"
        debug_attempted_matches=()
        substitution_applied=false   
        declare -A script_substitutions_data
        declare -A script_has_lists  
        intent_data_json=$(<"$intent_data_file")
        while IFS=$'\t' read -r script pattern value; do
            if [[ -n "$script" ]]; then
                script_has_lists["$script"]=1
                key="''${script}:''${pattern}"
                script_substitutions_data["$key"]="$value"
            fi
        done < <(
            jq -r 'to_entries[] | .key as $script | .value.substitutions[]? | 
                    [$script, .pattern, .value] | @tsv' \
            <<<"$intent_data_json"
        )
        levenshtein() {
          local a="$1" b="$1"
          local -i len_a=''${#a} len_b=''${#b}
          local -a d; local -i i j cost    
          for ((i=0; i<=len_a; i++)); do d[i]=$i; done
          for ((j=1; j<=len_b; j++)); do
            prev=$j
            for ((i=1; i<=len_a; i++)); do
              [[ "''${a:i-1:1}" == "''${b:j-1:1}" ]] && cost=0 || cost=1
              act=$(( d[i-1] + cost ))
              d[i]=$(( (d[i]+1) < (prev+1) ? 
                       ((d[i]+1) < act ? d[i]+1 : act) : 
                       ((prev+1) < act ? prev+1 : act) ))
              prev=$((d[i]))
            done
            d[0]=$j
          done
          echo ''${d[len_a]}
        }
        
        # ğŸ¦† says â® subz and entities lists handler yo
        resolve_entities() {
          local script="$1"
      
      local text="$2"
          local replacements
          local pattern out
          declare -A substitutions
          # ğŸ¦† says â® skip subs if script haz no listz
          has_lists=$(jq -e '."'"$script"'"?.substitutions | length > 0' "$intent_data_file" 2>/dev/null || echo false)
          if [[ "$has_lists" != "true" ]]; then
            echo -n "$text"
            echo "|declare -A substitutions=()"  # ğŸ¦† says â® empty substitutions
            return
          fi                    
          # ğŸ¦† says â® dis is our quacktionary yo 
          replacements=$(jq -r '.["'"$script"'"].substitutions[] | "\(.pattern)|\(.value)"' "$intent_data_file")
          while IFS="|" read -r pattern out; do
            if [[ -n "$pattern" && "$text" =~ $pattern ]]; then
              original="''${BASH_REMATCH[0]}"
              [[ -z "''$original" ]] && continue # ğŸ¦† says â® duck no like empty string
              substitutions["''$original"]="$out"
              substitution_applied=true # ğŸ¦† says â® rack if any substitution was applied
              text=$(echo "$text" | sed -E "s/\\b$pattern\\b/$out/g") # ğŸ¦† says â® swap the word, flip the script 
            fi
          done <<< "$replacements"      
          echo -n "$text"
          echo "|$(declare -p substitutions)" # ğŸ¦† says â® returning da remixed sentence + da whole 
        }
        trigram_similarity() {
          local str1="$1"
          local str2="$2"
          declare -a tri1 tri2 # ğŸ¦† says â® generate trigramz
          for ((i=0; i<''${#str1}-2; i++)); do
            tri1+=( "''${str1:i:3}" )
          done
          for ((i=0; i<''${#str2}-2; i++)); do
            tri2+=( "''${str2:i:3}" )
          done # ğŸ¦† says â® count dem' matches yo
          local matches=0
          for t in "''${tri1[@]}"; do
            [[ " ''${tri2[*]} " == *" $t "* ]] && ((matches++))
          done # ğŸ¦† says â® calc da % yo
          local total=$(( ''${#tri1[@]} + ''${#tri2[@]} ))
          (( total == 0 )) && echo 0 && return
          echo $(( 100 * 2 * matches / total ))  # ğŸ¦† says â® 0-100 scale
        }       
        levenshtein_similarity() {
          local a="$1" b="$2"
          local len_a=''${#a} len_b=''${#b}
          local max_len=$(( len_a > len_b ? len_a : len_b ))   
          (( max_len == 0 )) && echo 100 && return     
          local dist=$(levenshtein "$a" "$b")
          local score=$(( 100 - (dist * 100 / max_len) ))         
          # ğŸ¦† says â® boostz da score for same startin' charizard yo
          [[ "''${a:0:1}" == "''${b:0:1}" ]] && score=$(( score + 10 ))
          echo $(( score > 100 ? 100 : score )) # ğŸ¦† says â® 100 iz da moon yo
        }
        
        for f in "$MATCHER_DIR"/*.sh; do [[ -f "$f" ]] && source "$f"; done
        scripts_ordered_by_priority=( ${lib.concatMapStringsSep "\n" (name: "  \"${name}\"") processingOrder} )
        dt_info "$scripts_ordered_by_priority"
        find_best_fuzzy_match() {
          local input="$1"
          local best_score=0
          local best_match=""
          local normalized=$(echo "$input" | tr '[:upper:]' '[:lower:]' | tr -d '[:punct:]')
          local candidates
          mapfile -t candidates < <(jq -r '.[] | .[] | "\(.script):\(.sentence)"' "$YO_FUZZY_INDEX")
          dt_debug "Found ''${#candidates[@]} candidates for fuzzy matching"
          for candidate in "''${candidates[@]}"; do
            IFS=':' read -r script sentence <<< "$candidate"
            local norm_sentence=$(echo "$sentence" | tr '[:upper:]' '[:lower:]' | tr -d '[:punct:]')
            local tri_score=$(trigram_similarity "$normalized" "$norm_sentence")
            (( tri_score < 30 )) && continue
            local score=$(levenshtein_similarity "$normalized" "$norm_sentence")  
            if (( score > best_score )); then
              best_score=$score
              best_match="$script:$sentence"
              dt_info "New best match: $best_match ($score%)"
            fi
          done
          if [[ -n "$best_match" ]]; then
            echo "$best_match|$best_score"
          else
            echo ""
          fi
        }
           
        # ğŸ¦† says â® insert matchers, build da regex empire. yo
#        ${lib.concatMapStrings (name: makePatternMatcher name) scriptNamesWithIntents}  
        # ğŸ¦† says â® for dem scripts u defined intents for ..
        exact_match_handler() {        
          for script in "''${scripts_ordered_by_priority[@]}"; do
            # ğŸ¦† says â® .. we insert wat YOU sayz & resolve entities wit dat yo
            resolved_output=$(resolve_entities "$script" "$text")
            resolved_text=$(echo "$resolved_output" | cut -d'|' -f1)
            dt_debug "Tried: match_''${script} '$resolved_text'"
            # ğŸ¦† says â® we declare som substitutionz from listz we have - duckz knowz why 
            subs_decl=$(echo "$resolved_output" | cut -d'|' -f2-)
            declare -gA substitutions || true
            eval "$subs_decl" >/dev/null 2>&1 || true
            # ğŸ¦† says â® we hab a match quacky quacky diz sure iz hacky!
            if match_$script "$resolved_text"; then      
              if [[ "$(declare -p substitutions 2>/dev/null)" =~ "declare -A" ]]; then
                for original in "''${!substitutions[@]}"; do
                  dt_debug "Substitution: $original >''${substitutions[$original]}";
                  [[ -n "$original" ]] && dt_info "$original > ''${substitutions[$original]}" # ğŸ¦† says â® see wat duck did there?
                done # ğŸ¦† says â® i hop duck pick dem right - right?
              fi
              args=() # ğŸ¦† says â® duck gettin' ready 2 build argumentz 4 u script 
              for arg in "''${cmd_args[@]}"; do
                dt_debug "ADDING PARAMETER: $arg"
                args+=("$arg")  # ğŸ¦† says â® collecting them shell spell ingredients
              done
         
              # ğŸ¦† says â® final product - hope u like say duck!
              paramz="''${args[@]}" && echo
              echo "exact" > "$match_result_flag" # ğŸ¦† says â® tellz fuzzy handler we done
              
              echo "   â”Œâ”€(yo-$script)"
              echo "   â”‚ğŸ¦†"
              if [ ''${#args[@]} -eq 0 ]; then
                echo "   â””â”€ğŸ¦† says â® no parameters yo"
              else
                for ((i=0; i<''${#args[@]}; i+=2)); do
                  if [ $i -eq 0 ]; then
                    echo -n "   â””â”€â® "
                  else
                    echo -n "   â””â”€â® "
                  fi
                  echo -n "''${args[$i]}"
                  if [ $((i+1)) -lt ''${#args[@]} ]; then
                    echo " ''${args[$((i+1))]}"
                  else
                    echo
                  fi
                done
              fi
              dt_debug "Executing: yo $script $paramz" 
              # ğŸ¦† says â® EXECUTEEEEEEEAAA  â€“ HERE WE QUAAAAACKAAAOAA
              exec "yo-$script" "''${args[@]}"   
              # kill -9 $$  # ğŸ¦† says â® kill the entire script process
              return 0
            fi         
          done
          # ğŸ¦† says â® tell fuzzy no exact match found
          dt_info "Exact: No exact match found"
          echo "exact_finished" > "$match_result_flag"
        }        

        ${lib.concatMapStrings (name: makeFuzzyPatternMatcher name) scriptNamesWithIntents}  
        # ğŸ¦† SCREAMS â® FUZZY WOOOO TO THE MOON                
        fuzzy_match_handler() {
          resolved_output=$(resolve_entities "dummy" "$text") # ğŸ¦† says â® We'll resolve 4real after matchin'
          resolved_text=$(echo "$resolved_output" | cut -d'|' -f1)
          fuzzy_result=$(find_best_fuzzy_match "$resolved_text")
          [[ -z "$fuzzy_result" ]] && return 1

          IFS='|' read -r combined match_score <<< "$fuzzy_result"
          IFS=':' read -r matched_script matched_sentence <<< "$combined"
          if (( match_score < FUZZY_THRESHOLD )); then
            dt_debug "Fuzzy match score $match_score below threshold $FUZZY_THRESHOLD, skipping."
            return 1
          fi
          dt_debug "Best fuzzy script: $matched_script (score: $match_score%)"

          # ğŸ¦† says â® resolve entities agein, diz time for matched script yo
          resolved_output=$(resolve_entities "$matched_script" "$text")
          resolved_text=$(echo "$resolved_output" | cut -d'|' -f1)
          subs_decl=$(echo "$resolved_output" | cut -d'|' -f2-)
          declare -gA substitutions || true
          eval "$subs_decl" >/dev/null 2>&1 || true

          #if (( best_score >= $FUZZY_THRESHOLD )); then
          # ğŸ¦† says â® we hab a match quacky quacky diz sure iz hacky!
          if match_fuzzy_$matched_script "$resolved_text" "$matched_sentence"; then
            if [[ "$(declare -p substitutions 2>/dev/null)" =~ "declare -A" ]]; then
              for original in "''${!substitutions[@]}"; do
                dt_debug "Substitution: $original >''${substitutions[$original]}";
                [[ -n "$original" ]] && dt_info "$original > ''${substitutions[$original]}" # ğŸ¦† says â® see wat duck did there?
              done # ğŸ¦† says â® i hop duck pick dem right - right?
            fi
            args=() # ğŸ¦† says â® duck gettin' ready 2 build argumentz 4 u script 
            for arg in "''${cmd_args[@]}"; do
              dt_debug "ADDING PARAMETER: $arg"
              args+=("$arg")  # ğŸ¦† says â® collecting them shell spell ingredients
            done
            # ğŸ¦† says â® wait for exact match to finish
            # while kill -0 "$pid1" 2>/dev/null; do
            dt_debug "Fuzzy handler: Waiting for exact match to finish..."
            while [[ $(cat "$match_result_flag") == "waiting" ]]; do
              dt_debug "Fuzzy: Still waiting for exact match flag... (loop)"
              sleep 0.05
            done
            dt_debug "Fuzzy: Exact match flag found"
            # ğŸ¦† says â® check if exact match already won
            if [[ $(cat "$match_result_flag") == "exact" ]]; then 
              dt_debug "Exact match already handled execution. Fuzzy exiting."             
              exit 0
            fi    
            dt_debug "Fuzzy: Proceeding with fuzzy execution..."
            # ğŸ¦† says â® final product - hope u like say duck!
            paramz="''${args[@]}" && echo
            echo "   â”Œâ”€(yo-$matched_script)"
            echo "   â”‚ğŸ¦† Fuzzy"
            if [ ''${#args[@]} -eq 0 ]; then
              echo "   â””â”€ğŸ¦† says â® no parameters yo"
            else
              for ((i=0; i<''${#args[@]}; i+=2)); do
                if [ $i -eq 0 ]; then
                  echo -n "   â””â”€â® "
                else
                  echo -n "   â””â”€â® "
                fi
                echo -n "''${args[$i]}"
                if [ $((i+1)) -lt ''${#args[@]} ]; then
                  echo " ''${args[$((i+1))]}"
                else
                  echo
                fi
              done
            fi
            dt_info "Executing: yo $matched_script $paramz" 
            # ğŸ¦† says â® EXECUTEEEEEEEAAA  â€“ HERE WE QUAAAAACKAAAOAA
            exec "yo-$matched_script" "''${args[@]}"
            return 0
          fi
        }        

        # ğŸ¦† says â® if exact match winz, no need for fuzz! but fuzz ready to quack when regex chokes
        exact_match_handler &
        pid1=$!
        fuzzy_match_handler
#        pid1=$!
        # ğŸ¦† says â® if this is reached - we have NO MATCH
        if [[ $(cat "$match_result_flag") == "exact_finished" ]]; then
          say_no_match
        fi
        exit
      ''; # ğŸ¦† says â® thnx for quackin' along til da end!
    }; 
  };
  # ğŸ¦† says â® SAFETY FIRST! 
  assertions = [ 
    {
      assertion = assertionCheckForConflictingSentences.assertion;
      message = assertionCheckForConflictingSentences.message;
    } # ğŸ¦† says â® the duck be stateless, the regex be law, and da shell... is my pond.    
  ];}# ğŸ¦† say â® nobody beat diz nlp nao says sir quack a lot NOBODY I SAY!
# ğŸ¦† says â® QuackHack-McBLindy out!  
