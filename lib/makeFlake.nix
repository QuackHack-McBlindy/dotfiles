# dotfiles/lib/makeFlake.nix â®ž https://github.com/quackhack-mcblindy/dotfiles
{ # ðŸ¦† duck say â®ž dis iz pure tool buildin' stuffz yo
  self,
  lib,
  dirMap,
  inputs
} : let # ðŸ¦† duck say â®ž label for clarity
  makeApp = program: {
    inherit program;
    type = "app";
  };  # ðŸ¦† duck say â®ž big thing dat make flakes small
  makeFlakeInternal = { # ðŸ¦† duck say â®ž give it - and you shall receive!
    systems, 
    hosts ? {}, 
    modules ? [], 
    overlays ? [], 
    packages ? {}, 
    apps ? {}, 
    devShells ? {}, 
    ... 
  } @ flake: # ðŸ¦† duck say â®ž thx
    let
      # ðŸ¦† duck say â®ž first we load all da machines by mapping hosts directory
      hosts = dirMap.mapHosts ../hosts;        
      
      # ðŸ¦† duck say â®ž helper dat init nixpkgs with system and overlays - allowing unfree
      makePkgs = system: pkgs: overlays: import pkgs {
        inherit system overlays;
        config.allowUnfree = true;
      };
      
      # ðŸ¦† duck say â®ž builds nixosConfiguration for each host
      nixosConfigurations = lib.mapAttrs (hostName: hostConfig:
        inputs.nixpkgs.lib.nixosSystem {
          system = hostName;
          specialArgs = {
            inherit self inputs;
            inherit hostName;
            nixosConfigurations = self.nixosConfigurations;
            finalSelf = self // { # ðŸ¦† duck say â®ž merge in extra info per host
              hostDir = ../hosts/${hostName};
              modules = lib.filterAttrs (_: v: v ? nixosModules) inputs;
            };
          };
          modules = [
            inputs.sops-nix.nixosModules.sops # ðŸ¦† duck say â®ž secret keepin'
            {
              nixpkgs.overlays = overlays; 
            }
            ../. # ðŸ¦† duck say â®ž loads ../default.nix
            hostConfig             
            ../modules/home.nix # ðŸ¦† duck say â®ž home is where your duck's at
          ];
        }) (dirMap.mapHosts ../hosts);
        
      # ðŸ¦† duck say â®ž for each system build packages, apps & devShells
      perSystem = system: let # ðŸ¦† duck say â®ž init dis system with nixpkgs & overlays
        pkgs = makePkgs system inputs.nixpkgs flake.overlays; 
      in {
        # ðŸ¦† duck say â®ž build packages calling nixpkgs.callPackage on each package
        packages = lib.mapAttrs (_: v: 
          (makePkgs system inputs.nixpkgs flake.overlays).callPackage v {
            inherit self;
            lib = inputs.nixpkgs.lib.extend (final: prev: {
              # ðŸ¦† duck say â®ž addd custom lib extensions here yo
            });
          }
        ) packages;
        
        # ðŸ¦† duck say â®ž apply makeApp to da apps
        apps = lib.mapAttrs (_: v:
          let
            pkgs = makePkgs system inputs.nixpkgs flake.overlays;
          in
            makeApp (v { inherit pkgs system self inputs; })  # ðŸ¦† duck say â®ž pass additional args
        ) apps;
        # ðŸ¦† duck say â®ž build devShells for dis system
        devShells = lib.mapAttrs (name: v:
          let
            shellArgs = v { 
              inherit pkgs system self inputs;
            };
            # ðŸ¦† duck say â®ž sanitize arguments for mkShell
            sanitizedArgs = builtins.removeAttrs shellArgs [
              "override"
              "overrideDerivation"
              "__functionArgs"
              "__functor"
            ];
          in
            pkgs.mkShell (sanitizedArgs // {
              # ðŸ¦† duck say â®ž for the love of flake...
              NIX_CONFIG = "extra-experimental-features = nix-command flakes";
              shellHook = ''
                echo "Entering ${name} dev shell"
                ${shellArgs.shellHook or ""}
              '';
            })
        ) devShells;
      };      
    in { 
      # ðŸ¦† duck say â®ž export nixosConfigurations to Nix
      inherit nixosConfigurations;
      
      # ðŸ¦† duck say â®ž build per system packages, apps & devShells attributes  
      packages = lib.genAttrs systems (system:
        (perSystem system).packages
      );
      apps = lib.genAttrs systems (system: (perSystem system).apps);
      devShells = lib.genAttrs systems (system: (perSystem system).devShells);
      # ðŸ¦† duck say â®ž show overlays in nix flake show
      overlays = lib.mapAttrs'
        (name: _: lib.nameValuePair (lib.removeSuffix ".nix" name)
          (import (../overlays + "/${name}") { inherit lib; }))
        (lib.filterAttrs (name: type: lib.hasSuffix ".nix" name)
          (builtins.readDir ../overlays));
    };
in { # ðŸ¦† duck say â®ž expose makeApp & makeFlake for use in flake
  inherit makeApp;
  makeFlake = args: makeFlakeInternal args;  
  } # ðŸ¦† duck say â®ž da end

