# dotfiles/modules/yo.nix ‚Æû https://github.com/quackhack-mcblindy/dotfiles
{ # ü¶Ü duck say ‚Æû CLI framework - centralized script handling
  self, 
  config,
  lib,
  pkgs,   
  ...
} : with lib;
let # ü¶Ü says ‚Æû grabbin‚Äô all da scripts for ez listin'  
  scripts = config.yo.scripts; 
  scriptNames = builtins.attrNames scripts; # ü¶Ü says ‚Æû just names - we never name one
  # ü¶Ü says ‚Æû only scripts with known intentions
  scriptNamesWithIntents = builtins.filter (scriptName:
    let # ü¶Ü says ‚Æû a intent iz kinda ..
      intent = generatedIntents.${scriptName};
      # ü¶Ü says ‚Æû .. pointless if it haz no sentence data ..
      hasSentences = builtins.any (data: data ? sentences && data.sentences != []) intent.data;
    in # ü¶Ü says ‚Æû .. so datz how we build da scriptz!
      builtins.hasAttr scriptName generatedIntents && hasSentences
  ) (builtins.attrNames scriptsWithVoice); # ü¶Ü says ‚Æû datz quackin' cool huh?!

  # ü¶Ü says ‚Æû only scripts with voice enabled and non-null voice config
  scriptsWithVoice = lib.filterAttrs (_: script: 
    script.voice != null && (script.voice.enabled or true)
  ) config.yo.scripts;
  
  # ü¶Ü says ‚Æû generate intents
  generatedIntents = lib.mapAttrs (name: script: {
    priority = script.voice.priority or 3;
    data = [{
      inherit (script.voice) sentences lists;
    }];
  }) scriptsWithVoice;

  fuzzyFlatIndex = lib.flatten (lib.mapAttrsToList (scriptName: intent:
    lib.concatMap (data:
      lib.concatMap (sentence:
        map (expanded: {
          script = scriptName;
          sentence = expanded;
          signature = let
            words = lib.splitString " " (lib.toLower expanded);
            sorted = lib.sort (a: b: a < b) words;
          in builtins.concatStringsSep "|" sorted;
        }) (expandOptionalWords sentence)
      ) data.sentences
    ) intent.data
  ) (lib.mapAttrs (name: script: {
    priority = script.voice.priority or 3;
    data = [{
      inherit (script.voice) sentences lists;
    }];
  }) scriptsWithFuzzy));


  # ü¶Ü says ‚Æû QUACK! da duck take a list of listz and duck make all da possible combinationz
  cartesianProductOfLists = lists:
    # ü¶Ü says ‚Æû if da listz iz empty .. 
    if lists == [] then
      [ [] ] # ü¶Ü says ‚Æû .. i gib u empty listz of listz yo got it?
    else # ü¶Ü says ‚Æû ELSE WAT?!
      let # ü¶Ü says ‚Æû sorry.. i gib u first list here u go yo
        head = builtins.head lists;
        # ü¶Ü says ‚Æû remaining listz for u here u go bro!
        tail = builtins.tail lists;
        # ü¶Ü says ‚Æû calculate combinations for my tail - yo calc wher u at?!
        tailProduct = cartesianProductOfLists tail;
      in # ü¶Ü says ‚Æû for everyy x in da listz ..
        lib.concatMap (x:
          # ü¶Ü says ‚Æû .. letz combinez wit every tail combinationz ..  
          map (y: [x] ++ y) tailProduct
        ) head; # ü¶Ü says ‚Æû dang! datz a DUCK COMBO alright!  
# ü¶Ü EXAMPLE ‚Æû cartesianProductOfLists [ ["a" "b"] ["1" "2"] ["x" "y"] ]
# ü¶Ü BOOOOOM ‚Æü 
#  [ ["a" "1" "x"]
#    ["a" "1" "y"] 
#    ["a" "2" "x"]
#    ["a" "2" "y"]
#    ["b" "1" "x"]
#    ["b" "1" "y"]
#    ["b" "2" "x"]
#    ["b" "2" "y"] ]
         
  # ü¶Ü says ‚Æû here i duckie help yo out! makin' yo life eazy sleazy' wen declarative sentence yo typin'    
  expandOptionalWords = sentence: # ü¶Ü says ‚Æû qucik & simple sentences we quacky & hacky expandin'
    let # ü¶Ü says ‚Æû CHOP CHOP! Rest in lil' Pieceez bigg sentence!!1     
      tokens = lib.splitString " " sentence;      
      # ü¶Ü says ‚Æû definin' dem wordz in da (braces) taggin' dem' wordz az (ALTERNATIVES) lettin' u choose one of dem wen triggerin' 
      isRequiredGroup = t: lib.hasPrefix "(" t && lib.hasSuffix ")" t;
      # ü¶Ü says ‚Æû puttin' sentence wordz in da [bracket] makin' em' [OPTIONAL] when bitchin' u don't have to be pickin' woooho 
      isOptionalGroup = t: lib.hasPrefix "[" t && lib.hasSuffix "]" t;   
      expandToken = token: # ü¶Ü says ‚Æû dis gets all da real wordz out of one token (yo!)
        if isRequiredGroup token then
          let # ü¶Ü says ‚Æû thnx 4 lettin' ducklin' be cleanin' - i'll be removin' dem "()" 
            clean = lib.removePrefix "(" (lib.removeSuffix ")" token);
            alternatives = lib.splitString "|" clean; # ü¶Ü says ‚Æû use "|" to split (alternative|wordz) yo 
          in  # ü¶Ü says ‚Æû dat's dat 4 dem alternativez
            alternatives
        else if isOptionalGroup token then
          let # ü¶Ü says ‚Æû here we be goin' again - u dirty and i'll be cleanin' dem "[]"
            clean = lib.removePrefix "[" (lib.removeSuffix "]" token);
            alternatives = lib.splitString "|" clean; # ü¶Ü says ‚Æû i'll be stealin' dat "|" from u 
          in # ü¶Ü says ‚Æû u know wat? optional means we include blank too!
            alternatives ++ [ "" ]
        else # ü¶Ü says ‚Æû else i be returnin' raw token for yo
          [ token ];      
      # ü¶Ü says ‚Æû now i gib u generatin' all dem combinationz yo
      expanded = cartesianProductOfLists (map expandToken tokens);      
      # ü¶Ü says ‚Æû clean up if too much space, smush back into stringz for ya
      trimmedVariants = map (tokenList:
        let # ü¶Ü says ‚Æû join with spaces then trim them suckers
          raw = lib.concatStringsSep " " tokenList;
          # ü¶Ü says ‚Æû remove ALL extra spaces
          cleaned = lib.replaceStrings ["  "] [" "] (lib.strings.trim raw);
        in # ü¶Ü says ‚Æû wow now they be shinin'
          cleaned 
      ) expanded; # ü¶Ü says ‚Æû and they be multiplyyin'!      
      # ü¶Ü says ‚Æû throwin' out da empty and cursed ones yo
      nonEmpty = lib.filter (s: s != "") trimmedVariants;
      hasFixedText = v: builtins.match ".*[^\\{].*" v != null; # ü¶Ü says ‚Æû no no no, no nullin'
      validVariants = lib.filter hasFixedText nonEmpty;
    in # ü¶Ü says ‚Æû returnin' all unique variantz of da sentences ‚Äì holy duck dat'z fresh 
      lib.unique validVariants;
  
  # ü¶Ü says ‚Æû we be doin' sorta da same wit dem listz
  expandListInputVariants = value: 
    let # ü¶Ü says ‚Æû first we choppy choppy - break up da list into word tokenz
      tokens = lib.splitString " " value;
      # ü¶Ü says ‚Æû checkin' if a token be wrapped like [diz] = optional, ya feel?
      isOptional = t: lib.hasPrefix "[" t && lib.hasSuffix "]" t;
      # ü¶Ü says ‚Æû now ducklin' expandz each token ‚Äî either real or optional wit options
      expandToken = token:
        if isOptional token then
          let # ü¶Ü says ‚Æû time 2 clean dat square junk up 4 yo bro
            clean = lib.removePrefix "[" (lib.removeSuffix "]" token);
             # ü¶Ü says ‚Æû u know da drill - splittin' on da "|" to find alt optionalz
            alternatives = lib.splitString "|" clean;
          in
            alternatives
        else # ü¶Ü says ‚Æû not optional? just be givin' back da token as iz
          [ token ];
      expanded = cartesianProductOfLists (map expandToken tokens);
      variants = map (tokenList:
        lib.replaceStrings [ "  " ] [ " " ] (lib.concatStringsSep " " tokenList)
      ) expanded;  # ü¶Ü says ‚Æû only da fresh unique non-emptiez stayin‚Äô in da pond
    in lib.unique (lib.filter (s: s != "") variants);

  # ü¶Ü says ‚Æû optimized pattern expansion
  expandToRegex = sentence: data:
    let
      # ü¶Ü says ‚Æû helper function to convert patterns to regex
      convertPattern = token:
        if lib.hasPrefix "(" token then
          let
            clean = lib.removePrefix "(" (lib.removeSuffix ")" token);
            alternatives = lib.splitString "|" clean;
            escaped = map lib.escapeRegex alternatives;
          in "(?:" + lib.concatStringsSep "|" escaped + ")"
        else if lib.hasPrefix "[" token then
          let
            clean = lib.removePrefix "[" (lib.removeSuffix "]" token);
            alternatives = lib.splitString "|" clean;
            escaped = map lib.escapeRegex alternatives;
          in "(?:" + lib.concatStringsSep "|" escaped + ")?"
        else
          lib.escapeRegex token;
      
      # ü¶Ü says ‚Æû split into tokens while preserving special groups
      tokenize = s:
        let
          groups = builtins.match "([^{]*)(\{[^}]*\})?(.*)" s;
        in
          if groups == null then [s]
          else let
            prefix = builtins.elemAt groups 0;
            param = builtins.elemAt groups 1;
            rest = builtins.elemAt groups 2;
            tokens = if prefix != "" then [prefix] else [];
            tokensWithParam = if param != null then tokens ++ [param] else tokens;
          in tokensWithParam ++ tokenize rest;
      
      # ü¶Ü says ‚Æû process tokens into regex parts
      tokens = tokenize sentence;
      regexParts = map (token:
        if lib.hasPrefix "{" token then
          let
            param = lib.removePrefix "{" (lib.removeSuffix "}" token);
            isWildcard = data.lists.${param}.wildcard or false;
          in if isWildcard then "(.*)" else "\\b([^ ]+)\\b"
        else
          convertPattern token
      ) tokens;
      
      # ü¶Ü says ‚Æû combine parts into final regex
      regex = "^" + lib.concatStrings regexParts + "$";
    in
      regex; 

  # ü¶Ü says ‚Æû take each value like "yo|hey" and map it to its 'out' ‚Äì buildin‚Äô da translation matrix yo!
  makeEntityResolver = data: listName: # ü¶Ü says ‚Æû i like ducks
    lib.concatMapStrings (entity:
      let 
        variants = expandListInputVariants entity."in"; # ü¶Ü says ‚Æû "in" must always be quoted in Nix. never forget yo
      in # ü¶Ü says ‚Æû otherwize itz an in like this one!
        lib.concatMapStrings (variant: ''
          "${variant}") echo "${entity.out}";;
        '') variants # ü¶Ü says ‚Æû all of them yo!
    ) data.lists.${listName}.values; # ü¶Ü says ‚Æû maps each "in" value to an echo of its "out"
  
  # ü¶Ü says ‚Æû where da magic dynamic regex iz at 
  makePatternMatcher = scriptName: let
    dataList = generatedIntents.${scriptName}.data;    
  in '' # ü¶Ü says ‚Æû diz iz how i pick da script u want 
    match_${scriptName}() { # ü¶Ü says ‚Æû shushin' da caps ‚Äì lowercase life 4 cleaner dyn regex zen ‚ú®
      local input="$(echo "$1" | tr '[:upper:]' '[:lower:]')" 
      # ü¶Ü says ‚Æû always show input in debug mode
      # ü¶Ü says ‚Æû watch the fancy stuff live in action  
      dt_debug "Trying to match for script: ${scriptName}" >&2
      dt_debug "Input: $input" >&2
      # ü¶Ü says ‚Æû duck presentin' - da madnezz 
      ${lib.concatMapStrings (data:
        lib.concatMapStrings (sentence:
          lib.concatMapStrings (sentenceText: let
            # ü¶Ü says ‚Æû now sentenceText is one of the expanded variants!
            parts = lib.splitString "{" sentenceText; # ü¶Ü says ‚Æû diggin' out da goodies from curly nests! Gimme dem {param} nuggets! 
            firstPart = lib.escapeRegex (lib.elemAt parts 0); # ü¶Ü says ‚Æû gotta escape them weird chars 
            restParts = lib.drop 1 parts;  # ü¶Ü says ‚Æû now we in the variable zone quack?  
            # ü¶Ü says ‚Æû process each part to build regex and params
            regexParts = lib.imap (i: part:
              let
                split = lib.splitString "}" part; # ü¶Ü says ‚Æû yeah yeah curly close that syntax shell
                param = lib.elemAt split 0; # ü¶Ü says ‚Æû name of the param in da curly ‚Äì ex: {user}
                after = lib.concatStrings (lib.tail split); # ü¶Ü says ‚Æû anything after the param in this chunk
                # ü¶Ü says ‚Æû Wildcard mode! anything goes - duck catches ALL the worms! (.*)
                isWildcard = data.lists.${param}.wildcard or false;
                regexGroup = if isWildcard then "(.*)" else "\\b([^ ]+)\\b"; # 82%
                # regexGroup = if isWildcard then "(.*)" else "([^ ]+)";
                # ü¶Ü says ‚Æû ^ da regex that gon match actual input text
              in {
                regex = regexGroup + lib.escapeRegex after;
                param = param;
              }
            ) restParts;

            fullRegex = let
              clean = lib.strings.trim (firstPart + lib.concatStrings (map (v: v.regex) regexParts));
            in "^${clean}$"; # ü¶Ü says ‚Æû mash all regex bits 2gether
            paramList = map (v: v.param) regexParts; # ü¶Ü says ‚Æû the squad of parameters 
          in ''
            local regex='^${fullRegex}$'
            dt_debug "REGEX: $regex"
            if [[ "$input" =~ $regex ]]; then  # ü¶Ü says ‚Æû DANG DANG ‚Äì regex match engaged 
              ${lib.concatImapStrings (i: paramName: ''
                # ü¶Ü says ‚Æû extract match group #i+1 ‚Äì param value, come here plz 
                param_value="''${BASH_REMATCH[${toString (i+1)}]}"
                # ü¶Ü says ‚Æû if param got synonym, apply the duckfilter 
                if [[ -n "''${param_value:-}" && -v substitutions["$param_value"] ]]; then
                  subbed="''${substitutions["$param_value"]}"
                  if [[ -n "$subbed" ]]; then
                    param_value="$subbed"
                  fi
                fi           
                ${lib.optionalString (
                  data.lists ? ${paramName} && !(data.lists.${paramName}.wildcard or false)
                ) ''
                  # ü¶Ü says ‚Æû apply substitutions before case matchin'
                  if [[ -v substitutions["$param_value"] ]]; then
                    param_value="''${substitutions["$param_value"]}"
                  fi
                  case "$param_value" in
                    ${makeEntityResolver data paramName}
                    *) ;;
                  esac
                ''} # ü¶Ü says ‚Æû declare global param ‚Äì duck want it everywhere! (for bash access)
                declare -g "_param_${paramName}"="$param_value"            
                declare -A params=()
                params["${paramName}"]="$param_value"
                matched_params+=("$paramName")
              '') paramList} # ü¶Ü says ‚Æû set dat param as a GLOBAL VAR yo! every duck gotta know 
              # ü¶Ü says ‚Æû build cmd args: --param valu
              cmd_args=()
              ${lib.concatImapStrings (i: paramName: ''
                value="''${BASH_REMATCH[${toString i}]}"
                cmd_args+=(--${paramName} "$value")
              '') paramList}
              dt_debug "REMATCH 1: ''${BASH_REMATCH[1]}"
              dt_debug "REMATCH 2: ''${BASH_REMATCH[2]}"
              dt_debug "REMATCH 3: ''${BASH_REMATCH[3]}"
              dt_debug "MATCHED SCRIPT: ${scriptName}"
              dt_debug "ARGS: ''${cmd_args[@]}"
              return 0
            fi
          '') (expandOptionalWords sentence)
        ) data.sentences
      ) dataList}
      return 1
    }
  ''; # ü¶Ü says ‚Æû dat was fun! let'z do it again some time

  # ü¶Ü says ‚Æû quack and scan, match bagan
  makeFuzzyPatternMatcher = scriptName: let
    dataList = generatedIntents.${scriptName}.data;
  in '' # ü¶Ü says ‚Æû fuzz in code, waddle mode
    match_fuzzy_${scriptName}() {
      local input="$(echo "$1" | tr '[:upper:]' '[:lower:]')"
      local matched_sentence="$2"
      # ü¶Ü says ‚Æû skip regex! dat shit iz crazy - use aligned wordz yo
      declare -A params=()
      local input_words=($input)
      local sentence_words=($matched_sentence)     
      # ü¶Ü says ‚Æû extract params by aligning words cool huh
      for i in ''${!sentence_words[@]}; do
        local word="''${sentence_words[$i]}"
        if [[ "$word" == \{*\} ]]; then
          local param_name="''${word:1:-1}"
          params["$param_name"]="''${input_words[$i]}"
        fi
      done
      # ü¶Ü says ‚Æû apply subs to params yo
      for param in "''${!params[@]}"; do
        local value="''${params[$param]}"
        if [[ -v substitutions["$value"] ]]; then
          params["$param"]="''${substitutions["$value"]}"
        fi
      done
      # ü¶Ü says ‚Æû build da paramz
      cmd_args=()
      for param in "''${!params[@]}"; do
        cmd_args+=(--"$param" "''${params[$param]}")
      done
      return 0
    }
  '';
  
  # ü¶Ü says ‚Æû matcher to json yao
  matchers = lib.mapAttrsToList (scriptName: data:
    let
      matcherCode = makePatternMatcher scriptName;
    in {
      name = scriptName;
      value = pkgs.writeText "${scriptName}-matcher" matcherCode;
    }
  ) generatedIntents;

  # ü¶Ü says ‚Æû one shell script dat sourcez dem allz
  matcherSourceScript = pkgs.writeText "matcher-loader.sh" (
    lib.concatMapStringsSep "\n" (m: "source ${m.value}") matchers
  );

  # ü¶Ü says ‚Æû oh duck... dis is where speed goes steroids yo iz diz cachin'?
  intentDataFile = pkgs.writeText "intent-entity-map4.json"
    (builtins.toJSON (
      lib.mapAttrs (_scriptName: intentList:
        let
          allData = lib.flatten (map (d: d.lists or {}) intentList.data);
          # ü¶Ü says ‚Æû collect all sentences for diz intent
          sentences = lib.concatMap (d: d.sentences or []) intentList.data;      
          # ü¶Ü says ‚Æû expand all sentence variants
          expandedSentences = lib.unique (lib.concatMap expandOptionalWords sentences);
          # ü¶Ü says ‚Æû "in" > "out" for dem' subz 
          substitutions = lib.flatten (map (lists: 
            lib.flatten (lib.mapAttrsToList (_listName: listData: 
              if listData ? values then
                lib.flatten (map (item: 
                  let
                    rawIn = item."in";
                    value = item.out;
                    cleaned = lib.removePrefix "[" (lib.removeSuffix "]" rawIn);
                    variants = lib.splitString "|" cleaned;     
                  in map (v: let
                    cleanV = lib.replaceStrings ["  "] [" "] (lib.strings.trim v);
                  in {   
                    pattern = if builtins.match ".* .*" cleanV != null
                              then cleanV
                              else "(${cleanV})";
                    value = value;
                  }) variants
                ) listData.values)
              else []
            ) lists)
          ) allData);
          # ü¶Ü says ‚Æû CRITICAL: Include the lists data for wildcard detection
          lists = lib.foldl (acc: d: acc // (d.lists or {})) {} intentList.data;
        in {
          inherit substitutions;
          inherit sentences;
          inherit lists;
        }
      ) generatedIntents
    ));


  # ü¶Ü says ‚Æû quack! now we preslicin' dem sentences wit their fuzzynutty signatures for bitchin' fast fuzz-lookup!
  fuzzyIndex = lib.mapAttrsToList (scriptName: intent:
    lib.concatMap (data: # ü¶Ü says ‚Æû dive into each intent entryz like itz bread crumbs
      lib.concatMap (sentence: # ü¶Ü says ‚Æû grab all dem raw sentence templates
        map (expanded: { # ü¶Ü says ‚Æû ayy, time to expand theze feathers
          script = scriptName; # ü¶Ü says ‚Æû label diz bird wit itz intent script yo
          sentence = expanded; # ü¶Ü says ‚Æû this da expanded sentence duck gon' match against
          # ü¶Ü says ‚Æû precompute signature for FAAASTEERRr matching - quicky quacky snappy matchin' yo! 
          signature = let
            words = lib.splitString " " (lib.toLower expanded); # ü¶Ü says ‚Æû lowercase & split likez stale rye
            sorted = lib.sort (a: b: lib.hasPrefix a b) words; # ü¶Ü says ‚Æû duck sort dem quackz alphabetically-ish quack quack
          in builtins.concatStringsSep "|" sorted;  # ü¶Ü says ‚Æû make a fuzzy-flyin‚Äô signature string, pipe separated - yo' know it 
        }) (expandOptionalWords sentence) # ü¶Ü says ‚Æû diz iz where optional wordz becomez reality
      ) data.sentences # ü¶Ü says ‚Æû waddlin' through all yo' sentencez
    ) intent.data # ü¶Ü says ‚Æû scoopin' from every intentz
  ) generatedIntents; # ü¶Ü says ‚Æû diz da sacred duck scripture ‚Äî all yo' intents livez here boom  

  # ü¶Ü says ‚Æû fuzzy index only for allowed yo scriptz dat allow dem fuzzy matchin' yo
  scriptsWithFuzzy = lib.filterAttrs (_: script: 
    script.voice != null && 
    (script.voice.enabled or true) &&
    (script.voice.fuzzy.enable or true)  # ü¶Ü Must explicitly allow fuzzy
  ) config.yo.scripts;

  splitWordsFile = pkgs.writeText "split-words.json" (builtins.toJSON config.yo.SplitWords);
  sorryPhrasesFile = pkgs.writeText "sorry-phrases.json" (builtins.toJSON config.yo.sorryPhrases);
  fuzzyIndexFile = pkgs.writeText "fuzzy-index.json" (builtins.toJSON fuzzyIndex);
  fuzzyIndexFlatFile = pkgs.writeText "fuzzy-rust-index.json" (builtins.toJSON fuzzyFlatIndex);  
  matcherDir = pkgs.linkFarm "yo-matchers" (
    map (m: { name = "${m.name}.sh"; path = m.value; }) matchers
  ); 


  # ü¶Ü says ‚Æû export da nix store path to da intent data - could be useful yo
  environment.variables.YO_SPLIT_WORDS = splitWordsFile;
  environment.variables.YO_SORRY_PHRASES = sorryPhrasesFile;
  environment.variables.YO_INTENT_DATA = intentDataFile;
  environment.variables."·ª≤O_FUZZY_INDEX" = fuzzyIndexFile;  
  environment.variables.MATCHER_DIR = matcherDir;
  environment.variables.MATCHER_SOURCE = matcherSourceScript;

  environment.etc = {
    "split-words.json".source = splitWordsFile;
    "sorry-phrases.json".source = sorryPhrasesFile;
    "yo/intent-data.json".source = intentDataFile;
    "yo/fuzzy-index.json".source = fuzzyIndexFile;
    "yo/matchers" = {
      source = matcherDir;
    };
    "yo/matcher-loader.sh".source = matcherSourceScript;
  };
  
  file."split-words.json" = splitWordsFile;
  
  # ü¶Ü says ‚Æû priority system 4 runtime optimization
  scriptRecordsWithIntents = 
    let # ü¶Ü says ‚Æû calculate priority
      calculatePriority = scriptName:
        generatedIntents.${scriptName}.priority or 3; # Default medium

      # ü¶Ü says ‚Æû create script records metadata
      makeRecord = scriptName: rec {
        name = scriptName;
        priority = calculatePriority scriptName;
        hasComplexPatterns = 
          let 
            intent = generatedIntents.${scriptName};
            patterns = lib.concatMap (d: d.sentences) intent.data;
          in builtins.any (p: lib.hasInfix "{" p || lib.hasInfix "[" p) patterns;
      };    
    in lib.sort (a: b:
        # ü¶Ü says ‚Æû primary sort: lower number = higher priority
        a.priority < b.priority 
        # ü¶Ü says ‚Æû secondary sort: simple patterns before complex ones
        || (a.priority == b.priority && !a.hasComplexPatterns && b.hasComplexPatterns)
        # ü¶Ü says ‚Æû third sort: alphabetical for determinism
        || (a.priority == b.priority && a.hasComplexPatterns == b.hasComplexPatterns && a.name < b.name)
      ) (map makeRecord scriptNamesWithIntents);
  # ü¶Ü says ‚Æû generate optimized processing order
  processingOrder = map (r: r.name) scriptRecordsWithIntents;

  # ü¶Ü duck say ‚Æû quacky hacky helper 2 escape md special charizardz yo
  escapeMD = str: let
    replacements = [
      [ "\\" "\\\\" ]
      [ "*" "\\*" ]
      [ "`" "\\`" ]
      [ "_" "\\_" ]
      [ "[" "\\[" ]
      [ "]" "\\]" ]
    ];
  in
    lib.foldl (acc: r: lib.replaceStrings [ (builtins.elemAt r 0) ] [ (builtins.elemAt r 1) ] acc) str replacements;
 
 
  # ü¶Ü says ‚Æû conflict detection - no bad voice intentz quack!  
  assertionCheckForConflictingSentences = let
    # ü¶Ü says ‚Æû collect all expanded sentences with their script originz
    allExpandedSentences = lib.flatten (lib.mapAttrsToList (scriptName: intent:
      lib.concatMap (data:
        lib.concatMap (sentence:
          map (expanded: {
            inherit scriptName;
            sentence = expanded;
            original = sentence;
            # ü¶Ü says ‚Æû extract parameter positionz & count da fixed words
            hasWildcardAtEnd = lib.hasSuffix " {search}" (lib.toLower expanded) || 
                              lib.hasSuffix " {param}" (lib.toLower expanded) ||
                              (lib.hasInfix " {" expanded && 
                               !(lib.hasInfix "} " expanded)); # ü¶Ü says ‚Æû wildcard at end if no } followed by space
            fixedWordCount = let
              words = lib.splitString " " expanded;
              nonParamWords = lib.filter (word: 
                !(lib.hasPrefix "{" word) && !(lib.hasSuffix "}" word)
              ) words;
            in lib.length nonParamWords;
          }) (expandOptionalWords sentence)
        ) data.sentences
      ) intent.data
    ) generatedIntents);
    # ü¶Ü says ‚Æû check for prefix conflictz
    checkPrefixConflicts = sentences:
      let
        sortedSentences = lib.sort (a: b: 
          lib.stringLength a.sentence < lib.stringLength b.sentence
        ) sentences;
        conflicts = lib.foldl (acc: shorterItem:
          let
            shorter = shorterItem.sentence;
            shorterScript = shorterItem.scriptName;
            shorterHasWildcard = shorterItem.hasWildcardAtEnd;
          in
            acc ++ (lib.foldl (innerAcc: longerItem:
              let
                longer = longerItem.sentence;
                longerScript = longerItem.scriptName;
              in
                if shorterScript != longerScript then
                  if lib.hasPrefix (shorter + " ") longer && shorterHasWildcard then
                    innerAcc ++ [{
                      type = "PREFIX_CONFLICT";
                      shorter = shorter;
                      longer = longer;
                      scripts = [shorterScript longerScript];
                      reason = "Shorter pattern '${shorter}' (ends with wildcard) is a prefix of '${longer}'";
                    }]
                  else
                    innerAcc
                else
                  innerAcc
            ) [] sortedSentences)
        ) [] sortedSentences;
      in
        conflicts;
    # ü¶Ü says ‚Æû find prefix conflictz!
    sentencesByText = lib.groupBy (item: item.sentence) allExpandedSentences;
    exactConflicts = lib.filterAttrs (sentence: items:
      let 
        uniqueScripts = lib.unique (map (item: item.scriptName) items);
      in 
        lib.length uniqueScripts > 1
    ) sentencesByText; 
    # ü¶Ü says ‚Æû find duplicatez!
    exactConflictList = lib.mapAttrsToList (sentence: items:
      let
        scripts = lib.unique (map (item: item.scriptName) items);
      in { # ü¶Ü  says ‚Æû format exact conflictz dawg
        type = "EXACT_CONFLICT";
        sentence = sentence;
        scripts = scripts;
        reason = "Exact pattern match in scripts: ${lib.concatStringsSep ", " scripts}";
      }
    ) exactConflicts;   
    # ü¶Ü  says ‚Æû find prefix conflictz
    prefixConflicts = checkPrefixConflicts allExpandedSentences;    
    # ü¶Ü  says ‚Æû letz put dem conflictz together okay?
    allConflicts = exactConflictList ++ prefixConflicts;
    hasConflicts = allConflicts != [];    
    # ü¶Ü  says ‚Æû find da prefix conflictz  
  in {
    assertion = !hasConflicts;
    message = 
      if hasConflicts then
        let
          conflictMsgs = map (conflict:
            if conflict.type == "EXACT_CONFLICT" then
              ''
              ü¶Ü says ‚Æû CONFLICT! 
                Pattern "${conflict.sentence}"
                In scripts: ${lib.concatStringsSep ", " conflict.scripts}
              ''
            else if conflict.type == "PREFIX_CONFLICT" then
              ''
              ü¶Ü says ‚Æû CONFLICT!
                Shorter: "${conflict.shorter}" (ends with wildcard)
                Longer:  "${conflict.longer}"
                Scripts: ${lib.concatStringsSep ", " conflict.scripts}
                Reason:  ${conflict.reason}
              ''
            else
              ""
          ) allConflicts;
        in
          "Sentence conflicts detected in voice definition:\n\n" +
          lib.concatStringsSep "\n" conflictMsgs +
          "\n\nü¶Ü says ‚Æû fix da conflicts before rebuildin' yo!"
      else
        "No sentence conflicts found.";
  };

  # ü¶Ü says ‚Æû category based helper with actual names instead of {param}
  voiceSentencesHelpFile = pkgs.writeText "voice-sentences-help.md" (
    let
      scriptsWithVoice = lib.filterAttrs (_: script: 
        script.voice != null && script.voice.sentences != [] && (script.voice.enabled or true)
      ) config.yo.scripts;
      
      # ü¶Ü says ‚Æû replace {param} with actual values from voice lists
      replaceParamsWithValues = sentence: voiceData:
        let
          # ü¶Ü says ‚Æû find all {param} placeholders in the sentence
          paramMatches = builtins.match ".*(\\{([^}]+)\\}).*" sentence;
          processToken = token:
            if lib.hasPrefix "{" token && lib.hasSuffix "}" token then
              let
                paramName = lib.removePrefix "{" (lib.removeSuffix "}" token);
                listData = voiceData.lists.${paramName} or null;
              in
                if listData != null then
                  if listData.wildcard or false then
                    "ANYTHING"
                  else
                    let
                      # ü¶Ü says ‚Æû get all possible input values
                      values = map (v: v."in") listData.values;
                      # ü¶Ü says ‚Æû expand any optional patterns like [foo|bar]
                      expandedValues = lib.concatMap expandListInputVariants values;
                      # ü¶Ü says ‚Æû take first few examples for display
                      examples = lib.take 3 (lib.unique expandedValues);
                    in
                      if examples == [] then "ANYTHING"
                      else "(" + lib.concatStringsSep "|" examples + 
                           (if lib.length examples < lib.length expandedValues then "|...)" else ")")
                else
                  "ANYTHING" # ü¶Ü says ‚Æû fallback if param not found
            else
              token;
          
          # ü¶Ü says ‚Æû split sentence and process each token
          tokens = lib.splitString " " sentence;
          processedTokens = map processToken tokens;
        in
          lib.concatStringsSep " " processedTokens;
      
      # ü¶Ü says ‚Æû group by category
      groupedScripts = lib.groupBy (script: script.category or "üß© Miscellaneous") 
        (lib.attrValues scriptsWithVoice);
      
      # ü¶Ü says ‚Æû generate category sections with param replacement
      categorySections = lib.mapAttrsToList (category: scripts:
        let
          scriptLines = map (script:
            let
              # ü¶Ü says ‚Æû replace params in each sentence
              sentenceLines = lib.concatMapStrings (sentence: 
                let processedSentence = replaceParamsWithValues sentence script.voice;
                in "    - \"${escapeMD processedSentence}\"\n"
              ) script.voice.sentences;
            in
              "  **${escapeMD script.name}**:\n${sentenceLines}"
          ) (lib.sort (a: b: a.name < b.name) scripts);
        in
          "# ${category}\n\n${lib.concatStringsSep "\n" scriptLines}"
      ) groupedScripts;
      
      # ü¶Ü says ‚Æû statistics
      totalScripts = lib.length (lib.attrNames config.yo.scripts);
      voiceScripts = lib.length (lib.attrNames scriptsWithVoice);
      totalPatterns = config.yo.generatedPatterns;
      totalPhrases = config.yo.understandsPhrases;    
      stats = ''  
  # ----‚îÄ‚îÄ‚îÄ‚îÄ----‚îÄ‚îÄ‚ãÜ‚ãÖ‚òÜ‚òÜ‚òÜ‚ãÖ‚ãÜ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ----‚îÄ #
  # Total:  
  - **Scripts with voice enabled**: ${toString voiceScripts} / ${toString totalScripts}
  - **Generated patterns**: ${toString totalPatterns}
  - **Understandable phrases**: ${toString totalPhrases}
      '';
    in
      "# ü¶Ü Voice Commands\n√Ö\n\n${lib.concatStringsSep "\n\n" categorySections}\n\n${stats}"
  );


  # ü¶Ü says ‚Æû for README version badge yo
  nixosVersion = let
    raw = builtins.readFile /etc/os-release;
    versionMatch = builtins.match ".*VERSION_ID=([0-9\\.]+).*" raw;
  in builtins.replaceStrings [ "." ] [ "%2E" ] (builtins.elemAt versionMatch 0);

  sysHosts = builtins.attrNames self.nixosConfigurations;
  vmHosts = builtins.filter (host:
    self.nixosConfigurations.${host}.self.config.system.build ? vm
  ) sysHosts;  
  # ü¶Ü duck say ‚Æû comma sep list of your hosts
  sysHostsComma = builtins.concatStringsSep "," sysHosts;

  # ü¶Ü duck say ‚Æû validate time format - HH:MM (24h)
  isValidTime = timeStr:
    let
      matches = builtins.match "([0-9]{1,2}):([0-9]{2})" timeStr;
    in
      if matches != null then
        let
          hourStr = builtins.elemAt matches 0;
          minuteStr = builtins.elemAt matches 1;
          # ü¶Ü duck say ‚Æû remove leading zeros for JSON parsin'
          cleanNumber = str:
            if builtins.substring 0 1 str == "0" && builtins.stringLength str > 1
            then builtins.substring 1 (builtins.stringLength str) str
            else str;
          hour = builtins.fromJSON (cleanNumber hourStr);
          minute = builtins.fromJSON (cleanNumber minuteStr);
        in
          hour >= 0 && hour <= 23 && minute >= 0 && minute <= 59
      else false;
  
  # ü¶Ü duck say ‚Æû validate list of timez
  validateTimes = times:
    if times == null then null
    else
      let
        invalidTimes = lib.filter (time: !isValidTime time) times;
      in
        if invalidTimes != [] then
          throw "ü¶Ü duck say ‚Æû fuck ‚ùå Invalid time format in runAt: ${lib.concatStringsSep ", " invalidTimes}. Use HH:MM (24-hour format)"
        else times;

  # ü¶Ü duck say ‚Æû expoort param into shell script
  yoEnvGenVar = script: let
    withDefaults = builtins.filter (p: p.default != null) script.parameters;
    exports = map (p: 
      let # ü¶Ü duck say ‚Æû convert dem Nix types 2 shell strings
        defaultValue = 
          if p.type == "string" then lib.escapeShellArg (toString p.default)
          else if p.type == "int" then toString p.default
          else if p.type == "bool" then (if p.default then "true" else "false")
          else if p.type == "path" then lib.escapeShellArg (toString p.default)
          else lib.escapeShellArg (toString p.default);
      in
        "export ${p.name}=${defaultValue}"
    ) withDefaults;
  in lib.concatStringsSep "\n" exports;

  scriptType = types.submodule ({ name, configFinal, ... }: {   
# ü¶Ü ‚Æû OPTIONS ü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Ü#    
    options = { # ü¶Ü duck say ‚Æû a name cool'd be cool right?
      name = mkOption {
        type = types.str;
        internal = true;
        readOnly = true;
        default = name;
        description = "Script name derived from attribute key";
      }; # ü¶Ü duck say ‚Æû describe yo script yo!
      description = mkOption {
        type = types.str;
        default = "";
        description = "Description of the script";
      }; # ü¶Ü duck say > categoryiez da script (for sorting in `yo --help` & README.md    
      category = mkOption {
        type = types.str;
        default = "";
        description = "Category of the script";
      };
      filePath = mkOption {
        type = types.str;
        readOnly = true;
      };
      # ü¶Ü duck say ‚Æû yo go ahead describe da script yo     
      visibleInReadme = mkOption {
        type = types.bool;
        default = ./category != "";
        defaultText = "category != \"\"";
        description = "Whether to include this script in README.md";
      }; # ü¶Ü duck say ‚Æû duck trace log level
      logLevel = mkOption {
        type = types.enum ["DEBUG" "INFO" "WARNING" "ERROR" "CRITICAL"];
        default = "INFO";
        description = "Sets the log level for Duck Trace";
      }; # ü¶Ü duck say ‚Æû extra code to be ran & displayed whelp calling da scripts --help cmd  
      helpFooter = mkOption {
        type = types.lines;
        default = "";
        description = "Additional shell code to run when generating help text";
      }; # ü¶Ü duck say ‚Æû generatez systemd service for da script if true 
      autoStart = mkOption {
        type = types.bool;
        default = false;
        description = "Run the script in the background at startup";
      };
      runEvery = mkOption {
        type = types.nullOr types.str;
        default = null;
        description = "Run this script periodically every X minutes";
      }; # ü¶Ü duck say ‚Æû run at specific time
      runAt = mkOption {
        type = types.nullOr (types.listOf (types.strMatching "[0-9]{1,2}:[0-9]{2}"));
        default = null;
        description = "Run this script at specific times daily (format: [HH:MM, ...], 24-hour)";
        apply = validateTimes;
      }; # ü¶Ü duck say ‚Æû code to be executed when calling tda script yo      
      code = mkOption {
        type = types.lines;
        description = "The script code";
      }; # ü¶Ü duck say ‚Æû alias for da script for extra execution triggerz 
      aliases = mkOption {
        type = types.listOf types.str;
        default = [];
        description = "Alternative command names for this script";
      }; # ü¶Ü duck say ‚Æû read-only option dat showz da number of generated regex patternz
      voicePatterns = mkOption {
        type = types.int;
        internal = true;
        readOnly = true;
        description = "Number of regex patterns generated for this script's voice commands";      
      }; # ü¶Ü duck say ‚Æû phrase coverage for this script
      voicePhrases = mkOption {
        type = types.int;
        internal = true;
        readOnly = true;
        description = "Theoretical number of unique spoken phrases this script can understand";   
      }; # ü¶Ü duck say ‚Æû parameter options for the yo script we writin' 
      parameters = mkOption {
        type = types.listOf (types.submodule {
          options = { # ü¶Ü duck say ‚Æû parameters = [{ name = ""; description = ""; default = "": optional = "": type = ""; }]; 
            name = mkOption { type = types.str; };
            description = mkOption { type = types.str; };
            default = mkOption {
              type = types.nullOr (types.oneOf [
                types.str
                types.int
                types.bool
                types.path
              ]);
              default = null;
              description = "Default value if parameter is not provided";
            }; # ü¶Ü duck say ‚Æû i likez diz option - highly useful
            optional = mkOption { 
              type = types.bool; 
              default = ./default != null;
              description = "Whether this parameter can be omitted";
            }; # ü¶Ü duck say ‚Æû diz makez da param sleazy eazy to validate yo 
            type = mkOption {
              type = types.enum ["string" "int" "path" "bool"];
              default = "string";
              description = "Type of parameter. Use path for filepath int for numbers, bool for true/false flags, and string (default) for all others";
            }; # ü¶Ü duck say ‚Æû value option for allowed values (string type only)
            values = mkOption {
              type = types.nullOr (types.listOf types.str);
              default = null;
              description = "Allowed values for this parameter (only applicable for string type)";
            };
          };
        });
        default = [];
        description = "Parameters accepted by this script";
      };
      voice = mkOption {
        type = types.nullOr (types.submodule {
          options = {
            enabled = lib.mkOption {
              type = lib.types.bool;
              default = true;
              description = "Whether to generate voice intents for this script";
            };
            priority = mkOption {
              type = types.ints.between 1 5;
              default = 3;
              description = "Processing priority (1=highest, 5=lowest)";
            };
            fuzzy = mkOption {
              type = types.submodule {
                options = {
                  enable = mkOption {
                    type = types.bool;
                    default = true;
                    description = "Enable fuzzy voice matching for this script";
                  };
                  threshold = mkOption {
                    type = types.float;
                    default = 0.8;
                    description = "Script specific similarity threshold for fuzzy matching (0.0‚Äì1.0)";
                  };
                };
              };
              default = {};
              description = "Configuration for fuzzy voice matching";
            };
            sentences = mkOption {
              type = types.listOf types.str;
              default = [];
              description = "Voice command patterns for this script";
            };
            lists = mkOption {
              type = types.attrsOf (types.submodule {
                options = {
                  wildcard = mkOption {
                    type = types.bool;
                    default = false;
                    description = "Accept free-form text input";
                  };
                  values = mkOption {
                    type = types.listOf (types.submodule {
                      options."in" = mkOption { type = types.str; };
                      options.out = mkOption { type = types.str; };
                    });
                    default = [];
                  };
                };
              });
              default = {};
              description = "Entity lists for voice parameters";
            };
          };
        });
        default = null;
        description = "Voice command configuration for this script";
      }; # ü¶Ü duck say ‚Æû read-only option dat showz if da script haz voice
      voiceReady = mkOption {
        type = types.bool;
        internal = true;
        readOnly = true;
        description = "Whether this script has voice commands configured";
      };
    };
    config = let # ü¶Ü duck say ‚Æû map categories to bin directories
      categoryDirMap = {
        "üéß Media Management" = "bin/media";
        "üó£Ô∏è Voice" = "bin/voice";
        "üõñ Home Automation" = "bin/home";
        "üßπ Maintenance" = "bin/maintenance";
        "üß© Miscellaneous" = "bin/misc";
        "üåê Networking" = "bin/network";
        "üåç Localization" = "bin/misc";
        "‚ö° Productivity" = "bin/productivity";
        "üñ•Ô∏è System Management" = "bin/system";
        "üìÅ File Operations" = "bin/files";        
        "üîê Security & Encryption" = "bin/security";
      };  
      script = config.yo.scripts.${name};      
      vr = config.yo.scripts.${name}.voiceReady;      
      category = config.yo.scripts.${name}.category;
      resolvedDir = categoryDirMap.${category} or "bin/misc"; # ü¶Ü duck say ‚Æû falback to bin/misc
    in { # ü¶Ü duck say ‚Æû set scripts filepath
      filePath = mkDefault "${resolvedDir}/${name}.nix";
      voiceReady = mkDefault (
        script.voice != null && 
        script.voice.sentences != [] &&
        script.voice.sentences != null
      );
      # ü¶Ü duck say ‚Æû set script counterz
      voicePatterns = mkDefault (countGeneratedPatterns script);
      voicePhrases = mkDefault (countUnderstoodPhrases script);
    };
  });
  cfg = config.yo;

  # ü¶Ü duck say ‚Æû letz create da yo scripts pkgs - we symlinkz all yo scripts togetha .. quack quack 
  yoScriptsPackage = pkgs.symlinkJoin {
    name = "yo-scripts"; # ü¶Ü duck say ‚Æû map over yo scripts and gen dem shell scriptz wrapperz!!
    paths = mapAttrsToList (name: script:
      let # ü¶Ü duck say ‚Æû compile help sentences at build time      
        # ü¶Ü duck say ‚Æû compile help sentences at build time
        voiceSentencesHelp = if script.voice != null && script.voice.sentences != [] then
          let
            patterns = countGeneratedPatterns script;
            phrases = countUnderstoodPhrases script;
            # ü¶Ü duck say ‚Æû copy the parameter replacement logic from voiceSentencesHelpFile
            replaceParamsWithValues = sentence: voiceData:
              let
                processToken = token:
                  if lib.hasPrefix "{" token && lib.hasSuffix "}" token then
                    let
                      paramName = lib.removePrefix "{" (lib.removeSuffix "}" token);
                      listData = voiceData.lists.${paramName} or null;
                    in
                      if listData != null then
                        if listData.wildcard or false then
                          "ANYTHING"
                        else
                          let
                            # ü¶Ü duck say ‚Æû get all possible input values
                            values = map (v: v."in") listData.values;
                            # ü¶Ü duck say ‚Æû expand any optional patterns like [foo|bar]
                            expandedValues = lib.concatMap expandListInputVariants values;
                            # ü¶Ü duck say ‚Æû take first few examples for display
                            examples = lib.take 3 (lib.unique expandedValues);
                          in
                            if examples == [] then "ANYTHING"
                            else "(" + lib.concatStringsSep "|" examples + 
                                 (if lib.length examples < lib.length expandedValues then "|...)" else ")")
                      else
                        "ANYTHING" # ü¶Ü duck say ‚Æû fallback if param not found
                  else
                    token;
                
                # ü¶Ü duck say ‚Æû split sentence and process each token
                tokens = lib.splitString " " sentence;
                processedTokens = map processToken tokens;
              in
                lib.concatStringsSep " " processedTokens;
            
            # ü¶Ü duck say ‚Æû replace params in each sentence for the help display
            processedSentences = map (sentence: 
              replaceParamsWithValues sentence script.voice
            ) script.voice.sentences;
            
            sentencesMarkdown = lib.concatMapStrings (sentence: 
              "- \"${escapeMD sentence}\"\n"
            ) processedSentences;
          in
            "## Voice Commands\n\nPatterns: ${toString patterns}  \nPhrases: ${toString phrases}  \n\n${sentencesMarkdown}"
        else "";
       
      
        # ü¶Ü duck say ‚Æû generate a string for da CLI usage optional parameters [--like] diz yo
        param_usage = lib.concatMapStringsSep " " (param:
          if param.optional
          then "[--${param.name}]" # ü¶Ü duck say ‚Æû iptional params baked inoto brackets
          else "--${param.name}" # ü¶Ü duck say ‚Æû otherz paramz shown az iz yo
        # ü¶Ü duck say ‚Æû filter out da special flagz from standard usage 
        ) (lib.filter (p: !builtins.elem p.name ["!" "?"]) script.parameters);
        
        # ü¶Ü duck say ‚Æû diz iz where da magic'z at yo! trust da duck yo 
        scriptContent = ''
          #!${pkgs.runtimeShell}
#          set -euo pipefail # ü¶Ü duck say ‚Æû strict error handlin' yo - will exit on errorz
          set -o noglob  # ü¶Ü duck say ‚Æû disable wildcard expansion for ? and ! flags
          ${yoEnvGenVar script} # ü¶Ü duck say ‚Æû inject da env quack quack.... quack
          export LC_NUMERIC=C
          start=$(date +%s.%N)
#          trap 'end=$(date +%s.%N); elapsed=$(echo "$end - $start" | bc); printf "[ü¶Ü‚è±] Total time: %.3f seconds\n" "$elapsed"' EXIT
          # ü¶Ü duck say ‚Æû duckTrace log setup
          export DT_LOG_PATH="$HOME/.config/duckTrace/"
          mkdir -p "$DT_LOG_PATH"   
          export DT_LOG_FILE="${name}.log" # ü¶Ü duck say ‚Æû duck tracin' be namin' da log file for da ran script
          touch "$DT_LOG_PATH/$DT_LOG_FILE"
          export DT_LOG_LEVEL="${script.logLevel}" # ü¶Ü duck say ‚Æû da tracin' duck back to fetch da log level yo
          DT_MONITOR_HOSTS="${sysHostsComma}";
          DT_MONITOR_PORT="9999";
      
          # ü¶Ü duck say ‚Æû PHASE 1: preprocess special flagz woop woop
          VERBOSE=0
          DRY_RUN=false
          FILTERED_ARGS=()
          # ü¶Ü duck say ‚Æû LOOP through da ARGz til' duckie duck all  dizzy duck
          while [[ $# -gt 0 ]]; do
            case "$1" in
              \?) ((VERBOSE++)); shift ;;        # ü¶Ü duck say ‚Æû if da arg iz '?' == increment verbose counter
              '!') DRY_RUN=true; shift ;;        # ü¶Ü duck say ‚Æû if da arg iz '!' == enablez da dry run mode yo
              *) FILTERED_ARGS+=("$1"); shift ;; # ü¶Ü duck say ‚Æû else we collect dem arguments for script processin'
            esac
          done  
          VERBOSE=$VERBOSE
          export VERBOSE DRY_RUN
          
          # ü¶Ü duck say ‚Æû reset arguments without special flags
          set -- "''${FILTERED_ARGS[@]}"

          # ü¶Ü duck say ‚Æû PHASE 2: regular parameter parsin' flappin' flappin' quack quack yo
          declare -A PARAMS=()
          POSITIONAL=()
          VERBOSE=$VERBOSE
          DRY_RUN=$DRY_RUN
          # ü¶Ü duck say ‚Æû if ? flag used - sets scripts logLevel to DEBUG
          if [ "$VERBOSE" -ge 1 ]; then
            DT_LOG_LEVEL="DEBUG"
          fi
          
          # ü¶Ü duck say ‚Æû parse all parameters
          while [[ $# -gt 0 ]]; do
            case "$1" in
              --help|-h) # ü¶Ü duck say ‚Æû if  u needz help call `--help` or `-h`
                width=$(tput cols 2>/dev/null || echo 100) # ü¶Ü duck say ‚Æû get terminal width for formatin' - fallin' back to 100
                help_footer=$(${script.helpFooter}) # ü¶Ü duck say ‚Æû dynamically generatez da helpFooter if ya defined it yo   
                # ü¶Ü duck say ‚Æû script haz paramz?
                usage_suffix=""
                if [[ -n "${toString (script.parameters != [])}" ]]; then
                  usage_suffix=" [OPTIONS]"
                fi
                
                cat <<EOF | ${pkgs.glow}/bin/glow --width "$width" - # ü¶Ü duck say ‚Æû renderin' da cool & duckified CLI docz usin' Markdown & Glow yo 
# üöÄü¶Ü yo ${escapeMD script.name}
${script.description}
**Usage:** \`yo ${escapeMD script.name}''${usage_suffix}\`
${lib.optionalString (script.parameters != []) ''
## Parameters
${lib.concatStringsSep "\n\n" (map (param: ''
**\`--${param.name}\`**  
${param.description}  
${lib.optionalString param.optional "*(optional)*"} ${lib.optionalString (param.default != null) (let
  defaultText = 
    if param.type == "bool" then 
      (if param.default then "true" else "false")
    else 
      (toString param.default);
in "*(default: ${defaultText})*")}
${lib.optionalString (param.values != null && param.type == "string") 
  "*(allowed: ${lib.concatStringsSep ", " param.values})*"}
'') script.parameters)}
''}
${voiceSentencesHelp}

$help_footer
EOF
                exit 0
                ;;
              --*) # ü¶Ü duck say ‚Æû parse named paramz like: "--duck"
                param_name=''${1##--}
                # ü¶Ü duck say ‚Æû let'z check if diz param existz in da scriptz defined parameterz
                if [[ " ${concatMapStringsSep " " (p: 
                      if p.type == "bool" then p.name else ""
                    ) script.parameters} " =~ " $param_name " ]]; then
                  # ü¶Ü duck say ‚Æû boolean flag - presence means true, but also allow explicit true/false
                  if [[ $# -gt 1 && ( "$2" == "true" || "$2" == "false" ) ]]; then
                    PARAMS["$param_name"]="$2"
                    shift 2
                  else
                    PARAMS["$param_name"]="true"
                    shift 1
                  fi
                else
                  # ü¶Ü duck say ‚Æû regular param expects value
                  if [[ " ${concatMapStringsSep " " (p: p.name) script.parameters} " =~ " $param_name " ]]; then
                    PARAMS["$param_name"]="$2" # ü¶Ü duck say ‚Æû assignz da value
                    shift 2
                  else # ü¶Ü duck say ‚Æû unknown param? duck say fuck
                    echo -e "\033[1;31m ü¶Ü duck say ‚Æû fuck ‚ùå $1\033[0m Unknown parameter: $1"
                    exit 1
                  fi
                fi
                ;;
              *) # ü¶Ü duck say ‚Æû none of the above matchez? i guezz itz a positional param yo
                POSITIONAL+=("$1")
                shift
                ;;
            esac
          done

            # ü¶Ü duck say ‚Æû PHASE 3: assign dem' parameterz!
            ${concatStringsSep "\n" (lib.imap0 (idx: param: '' # ü¶Ü duck say ‚Æû match positional paramz to script paramz by index
              if (( ${toString idx} < ''${#POSITIONAL[@]} )); then
                ${param.name}="''${POSITIONAL[${toString idx}]}" # ü¶Ü duck say ‚Æû assign positional paramz to variable
              fi
            '') script.parameters)}
          # ü¶Ü duck say ‚Æû assign named paramz! PARAMS ‚Æû their variable
          ${concatStringsSep "\n" (map (param: ''
            if [[ -n "''${PARAMS[${param.name}]:-}" ]]; then
              ${param.name}="''${PARAMS[${param.name}]}"
            fi
          '') script.parameters)}

          # ü¶Ü duck say ‚Æû count da paramz you cant bring too many to da party yo
          ${optionalString (script.parameters != []) ''
            if [ ''${#POSITIONAL[@]} -gt ${toString (length script.parameters)} ]; then
              echo -e "\033[1;31m ü¶Ü duck say ‚Æû fuck ‚ùå Too many arguments (max ${toString (length script.parameters)})\033[0m" >&2
              exit 1
            fi
          ''}

          # ü¶Ü duck say ‚Æû param type validation quuackidly quack yo
          ${concatStringsSep "\n" (map (param: 
            optionalString (param.type != "string") ''
              if [ -n "''${${param.name}:-}" ]; then
                case "${param.type}" in
                  int)
                    if ! [[ "''${${param.name}}" =~ ^[0-9]+$ ]]; then
                      echo -e "\033[1;31m ü¶Ü duck say ‚Æû fuck ‚ùå ${name} --${param.name} must be integer\033[0m" >&2
                      exit 1
                    fi
                    ;;
                  path)
                    if ! [ -e "''${${param.name}}" ]; then
                      echo -e "\033[1;31m ü¶Ü duck say ‚Æû fuck ‚ùå ${name} Path not found: ''${${param.name}}\033[0m" >&2
                      exit 1
                    fi
                    ;;
                  bool)
                    if ! [[ "''${${param.name}}" =~ ^(true|false)$ ]]; then
                      echo -e "\033[1;31m ü¶Ü duck say ‚Æû fuck ‚ùå ${name} Parameter ${param.name} must be true or false\033[0m" >&2
                      exit 1
                    fi
                    ;;
                esac
              fi
            ''
          ) script.parameters)}


          # ü¶Ü duck say ‚Æû values validation - explicit allowed list yo
          ${concatStringsSep "\n" (map (param: 
            optionalString (param.values != null && param.type == "string") ''
              if [ -n "''${${param.name}:-}" ]; then
                # ü¶Ü duck say ‚Æû check if value is in allowed list
                allowed_values=(${lib.concatMapStringsSep " " (v: "'${lib.escapeShellArg v}'") param.values})
                value_found=false
                for allowed in "''${allowed_values[@]}"; do
                  if [[ "''${${param.name}}" == "$allowed" ]]; then
                    value_found=true
                    break
                  fi
                done
                if [[ "$value_found" == "false" ]]; then
                  echo -e "\033[1;31m ü¶Ü duck say ‚Æû fuck ‚ùå ${name} --${param.name} must be one of: ${lib.concatStringsSep ", " param.values}\033[0m" >&2
                  exit 1
                fi
              fi
            ''
          ) script.parameters)}


          # ü¶Ü duck say ‚Æû boolean defaults - false if not provided
          ${concatStringsSep "\n" (map (param: 
            optionalString (param.type == "bool" && param.default != null) ''
              if [[ -z "''${${param.name}:-}" ]]; then
                ${param.name}=${if param.default then "true" else "false"}
              fi
            '') script.parameters)}


          ${concatStringsSep "\n" (map (param: 
            optionalString (param.default != null) ''
              if [[ -z "''${${param.name}:-}" ]]; then
                ${param.name}=${
                  if param.type == "string" then 
                    "'${lib.escapeShellArg (toString param.default)}'" 
                  else if param.type == "int" then
                    "${toString param.default}"
                  else if param.type == "bool" then
                    (if param.default then "true" else "false")
                  else if param.type == "path" then
                    "'${lib.escapeShellArg (toString param.default)}'"
                  else
                    "'${lib.escapeShellArg (toString param.default)}'"
                }
              fi
            '') script.parameters)}
            
          # ü¶Ü duck say ‚Æû checkz required param yo - missing? errorz out 
          ${concatStringsSep "\n" (map (param: ''
            ${optionalString (!param.optional && param.default == null) ''
              if [[ -z "''${${param.name}:-}" ]]; then
                echo -e "\033[1;31m ü¶Ü duck say ‚Æû fuck ‚ùå ${name} Missing required parameter: ${param.name}\033[0m" >&2
                exit 1
              fi
            ''}
          '') script.parameters)}

          # ü¶Ü duck say ‚Æû EXECUTEEEEEAAAOO ü¶Üquackü¶Üquackü¶Üquackü¶Üquackü¶Üquackü¶Üquackü¶Üquackü¶Üquackü¶Üquackü¶Üquackü¶Üquackü¶Üyo
          ${script.code}
        '';
        # ü¶Ü duck say ‚Æû generate da entrypoint
        mainScript = pkgs.writeShellScriptBin "yo-${script.name}" scriptContent;
      in # ü¶Ü duck say ‚Æû letz wrap diz up already  
        pkgs.runCommand "yo-script-${script.name}" {} ''
          mkdir -p $out/bin  # ü¶Ü duck say ‚Æû symlinkz da main script
          ln -s ${mainScript}/bin/yo-${script.name} $out/bin/yo-${script.name} 
          ${concatMapStrings (alias: '' # ü¶Ü duck say ‚Æû dont forget to symlinkz da aliases too yo!
            ln -s ${mainScript}/bin/yo-${script.name} $out/bin/yo-${alias}
          '') script.aliases}
        ''
    ) cfg.scripts; # ü¶Ü duck say ‚Æû apply da logic to da yo scriptz
  };

  # ü¶Ü duck say ‚Æû build scripts for da --help command
  terminalScriptsTableFile = pkgs.writeText "yo-helptext.md" terminalScriptsTable;
  # ü¶Ü duck say ‚Æû markdown help text
  terminalScriptsTable = let # ü¶Ü duck say ‚Æû categorize scripts
    groupedScripts = lib.groupBy (script: script.category) (lib.attrValues cfg.scripts);
    # ü¶Ü duck say ‚Æû sort da scriptz by category
    visibleScripts2 = lib.filterAttrs (_: script: script.visibleInReadme) cfg.scripts;
    groupedScripts2 = lib.groupBy (script: script.category) (lib.attrValues visibleScripts2);
    sortedCategories2 = lib.sort (a: b: 
      # ü¶Ü duck say ‚Æû system management goes first yo
      if a == "üñ•Ô∏è System Management" then true
      else if b == "üñ•Ô∏è System Management" then false
      else a < b # ü¶Ü duck say ‚Æû after dat everything else quack quack
    ) (lib.attrNames groupedScripts2);
  
    # ü¶Ü duck say ‚Æû create table rows with category separatorz 
    rows = lib.concatMap (category:
      let  # ü¶Ü duck say ‚Æû sort from A to √ñ  
        scripts = lib.sort (a: b: a.name < b.name) groupedScripts.${category};
      in
        [ # ü¶Ü duck say ‚Æû add **BOLD** header table row for category
          "| **${escapeMD category}** | | |"
        ] 
        ++ # ü¶Ü duck say ‚Æû each yo script goes into a table row
        (map (script:
          let # ü¶Ü duck say ‚Æû format list of aliases
            aliasList = if script.aliases != [] then
              concatStringsSep ", " (map escapeMD script.aliases)
            else "";
            # ü¶Ü duck say ‚Æû generate CLI parameter hints, with [] for optional/defaulted
            paramHint = concatStringsSep " " (map (param:
              if param.optional || param.default != null
              then "[--${param.name}]"
              else "--${param.name}"
            ) script.parameters);
            # ü¶Ü duck say ‚Æû render yo script syntax with param
            syntax = "\\`yo ${escapeMD script.name} ${paramHint}\\`";
          in # ü¶Ü duck say ‚Æû write full md table row - command | aliases | description
            "| ${syntax} | ${aliasList} | ${escapeMD script.description} |"
        ) scripts)
    ) sortedCategories2;
  in concatStringsSep "\n" rows;


  # ü¶Ü duck say ‚Æû count GENERATED regex patterns (the ~800 count)
  countGeneratedPatterns = script:
    if script.voice == null then
      0
    else
      let # ü¶Ü duck say ‚Æû expand sentence variants with optional wordz
        expandedSentences = lib.concatMap expandOptionalWords script.voice.sentences;
      in
        lib.length expandedSentences;
  
  # ü¶Ü duck say ‚Æû count phrase coverage  
  countUnderstoodPhrases = script:
    if script.voice == null then
      0
    else
      let # ü¶Ü duck say ‚Æû expand sentence variants with optional wordz
        expandedSentences = lib.concatMap expandOptionalWords script.voice.sentences;   
        # ü¶Ü duck say ‚Æû extract parameter names from sentences
        extractParamNames = sentence:
          let # ü¶Ü duck say ‚Æû split by { to find parameters
            parts = lib.splitString "{" sentence;
            paramNames = lib.concatMap (part:
              let
                paramPart = lib.splitString "}" part;
              in
                if lib.length paramPart > 1 then
                  [ (lib.elemAt paramPart 0) ]
                else
                  []
            ) (lib.tail parts); # ü¶Ü says ‚Æû skip the first part (before first {)
          in
            paramNames; 
        # ü¶Ü says ‚Æû count parameter combinations for each expanded sentence
        countPhrasesForSentence = sentence:
          let
            paramNames = extractParamNames sentence;
          in
            if paramNames == [] then
              1
            else
              let # ü¶Ü duck say ‚Æû count possible values for each parameter
                paramValueCounts = map (paramName:
                  let
                    list = script.voice.lists.${paramName} or null;
                  in
                    if list == null then 1
                    else lib.length list.values
                ) paramNames;           
                # ü¶Ü duck say ‚Æû multiply counts for all parameters
                totalCombinations = lib.foldl (a: b: a * b) 1 paramValueCounts;
              in
                totalCombinations; 
        # ü¶Ü duck say ‚Æû sum phrases across all expanded sentences
        totalPhrases = lib.foldl (total: sentence:
          total + countPhrasesForSentence sentence
        ) 0 expandedSentences;
      in
        totalPhrases;
  
  # ü¶Ü duck say ‚Æû count generated patterns
  countTotalGeneratedPatterns = scripts:
    lib.foldl (total: script: 
      total + countGeneratedPatterns script
    ) 0 (lib.attrValues scripts);
  
  # ü¶Ü duck say ‚Æû count phrases across all scriptz  
  countTotalUnderstoodPhrases = scripts:
    lib.foldl (total: script: 
      total + countUnderstoodPhrases script
    ) 0 (lib.attrValues scripts);
  
  
  # ü¶Ü duck say ‚Æû generatez safe systemd timer namez
  makeTimerName = scriptName: timeStr:
    let
      safeTime = replaceStrings [":"] ["-"] timeStr;
    in
      "yo-${scriptName}-at-${safeTime}";

  
in { # ü¶Ü duck say ‚Æû import server/client module
  imports = [ ./yo-rs.nix ];

  # ü¶Ü duck say ‚Æû options options duck duck
  options = { # ü¶Ü duck say ‚Æû quack 
    yo = {
      pkgs = mkOption {
        type = types.package;
        readOnly = true;
        description = "The final yo scripts package";
      }; # ü¶Ü duck say ‚Æû yo scriptz optionz yo
      scripts = mkOption {
        type = types.attrsOf scriptType;
        default = {};
        description = "Attribute set of scripts to be made available";
      };
      sorryPhrases = mkOption {
        type = types.listOf types.str;
        default = [ 
          "Kompis du pratar japanska jag fattar ingenting"
          "Det l√•ter som att du har en k√∂ttee bulle i k√§ften. √Ñt klart middagen och f√∂rs√∂k sedan igen."
          "eeyyy bruscchan √∂ppna k√§ften innan du pratar ja fattar nada ju"
          "men h√•ll k√§ften cp!"
          "noll koll . Golf boll."
          "Urs√§kta?"
        ];
        description = "List of phrases to be randomly picked for text-to-speect when no match is found during pattern matching.";
      };
      SplitWords = mkOption {
        type = types.listOf types.str;
        default = [ "samt" ];
        example = [ "and" "also" ];
        description = ''
          List of words that is used for command chaining.
          Using one of these words would effectively end current command and start the next one.
        '';
      };
      # ü¶Ü duck say ‚Æû generated regex patterns count
      generatedPatterns = mkOption {
        type = types.int;
        readOnly = true;
        description = "Number of regex patterns generated at build time";
      }; # ü¶Ü duck say ‚Æû count nlp phrases understood  
      understandsPhrases = mkOption {
        type = types.int;
        readOnly = true;
        description = "Theoretical number of unique spoken phrases the system can understand";
      };
    };
  };  
  
  # ü¶Ü ‚Æû CONFIG  ü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Üü¶Ü
  config = {  # ü¶Ü duck say ‚Æû expose diz module and all yo.scripts as a package  

    yo.scripts.do = {
      description = "Brain (do) is a Natural Language to Shell script translator that generates dynamic regex patterns at build time for defined yo.script sentences. It runs exact and fuzzy pattern matching at runtime with automatic parameter resolution and seamless shell script execution";
      category = "üó£Ô∏è Voice"; # ü¶Ü says ‚Æû duckgorize iz zmart wen u hab many scriptz i'd say!     
      logLevel = "INFO";
      helpFooter = ''
        cat ${voiceSentencesHelpFile} 
      '';
      parameters = [ # ü¶Ü says ‚Æû set your mosquitto user & password
        { name = "input"; description = "Text to translate"; optional = true; } 
        { name = "fuzzy"; type = "int"; description = "Minimum procentage for considering fuzzy matching sucessful. (1-100)"; default = 60; }
      ];
      code = ''
        set +u  
        FUZZY_THRESHOLD=$fuzzy
        text="$input"
                   
        # ü¶Ü says ‚Æû check yo.scripts.do if DEBUG mode yo
        if [ "$VERBOSE" -ge 1 ]; then
          DEBUG=1 YO_SPLIT_WORDS="${splitWordsFile}" YO_SORRY_PHRASES="${sorryPhrasesFile}" YO_INTENT_DATA="${intentDataFile}" YO_FUZZY_INDEX="${fuzzyIndexFlatFile}" yo-do "$input" $FUZZY_THRESHOLD
        else
          YO_SPLIT_WORDS="${splitWordsFile}" YO_SORRY_PHRASES="${sorryPhrasesFile}" YO_INTENT_DATA="${intentDataFile}" YO_FUZZY_INDEX="${fuzzyIndexFlatFile}" yo-do "$input" $FUZZY_THRESHOLD
        fi
      '';
    };


    yo.pkgs = yoScriptsPackage; # ü¶Ü duck say ‚Æû reference as: ${config.pkgs.yo}/bin/yo-<name>
    # ü¶Ü duck say ‚Æû set global counterz
    yo.generatedPatterns = countTotalGeneratedPatterns cfg.scripts;
    yo.understandsPhrases = countTotalUnderstoodPhrases cfg.scripts;

    # ü¶Ü ‚Æû  SAFETY ASSERTIONS  ‚Æú ü¶Ü
    assertions = let # ü¶Ü ‚Æû safety first
      scripts = cfg.scripts;
      scriptNames = attrNames scripts;    
      
      # ü¶Ü duck say ‚Æû runAt scripts need default values on required paramz
      runAtErrors = lib.mapAttrsToList (name: script:
        if script.runAt != null then
          let
            missingParams = lib.filter (p: !p.optional && p.default == null) script.parameters;
          in
            if missingParams != [] then
              "ü¶Ü duck say ‚Æû fuck ‚ùå Cannot schedule '${name}' at ${script.runAt} - missing defaults for: " +
              lib.concatMapStringsSep ", " (p: p.name) missingParams
            else null
        else null
      ) scripts;
      actualRunAtErrors = lib.filter (e: e != null) runAtErrors;
            
      # ü¶Ü duck say ‚Æû quackin' flappin' mappin' aliasez ‚Æû script dat belong to it 
      aliasMap = lib.foldl' (acc: script:
        lib.foldl' (acc': alias:
          acc' // { 
            ${alias} = (acc'.${alias} or []) ++ [script.name]; # ü¶Ü duck say ‚Æû mmerge or start a list yo
          }
        ) acc script.aliases
      ) {} (attrValues scripts);
      # ü¶Ü duck say ‚Æû find conflicts between script names & script aliases
      scriptNameConflicts = lib.filterAttrs (alias: _: lib.elem alias scriptNames) aliasMap;  
      # ü¶Ü duck say ‚Æû find dem' double aliasez 
      duplicateAliases = lib.filterAttrs (_: scripts: lib.length scripts > 1) aliasMap;
      # ü¶Ü duck say ‚Æû build da alias conflict error msg
      formatConflict = alias: scripts: 
        "Alias '${alias}' conflicts with script name (used by: ${lib.concatStringsSep ", " scripts})";       
      # ü¶Ü duck say ‚Æû build da double rainbowz error msg yo
      formatDuplicate = alias: scripts: 
        "Alias '${alias}' used by multiple scripts: ${lib.concatStringsSep ", " scripts}";
      # ü¶Ü duck say ‚Æû find auto-start scriptz - if i find i make sure it haz default values for all required paramz
      autoStartErrors = lib.mapAttrsToList (name: script:
        if script.autoStart then
          let # ü¶Ü duck say ‚Æû filter dem not optional no defaultz
            missingParams = lib.filter (p: !p.optional && p.default == null) script.parameters;
          in
            if missingParams != [] then
              "ü¶Ü duck say ‚Æû fuck ‚ùå Cannot auto-start '${name}' - missing defaults for: " +
              lib.concatMapStringsSep ", " (p: p.name) missingParams
            else null
        else null
      ) scripts;    
      
      nonInteractiveErrors = lib.mapAttrsToList (name: script:
        if script.autoStart || script.runEvery != null then
          let
            missingParams = lib.filter (p: !p.optional && p.default == null) script.parameters;
          in
            if missingParams != [] then
              "ü¶Ü duck say ‚Æû fuck ‚ùå Cannot schedule '${name}' - missing defaults for: " +
              lib.concatMapStringsSep ", " (p: p.name) missingParams
            else null
        else null
      ) scripts;      
      # ü¶Ü duck say ‚Æû clean out dem' nullz! no nullz in ma ASSertionthz! ... quack
      actualAutoStartErrors = lib.filter (e: e != null) autoStartErrors;   
      # ü¶Ü duck say ‚Æû Validate da shit out of 'value' option quack! only allowed wit string type yo!
      valueTypeErrors = lib.concatMap (script:
        lib.concatMap (param:
          if param.values != null && param.type != "string" then
            [ "ü¶Ü duck say ‚Æû fuck ‚ùå Parameter '${param.name}' in script '${script.name}' has 'value' defined but type is '${param.type}' (only 'string' type allowed)" ]
          else []
        ) script.parameters
      ) (lib.attrValues scripts);
    in [
      { # ü¶Ü duck say ‚Æû assert no alias name cpmflict with script name 
        assertion = scriptNameConflicts == {};
        message = "ü¶Ü duck say ‚Æû fuck ‚ùå Alias/script name conflicts:\n" +
          lib.concatStringsSep "\n" (lib.mapAttrsToList formatConflict scriptNameConflicts);
      }
      { # ü¶Ü duck say ‚Æû make sure dat aliases unique are yo
        assertion = duplicateAliases == {};
        message = "ü¶Ü duck say ‚Æû fuck ‚ùå Duplicate aliases:\n" +
          lib.concatStringsSep "\n" (lib.mapAttrsToList formatDuplicate duplicateAliases);
      }
      { # ü¶Ü duck say ‚Æû autoStart scriptz must be fully configured of course!
        assertion = actualAutoStartErrors == [];
        message = "Auto-start errors:\n" + lib.concatStringsSep "\n" actualAutoStartErrors;
      }  
      { # ü¶Ü duck say ‚Æû runAt script fully configured?
        assertion = actualRunAtErrors == [];
        message = "runAt scheduling errors:\n" + lib.concatStringsSep "\n" actualRunAtErrors;
      }      
      { # ü¶Ü duck say ‚Æû runEvery OR runAt NOT BOTH
        assertion = lib.all (script: 
          !(script.runEvery != null && script.runAt != null)
        ) (lib.attrValues scripts);
        message = "ü¶Ü duck say ‚Æû fuck ‚ùå Script cannot have both runEvery and runAt set";
      }
      { # ü¶Ü duck say ‚Æû value option only 4 strings i said!
        assertion = valueTypeErrors == [];
        message = "Value type errors:\n" + lib.concatStringsSep "\n" valueTypeErrors;
      }
    ];
    # ü¶Ü duck say ‚Æû TODO replace with: system.activationScripts.update-readme.text = "${updateReadme}/bin/update-readme";

    environment.systemPackages = [
      config.yo.pkgs
      pkgs.glow # ü¶Ü duck say ‚Æû For markdown renderin' in da terminal
      (pkgs.writeShellScriptBin "yo" ''
        #y!${pkgs.runtimeShell}
        set -o noglob # ü¶Ü duck say ‚Æû help command data (
        script_dir="${yoScriptsPackage}/bin" 
        # ü¶Ü duck say ‚Æû help command data (yo --help
        show_help() {
          #width=$(tput cols) # ü¶Ü duck say ‚Æû Auto detect width
          width=130 # ü¶Ü duck say ‚Æû fixed width
          cat <<EOF | ${pkgs.glow}/bin/glow --width $width -
        ## ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚ãÜ‚ãÖ‚òÜ‚òÜ‚òÜ‚ãÖ‚ãÜ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ ##
        ## ü¶ÜüöÄ **yo CLI** ü¶Üü¶Ü 
        ## ü¶Ü duck say ‚Æû quack! i help with scripts yo
        **Usage:** \`yo <command> [arguments]\`
        ## ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚ãÜ‚ãÖ‚òÜ‚òÜ‚òÜ‚ãÖ‚ãÜ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ ##
        ## ü¶Ü‚ú® Available Commands
        Parameters inside brackets are [optional]
        | Command Syntax               | Aliases    | Description |
        |------------------------------|------------|-------------|
        ${terminalScriptsTable}
        ## ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚ãÜ‚ãÖ‚òÜ‚òÜ‚òÜ‚ãÖ‚ãÜ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ‚îÄ ##
        ## ü¶Ü‚ùì Detailed Help
        For specific command help: \`yo <command> --help\`
        \`yo do --help\` will list all defined voice intents.
        \`yo zigduck --help\` will display a battery status report for your deevices.
        ü¶Üü¶Ü
        EOF
          exit 0
        } # ü¶Ü duck say ‚Æû handle zero args           
        if [[ $# -eq 0 ]]; then
          show_help
          exit 1
        fi
        # ü¶Ü duck say ‚Æû parse da command
        case "$1" in # ü¶Ü duck say ‚Æû handle zero args "-h" & "--help" to show da help
          -h|--help) show_help; exit 0 ;;
          *) command="$1"; shift ;;
        esac

        script_path="$script_dir/yo-$command"
        if [[ -x "$script_path" ]]; then
          exec "$script_path" "$@"
        else
          # ü¶Ü duck say ‚Æû TODO FuzzYo commands! but until then..
          echo -e "\033[1;31m ü¶Ü duck say ‚Æû fuck ‚ùå $1\033[0m Error: Unknown command '$command'" >&2
          show_help
          exit 1
        fi
      '')
      yoScriptsPackage
    ];

    # ü¶Ü duck say ‚Æû buildz systemd services    
    systemd.services = lib.mkMerge [
      # ü¶Ü duck say ‚Æû if `autoStart` is set
      (lib.mapAttrs' (name: script:
        lib.nameValuePair "yo-${name}" (mkIf script.autoStart {
          enable = true;
          wantedBy = ["multi-user.target"];
          after = ["sound.target" "network.target" "pulseaudio.socket" "sops-nix.service"];
    
          serviceConfig = {
            ExecStart = let
              args = lib.concatMapStringsSep " " (param:
                "--${param.name} ${lib.escapeShellArg param.default}"
              ) (lib.filter (p: p.default != null) script.parameters);
            in "${yoScriptsPackage}/bin/yo-${name} ${args}";
            RestartSec = 45;
            Restart = "on-failure";
          };
        })
      ) cfg.scripts)
    
      # ü¶Ü duck say ‚Æû if `runEvery` is set 
      (lib.mapAttrs' (name: script:
        lib.nameValuePair "yo-${name}-periodic" (mkIf (script.runEvery != null) {
          enable = true;
          description = "Periodic execution of yo.${name}";
          serviceConfig = {
            Type = "oneshot";

            ExecStart = let
              args = lib.concatMapStringsSep " " (param:
                "--${param.name} ${lib.escapeShellArg param.default}"
              ) (lib.filter (p: p.default != null) script.parameters);
            in "${yoScriptsPackage}/bin/yo-${name} ${args}";
          };
        })
      ) cfg.scripts)
      
      # ü¶Ü duck say ‚Æû if `runAt` is set: one service that can be triggered by multiple timerz
      (lib.mapAttrs' (name: script:
        lib.nameValuePair "yo-${name}-scheduled" (mkIf (script.runAt != null) {
          enable = true;
          description = let
            # ü¶Ü duck say ‚Æû create human-readable time list
            timesFormatted = if script.runAt != null then
              lib.concatStringsSep ", " script.runAt
            else "";
            # ü¶Ü duck say ‚Æû include script description if available
            baseDesc = if script.description != "" then
              "${script.description} (scheduled at ${timesFormatted})"
            else
              "Scheduled execution of yo.${name} at ${timesFormatted}";
          in baseDesc;
          serviceConfig = {
            Type = "oneshot";
            ExecStart = let
              args = lib.concatMapStringsSep " " (param:
                "--${param.name} ${lib.escapeShellArg param.default}"
              ) (lib.filter (p: p.default != null) script.parameters);
            in "${yoScriptsPackage}/bin/yo-${name} ${args}";
          };
        })
      ) cfg.scripts)
    ];

    # ü¶Ü duck say ‚Æû systemd timer configuration
    systemd.timers = lib.mkMerge [  
      # ü¶Ü duck say ‚Æû if `runEvery` is configured 
      (lib.mapAttrs' (name: script:
        lib.nameValuePair "yo-${name}-periodic" (mkIf (script.runEvery != null) {
          enable = true;
          wantedBy = ["timers.target"];
          timerConfig = {
            OnCalendar = "*-*-* *:0/${script.runEvery}";
            Unit = "yo-${name}-periodic.service";
            Persistent = true;
          };
        })
      ) cfg.scripts)
      
      # ü¶Ü duck say ‚Æû if `runAt` is configured: one timer per scheduled time
      (lib.foldl' lib.recursiveUpdate {} (lib.mapAttrsToList (name: script:
        if script.runAt != null then
          lib.listToAttrs (lib.map (timeStr:
            lib.nameValuePair (makeTimerName name timeStr) {
              enable = true;
              wantedBy = ["timers.target"];
              timerConfig = {
                OnCalendar = "*-*-* ${timeStr}:00";
                Unit = "yo-${name}-scheduled.service";
                Persistent = true;
              };
            }
            ) script.runAt)
        else {}
      ) cfg.scripts))
    ];
  };} # ü¶Ü duck say ‚Æû 2 long module 4 jokez.. bai bai yo
# ü¶Ü says ‚Æû QuackHack-McBLindy out!
# ... üõåü¶Üüí§
