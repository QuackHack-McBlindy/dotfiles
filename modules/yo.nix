# dotfiles/modules/yo.nix â® https://github.com/quackhack-mcblindy/dotfiles
{ # ğŸ¦† duck say â® CLI framework - centralized script handling
  config,
  lib,       
  pkgs,   
  ...
} : with lib;
let 
  # ğŸ¦† says â® for README version badge yo
  nixosVersion = let
    raw = builtins.readFile /etc/os-release;
    versionMatch = builtins.match ".*VERSION_ID=([0-9\\.]+).*" raw;
  in builtins.replaceStrings [ "." ] [ "%2E" ] (builtins.elemAt versionMatch 0);
  
  # ğŸ¦† duck say â® quacky hacky helper 2 escape md special charizardz yo
  escapeMD = str: let
    replacements = [
      [ "\\" "\\\\" ]
      [ "*" "\\*" ]
      [ "`" "\\`" ]
      [ "_" "\\_" ]
      [ "[" "\\[" ]
      [ "]" "\\]" ]
    ];
  in
    lib.foldl (acc: r: replaceStrings [ (builtins.elemAt r 0) ] [ (builtins.elemAt r 1) ] acc) str replacements;

  # ğŸ¦† duck say â® manual readme is so 1999 duckie
  updateReadme = pkgs.writeShellScriptBin "update-readme" ''
    README_PATH="${config.this.user.me.dotfilesDir}/README.md"
    CONTACT_OUTPUT=""
    USER_TMP=$(mktemp)
    HOST_TMP=$(mktemp)

    # ğŸ¦† duck say â® count scripts in bin
    count_bin() {
      nix eval ${config.this.user.me.dotfilesDir}#nixosConfigurations.${config.this.host.hostname}.config.yo.scripts --json | jq 'keys | length'
    }
    # ğŸ¦† duck say â® count scripts with sentences defined
    count_voice() {
      nix eval ${config.this.user.me.dotfilesDir}#nixosConfigurations.${config.this.host.hostname}.config.yo.scripts --json \
        | jq '[.[] | select(.voice? and .voice.sentences?)] | length'
    }
    # ğŸ¦† duck say â® count generated patterns
    count_patterns() {
      nix eval ${config.this.user.me.dotfilesDir}#nixosConfigurations.${config.this.host.hostname}.config.yo.generatedPatterns
    }
    # ğŸ¦† duck say â® count generated patterns
    count_phrases() {
      nix eval ${config.this.user.me.dotfilesDir}#nixosConfigurations.${config.this.host.hostname}.config.yo.understandsPhrases
    }    

    # ğŸ¦† duck say â® Get script counts
    total_scripts=$(count_bin)
    voice_scripts=$(count_voice)    
    total_patterns=$(count_patterns)
    total_phrases=$(count_phrases)

    # ğŸ¦† duck say â® nix > json > nix lol
    json2nix() {
      nix eval ${config.this.user.me.dotfilesDir}#nixosConfigurations.${config.this.host.hostname}.config."$1" --json | jq -r -f <(cat <<'JQ'
def indent(n): reduce range(0;n) as $i (""; . + "  ");
def toNixValue(level):
  if type == "object" then
    "{\n" +
    (to_entries
     | map(indent(level+1) + "\(.key) = \(.value|toNixValue(level+1));")
     | join("\n")) +
    "\n" + indent(level) + "}"
  elif type == "array" then
    "[\n" +
    (map(indent(level+1) + (toNixValue(level+1)))
     | join("\n")) +
    "\n" + indent(level) + "]"
  elif type == "string" then
    "\"\(.|tostring)\""
  elif type == "boolean" then
    if . then "true" else "false" end
  elif . == null then
    "null"
  else
    tostring
  end;

def toNix: toNixValue(0);
toNix
JQ
)
    }

    # ğŸ¦† duck say â® get da defined smart home
    ZIGBEE_DEVICES_BLOCK=$(json2nix house.zigbee.devices)
    ZIGBEE_SCENES_BLOCK=$(json2nix house.zigbee.scenes)
    TVS_BLOCK=$(json2nix house.tv)

    SMART_HOME_BLOCK=$(
      echo "<details><summary><strong>"
      echo "Zigbee devices at \`config.house.zigbee.devices\`."
      echo "</strong></summary>"
      echo "<!-- ZIGBEE_DEVICES_START -->"
      echo '```nix'
      echo "$ZIGBEE_DEVICES_BLOCK"
      echo '```'
      echo "<!-- ZIGBEE_DEVICES_END -->"
      echo "</details>"
  
      echo "<br>"
      echo "<details><summary><strong>"
      echo "Zigbee scenes at \`config.house.zigbee.scenes\`."
      echo "</strong></summary>"
      echo "<!-- ZIGBEE_SCENES_START -->"
      echo '```nix'
      echo "$ZIGBEE_SCENES_BLOCK"
      echo '```'
      echo "<!-- ZIGBEE_SCENES_END -->"
      echo "</details>"

      echo "<br>"
      echo "<details><summary><strong>"
      echo "Android TV devices at \`config.house.tv\`."
      echo "</strong></summary>"
      echo "<!-- TVS_START -->"
      echo '```nix'
      echo "$TVS_BLOCK"
      echo '```'
      echo "<!-- TVS_END -->"
      echo "</details>"      
    )

    # ğŸ¦† duck say â® Extract versions
    nixos_version=$(nixos-version | cut -d. -f1-2)
    kernel_version=$(uname -r | cut -d'-' -f1)
    nix_version=$(nix --version | awk '{print $3}')
    bash_version=$(bash --version | head -n1 | awk '{print $4}' | cut -d'(' -f1)
    gnome_version=$(gnome-shell --version | awk '{print $3}')
    python_version=$(python3 --version | awk '{print $2}')  

    # ğŸ¦† duck say â® Construct badge URLs
    nixos_badge="https://img.shields.io/badge/NixOS-''${nixos_version}-blue?style=flat-square\\&logo=NixOS\\&logoColor=white"
    linux_badge="https://img.shields.io/badge/Linux-''${kernel_version}-red?style=flat-square\\&logo=linux\\&logoColor=white"
    nix_badge="https://img.shields.io/badge/Nix-''${nix_version}-blue?style=flat-square\\&logo=nixos\\&logoColor=white"
    bash_badge="https://img.shields.io/badge/bash-''${bash_version}-red?style=flat-square\\&logo=gnubash\\&logoColor=white"
    gnome_badge="https://img.shields.io/badge/GNOME-''${gnome_version}-purple?style=flat-square\\&logo=gnome\\&logoColor=white"
    python_badge="https://img.shields.io/badge/Python-''${python_version}-%23FFD43B?style=flat-square\\&logo=python\\&logoColor=white"
  
    # ğŸ¦† duck say â® Contact badges
    matrix_url="${config.this.user.me.matrix}"
    if [[ -n "${config.this.user.me.matrix}" ]]; then
      CONTACT_OUTPUT+="[![Matrix](https://img.shields.io/badge/Matrix-Chat-000000?style=flat-square&logo=matrix&logoColor=white)](${config.this.user.me.matrix})"$'\n'
    fi
    discord_url="${config.this.user.me.discord}"
    if [[ -n "${config.this.user.me.discord}" ]]; then
      CONTACT_OUTPUT+="[![Discord](https://img.shields.io/badge/Discord-Chat-5865F2?style=flat-square&logo=discord&logoColor=white)](${config.this.user.me.discord})"$'\n'
    fi
    email_address="${config.this.user.me.email}"
    if [[ -n "${config.this.user.me.email}" ]]; then
      CONTACT_OUTPUT+="[![Email](https://img.shields.io/badge/Email-Contact-6D4AFF?style=flat-square&logo=protonmail&logoColor=white)](mailto:${config.this.user.me.email})"$'\n'
    fi
    repo_url="${config.this.user.me.repo}"
    if [[ -n "$repo_url" ]]; then
      if [[ "$repo_url" =~ git@github.com:([^/]+)/([^/]+)\.git ]]; then
        repo_owner="''${BASH_REMATCH[1]}"
        repo_name="''${BASH_REMATCH[2]%.git}"
        github_discussions_url="https://github.com/''${repo_owner}/''${repo_name}/discussions"
      elif [[ "$repo_url" =~ https://github.com/([^/]+)/([^/]+)\.git ]]; then
        repo_owner="''${BASH_REMATCH[1]}"
        repo_name="''${BASH_REMATCH[2]%.git}"
        github_discussions_url="https://github.com/''${repo_owner}/''${repo_name}/discussions"
      else
        github_discussions_url=""
      fi
    else
      github_discussions_url=""
    fi
    if [[ -n "${config.this.user.me.repo}" ]]; then
      if [[ "${config.this.user.me.repo}" =~ (git@|https://)github.com(:|/)([^/]+)/([^/]+).git ]]; then
        repo_owner="''${BASH_REMATCH[3]}"
        repo_name="''${BASH_REMATCH[4]%.git}"
        CONTACT_OUTPUT+="[![GitHub Discussions](https://img.shields.io/badge/Discussions-Join-181717?style=flat-square&logo=github&logoColor=white)](https://github.com/''${repo_owner}/''${repo_name}/discussions)"$'\n'
      fi
    fi

    FLAKE_OUTPUT=$(nix flake show "${config.this.user.me.dotfilesDir}" | sed -e 's/\x1B\[[0-9;]*[A-Za-z]//g')
    FLAKE_BLOCK=$(
      echo '```nix'
      echo "$FLAKE_OUTPUT"
      echo '```'
    )

    # ğŸ¦† duck say â®  get generated help text
    HELP_CONTENT=$(<${helpTextFile})

    DOCS_CONTENT=$(cat <<'EOF'
## ğŸš€ **yo CLI ğŸ¦†**
The \`yo\` CLI is a framework designed to execute scripts defined in the \`./bin\` directory.  
It provides a unified interface for script execution, centralizes all help commands, and automatically updates the documentation.  

**Usage:** \`yo <command> [arguments]\`  

### **Usage Examples:**  
The yo CLI supports flexible parameter parsing through two primary mechanisms:  

```bash
# Named Parameters  
$ yo deploy --host laptop --flake /home/pungkula/dotfiles

# Positional Parameters
$ yo deploy laptop /home/pungkula/dotfiles

# Scripts can also be executed with natural language text by typing:
$ yo do "is laptop overheating"
# Natural language voice commands are also supported, say:
"yo bitch reboot the laptop"

# If the server is not running, it can be manually started with:
$ yo transcription
$ yo wake

# Get list of all defined sentences for voice commands:
$ yo do --help
```

### âœ¨ Available Commands
Set default values for your parameters to have them marked [optional]
| Command Syntax               | Aliases    | Description | VoiceReady |
|------------------------------|------------|-------------|--|
${helpText}
### â“ Detailed Help
For specific command help: 
\`yo <command> --help\`
\`yo <command> -h\`
EOF
    )

    tmpfile=$(mktemp)
    CONTACT_BLOCK=$(
      echo "<!-- CONTACT_START -->"
      echo "$CONTACT_OUTPUT"
      echo "<!-- CONTACT_END -->"
    )

    FLAKE_BLOCK_NIX=$(
      echo '```nix'
      cat "${config.this.user.me.dotfilesDir}/flake.nix"
      echo '```'
    )
    
    USER_BLOCK=$(
      echo '```nix'
      nix eval --json \
        "${config.this.user.me.dotfilesDir}#nixosConfigurations.${config.this.host.hostname}.config.this.user.me" \
        | jq -r '
          def to_nix($indent):
            def ind: ("  " * $indent);
            if type == "object" then
              "\(ind){\n" + (
                to_entries | map(
                  "\(ind)  \(.key) = \(.value | to_nix($indent + 1))"
                ) | join(";\n")
              ) + "\n\(ind)};"
            elif type == "array" then
              if map(type == "string") | all then
                "[ " + (map("\"\(.)\"") | join(" ")) + " ]"
              else
                "[\n" + (
                  map(to_nix($indent + 1)) | join("\n")
                ) + "\n\(ind)];"
              end
            elif type == "string" then
              "\"\(.)\""
            else
              tostring
            end;
          to_nix(0)' \
      | sed -e '1s/^{/{/' -e 's/;;/;/g' -e '/^$/d'
      echo '```'
    )
    
    HOST_BLOCK=$(
      echo '```nix'
      nix eval --json \
        "${config.this.user.me.dotfilesDir}#nixosConfigurations.${config.this.host.hostname}.config.this.host" \
        | jq -r '
          def to_nix($indent):
            def ind: ("  " * $indent);
            if type == "object" then
              "\(ind){\n" + (
                to_entries | map(
                  "\(ind)  \(.key) = \(.value | to_nix($indent + 1))"
                ) | join(";\n")
              ) + "\n\(ind)};"
            elif type == "array" then
              if map(type == "string") | all then
                "[ " + (map("\"\(.)\"") | join(" ")) + " ]"
              else
                "[\n" + (
                  map(to_nix($indent + 1)) | join("\n")
                ) + "\n\(ind)];"
              end
            elif type == "string" then
              "\"\(.)\""
            else
              tostring
            end;
          to_nix(0)' \
      | sed -e '1s/^{/{/' -e 's/;;/;/g' -e '/^$/d'
      echo '```'
    )
       
    THEME_BLOCK=$(
      echo '```nix'
      nix eval --json \
        "${config.this.user.me.dotfilesDir}#nixosConfigurations.${config.this.host.hostname}.config.this.theme" \
        | jq -r '
          def to_nix($indent):
            def ind: ("  " * $indent);
            if type == "object" then
              "\(ind){\n" + (
                to_entries | map(
                  "\(ind)  \(.key) = \(.value | to_nix($indent + 1))"
                ) | join(";\n")
              ) + "\n\(ind)};"
            elif type == "array" then
              if map(type == "string") | all then
                "[ " + (map("\"\(.)\"") | join(" ")) + " ]"
              else
                "[\n" + (
                  map(to_nix($indent + 1)) | join("\n")
                ) + "\n\(ind)];"
              end
            elif type == "string" then
              "\"\(.)\""
            else
              tostring
            end;
          to_nix(0)' \
      | sed -e '1s/^{/{/' -e 's/;;/;/g' -e '/^$/d'
      echo '```'
    )    

    STATS_BLOCK=$(
      echo "- __$total_scripts qwacktastic scripts in /bin - $voice_scripts scripts have voice commands.__ <br>"
      echo "- __$total_patterns dynamically generated regex patterns - makes $total_phrases phrases available as commands.__ <br>"            
    ) 
     
    # ğŸ¦† duck say â® Update version badges
    sed -i -E \
      -e "s|https://img.shields.io/badge/NixOS-[^)]*|$nixos_badge|g" \
      -e "s|https://img.shields.io/badge/Linux-[^)]*|$linux_badge|g" \
      -e "s|https://img.shields.io/badge/Nix-[^)]*|$nix_badge|g" \
      -e "s|https://img.shields.io/badge/bash-[^)]*|$bash_badge|g" \
      -e "s|https://img.shields.io/badge/GNOME-[^)]*|$gnome_badge|g" \
      -e "s|https://img.shields.io/badge/Python-[^)]*|$python_badge|g" \
      "$README_PATH"
     
    awk -v docs="$DOCS_CONTENT" \
        -v contact="$CONTACT_BLOCK" \
        -v tree="$FLAKE_BLOCK" \
        -v flake="$FLAKE_BLOCK_NIX" \
        -v host="$HOST_BLOCK" \
        -v user="$USER_BLOCK" \
        -v theme="$THEME_BLOCK" \
        -v stats="$STATS_BLOCK" \
        -v smart="$SMART_HOME_BLOCK" \
        '
      BEGIN { in_docs=0; in_contact=0; in_tree=0; in_flake=0; in_host=0; in_user=0; in_stats=0; in_smart=0; printed=0 }

      /<!-- YO_DOCS_START -->/ { in_docs=1; print; print docs; next }
      /<!-- YO_DOCS_END -->/ { in_docs=0; print; next }
      /<!-- SCRIPT_STATS_START -->/ { in_stats=1; print; print stats; next }
      /<!-- SCRIPT_STATS_END -->/ { in_stats=0; print; next }
      /<!-- HOST_START -->/ { in_host=1; print; print host; next }
      /<!-- HOST_END -->/ { in_host=0; print; next }
      /<!-- THEME_START -->/ { in_theme=1; print; print theme; next }
      /<!-- THEME_END -->/ { in_theme=0; print; next }
      /<!-- SMARTHOME_START -->/ { in_smart=1; print; print smart; next }
      /<!-- SMARTHOME_END -->/ { in_smart=0; print; next }
      /<!-- USER_START -->/ { in_user=1; print; print user; next }
      /<!-- USER_END -->/ { in_user=0; print; next }
      /<!-- TREE_START -->/ { in_tree=1; print; print tree; next }
      /<!-- TREE_END -->/ { in_tree=0; print; next }
      /<!-- FLAKE_START -->/ { in_flake=1; print; print flake; next }
      /<!-- FLAKE_END -->/ { in_flake=0; print; next }
      !in_docs && !in_tree && !in_theme && !in_flake && !in_smart && !in_stats && !in_host && !in_user { print }
      ' "$README_PATH" > "$tmpfile"  

    # ğŸ¦† duck say â® diff check
    if ! cmp -s "$tmpfile" "$README_PATH"; then
      echo "ğŸ¦† duck say > Changes detected, updating README.md"
      if ! install -m 644 "$tmpfile" "$README_PATH"; then
        echo -e "\033[1;31m ğŸ¦† duck say â® fuck âŒ Failed to update README.md (permissions?)" >&2
        rm "$tmpfile"
        exit 1
      fi
    else
      echo "ğŸ¦† duck say > âœ… No content changes needed"
    fi
  
    if ! diff -q "$tmpfile" "$README_PATH" >/dev/null; then
      if [ -w "$README_PATH" ]; then
        cat "$tmpfile" > "$README_PATH"
        echo "ğŸ¦† duck say > Updated README.md"
      else
        echo -e "\033[1;31m ğŸ¦† duck say â® fuck âŒ Cannot update $README_PATH: Permission denied" >&2
        exit 1
      fi
    else
      echo "No changes needed"
    fi

    rm "$tmpfile"
    rm "$USER_TMP" "$HOST_TMP"
  '';

  # ğŸ¦† duck say â® expoort param into shell script
  yoEnvGenVar = script: let
    withDefaults = builtins.filter (p: p.default != null) script.parameters;
    exports = map (p: 
      let # ğŸ¦† duck say â® convert dem Nix types 2 shell strings
        defaultValue = 
          if p.type == "string" then lib.escapeShellArg (toString p.default)
          else if p.type == "int" then toString p.default
          else if p.type == "bool" then (if p.default then "true" else "false")
          else if p.type == "path" then lib.escapeShellArg (toString p.default)
          else lib.escapeShellArg (toString p.default);
      in
        "export ${p.name}=${defaultValue}"
    ) withDefaults;
  in lib.concatStringsSep "\n" exports;

  scriptType = types.submodule ({ name, configFinal, ... }: {   
# ğŸ¦† â® OPTIONS ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†#    
    options = { # ğŸ¦† duck say â® a name cool'd be cool right?
      name = mkOption {
        type = types.str;
        internal = true;
        readOnly = true;
        default = name;
        description = "Script name derived from attribute key";
      }; # ğŸ¦† duck say â® describe yo script yo!
      description = mkOption {
        type = types.str;
        default = "";
        description = "Description of the script";
      }; # ğŸ¦† duck say > categoryiez da script (for sorting in `yo --help` & README.md    
      category = mkOption {
        type = types.str;
        default = "";
        description = "Category of the script";
      };
      filePath = mkOption {
        type = types.str;
        readOnly = true;
      };
      # ğŸ¦† duck say â® yo go ahead describe da script yo     
      visibleInReadme = mkOption {
        type = types.bool;
        default = ./category != "";
        defaultText = "category != \"\"";
        description = "Whether to include this script in README.md";
      }; # ğŸ¦† duck say â® duck trace log level
      logLevel = mkOption {
        type = types.enum ["DEBUG" "INFO" "WARNING" "ERROR" "CRITICAL"];
        default = "INFO";
        description = "Sets the log level for Duck Trace";
      }; # ğŸ¦† duck say â® extra code to be ran & displayed whelp calling da scripts --help cmd  
      helpFooter = mkOption {
        type = types.lines;
        default = "";
        description = "Additional shell code to run when generating help text";
      }; # ğŸ¦† duck say â® generatez systemd service for da script if true 
      autoStart = mkOption {
        type = types.bool;
        default = false;
        description = "Run the script in the background at startup";
      };
      runEvery = mkOption {
        type = types.nullOr types.str;
        default = null;
        description = "Run this script periodically every X minutes";
      }; # ğŸ¦† duck say â® code to be executed when calling tda script yo      
      code = mkOption {
        type = types.lines;
        description = "The script code";
      }; # ğŸ¦† duck say â® alias for da script for extra execution triggerz 
      aliases = mkOption {
        type = types.listOf types.str;
        default = [];
        description = "Alternative command names for this script";
      }; # ğŸ¦† duck say â® read-only option dat showz da number of generated regex patternz
      voicePatterns = mkOption {
        type = types.int;
        internal = true;
        readOnly = true;
        description = "Number of regex patterns generated for this script's voice commands";      
      }; # ğŸ¦† duck say â® phrase coverage for this script
      voicePhrases = mkOption {
        type = types.int;
        internal = true;
        readOnly = true;
        description = "Theoretical number of unique spoken phrases this script can understand";   
      }; # ğŸ¦† duck say â® parameter options for the yo script we writin' 
      parameters = mkOption {
        type = types.listOf (types.submodule {
          options = { # ğŸ¦† duck say â® parameters = [{ name = ""; description = ""; default = "": optional = "": type = ""; }]; 
            name = mkOption { type = types.str; };
            description = mkOption { type = types.str; };
            default = mkOption {
              type = types.nullOr (types.oneOf [
                types.str
                types.int
                types.bool
                types.path
              ]);
              default = null;
              description = "Default value if parameter is not provided";
            }; # ğŸ¦† duck say â® i likez diz option - highly useful
            optional = mkOption { 
              type = types.bool; 
              default = ./default != null;
              description = "Whether this parameter can be omitted";
            }; # ğŸ¦† duck say â® diz makez da param sleazy eazy to validate yo 
            type = mkOption {
              type = types.enum ["string" "int" "path" "bool"];
              default = "string";
              description = "Type of parameter. Use path for filepath int for numbers, bool for true/false flags, and string (default) for all others";
            };
          };
        });
        default = [];
        description = "Parameters accepted by this script";
      };
      voice = mkOption {
        type = types.nullOr (types.submodule {
          options = {
            enabled = lib.mkOption {
              type = lib.types.bool;
              default = true;
              description = "Whether to generate voice intents for this script";
            };
            priority = mkOption {
              type = types.ints.between 1 5;
              default = 3;
              description = "Processing priority (1=highest, 5=lowest)";
            };
            sentences = mkOption {
              type = types.listOf types.str;
              default = [];
              description = "Voice command patterns for this script";
            };
            lists = mkOption {
              type = types.attrsOf (types.submodule {
                options = {
                  wildcard = mkOption {
                    type = types.bool;
                    default = false;
                    description = "Accept free-form text input";
                  };
                  values = mkOption {
                    type = types.listOf (types.submodule {
                      options."in" = mkOption { type = types.str; };
                      options.out = mkOption { type = types.str; };
                    });
                    default = [];
                  };
                };
              });
              default = {};
              description = "Entity lists for voice parameters";
            };
          };
        });
        default = null;
        description = "Voice command configuration for this script";
      }; # ğŸ¦† duck say â® read-only option dat showz if da script haz voice
      voiceReady = mkOption {
        type = types.bool;
        internal = true;
        readOnly = true;
        description = "Whether this script has voice commands configured";
      };
    };
    config = let # ğŸ¦† duck say â® map categories to bin directories
      categoryDirMap = {
        "ğŸ§ Media Management" = "bin/media";
        "âš™ï¸ Configuration" = "bin/config";
        "ğŸ›– Home Automation" = "bin/home";
        "ğŸ§¹ Maintenance" = "bin/maintenance";
        "ğŸ§© Miscellaneous" = "bin/misc";
        "ğŸŒ Networking" = "bin/network";
        "ğŸŒ Localization" = "bin/misc";
        "âš¡ Productivity" = "bin/productivity";
        "ğŸ–¥ï¸ System Management" = "bin/system";
        "ğŸ” Security & Encryption" = "bin/security";
      };  
      script = config.yo.scripts.${name};      
      vr = config.yo.scripts.${name}.voiceReady;      
      category = config.yo.scripts.${name}.category;
      resolvedDir = categoryDirMap.${category} or "bin/misc"; # ğŸ¦† duck say â® falback to bin/misc
    in { # ğŸ¦† duck say â® set scripts filepath
      filePath = mkDefault "${resolvedDir}/${name}.nix";
      voiceReady = mkDefault (
        script.voice != null && 
        script.voice.sentences != [] &&
        script.voice.sentences != null
      );
      # ğŸ¦† duck say â® set script counterz
      voicePatterns = mkDefault (countGeneratedPatterns script);
      voicePhrases = mkDefault (countUnderstoodPhrases script);
    };
  });
  cfg = config.yo;

  # ğŸ¦† duck say â® letz create da yo scripts pkgs - we symlinkz all yo scripts togetha .. quack quack 
  yoScriptsPackage = pkgs.symlinkJoin {
    name = "yo-scripts"; # ğŸ¦† duck say â® map over yo scripts and gen dem shell scriptz wrapperz!!
    paths = mapAttrsToList (name: script:
      let # ğŸ¦† duck say â® compile help sentences at build time
        voiceSentencesHelp = if script.voice != null && script.voice.sentences != [] then
          let
            sentencesMarkdown = lib.concatMapStrings (sentence: "- \"${escapeMD sentence}\"\n") script.voice.sentences;
          in
            "## Voice Commands\n\n${sentencesMarkdown}"
        else "";
      
        # ğŸ¦† duck say â® generate a string for da CLI usage optional parameters [--like] diz yo
        param_usage = lib.concatMapStringsSep " " (param:
          if param.optional
          then "[--${param.name}]" # ğŸ¦† duck say â® iptional params baked inoto brackets
          else "--${param.name}" # ğŸ¦† duck say â® otherz paramz shown az iz yo
        # ğŸ¦† duck say â® filter out da special flagz from standard usage 
        ) (lib.filter (p: !builtins.elem p.name ["!" "?"]) script.parameters);
        
        # ğŸ¦† duck say â® diz iz where da magic'z at yo! trust da duck yo 
        scriptContent = ''
          #!${pkgs.runtimeShell}
#          set -euo pipefail # ğŸ¦† duck say â® strict error handlin' yo - will exit on errorz
          ${yoEnvGenVar script} # ğŸ¦† duck say â® inject da env quack quack.... quack
          export LC_NUMERIC=C
          start=$(date +%s.%N)
#          trap 'end=$(date +%s.%N); elapsed=$(echo "$end - $start" | bc); printf "[ğŸ¦†â±] Total time: %.3f seconds\n" "$elapsed"' EXIT
          # ğŸ¦† duck say â® duckTrace log setup
          export DT_LOG_PATH="$HOME/.config/duckTrace/"
          mkdir -p "$DT_LOG_PATH"   
          export DT_LOG_FILE="${name}.log" # ğŸ¦† duck say â® duck tracin' be namin' da log file for da ran script
          touch "$DT_LOG_PATH/$DT_LOG_FILE"
          export DT_LOG_LEVEL="${script.logLevel}" # ğŸ¦† duck say â® da tracin' duck back to fetch da log level yo

          # ğŸ¦† duck say â® PHASE 1: preprocess special flagz woop woop
          VERBOSE=0
          DRY_RUN=false
          FILTERED_ARGS=()
          # ğŸ¦† duck say â® LOOP through da ARGz til' duckie duck all  dizzy duck
          while [[ $# -gt 0 ]]; do
            case "$1" in
              \?) ((VERBOSE++)); shift ;;        # ğŸ¦† duck say â® if da arg iz '?' == increment verbose counter
              '!') DRY_RUN=true; shift ;;        # ğŸ¦† duck say â® if da arg iz '!' == enablez da dry run mode yo
              *) FILTERED_ARGS+=("$1"); shift ;; # ğŸ¦† duck say â® else we collect dem arguments for script processin'
            esac
          done  
          VERBOSE=$VERBOSE
          export VERBOSE DRY_RUN
     
          # ğŸ¦† duck say â® reset arguments without special flags
          set -- "''${FILTERED_ARGS[@]}"

          # ğŸ¦† duck say â® PHASE 2: regular parameter parsin' flappin' flappin' quack quack yo
          declare -A PARAMS=()
          POSITIONAL=()
          VERBOSE=$VERBOSE
          DRY_RUN=$DRY_RUN
          
          # ğŸ¦† duck say â® parse all parameters
          while [[ $# -gt 0 ]]; do
            case "$1" in
              --help|-h) # ğŸ¦† duck say â® if  u needz help call `--help` or `-h`
                width=$(tput cols 2>/dev/null || echo 100) # ğŸ¦† duck say â® get terminal width for formatin' - fallin' back to 100
                help_footer=$(${script.helpFooter}) # ğŸ¦† duck say â® dynamically generatez da helpFooter if ya defined it yo   
                # ğŸ¦† duck say â® script haz paramz?
                usage_suffix=""
                if [[ -n "${toString (script.parameters != [])}" ]]; then
                  usage_suffix=" [OPTIONS]"
                fi
                
                cat <<EOF | ${pkgs.glow}/bin/glow --width "$width" - # ğŸ¦† duck say â® renderin' da cool & duckified CLI docz usin' Markdown & Glow yo 
# ğŸš€ğŸ¦† yo ${escapeMD script.name}
**Usage:** \`yo ${escapeMD script.name}''${usage_suffix}\`
${script.description}
${lib.optionalString (script.parameters != []) ''
## Parameters
${lib.concatStringsSep "\n\n" (map (param: ''
**\`--${param.name}\`**  
${param.description}  
${lib.optionalString param.optional "*(optional)*"} ${lib.optionalString (param.default != null) (let
  defaultText = 
    if param.type == "bool" then 
      (if param.default then "true" else "false")
    else 
      (toString param.default);
in "*(default: ${defaultText})*")}
'') script.parameters)}
''}
${voiceSentencesHelp}

$help_footer
EOF
                exit 0
                ;;
              --*) # ğŸ¦† duck say â® parse named paramz like: "--duck"
                param_name=''${1##--}
                # ğŸ¦† duck say â® let'z check if diz param existz in da scriptz defined parameterz
                if [[ " ${concatMapStringsSep " " (p: 
                      if p.type == "bool" then p.name else ""
                    ) script.parameters} " =~ " $param_name " ]]; then
                  # ğŸ¦† duck say â® boolean flag - presence means true, but also allow explicit true/false
                  if [[ $# -gt 1 && ( "$2" == "true" || "$2" == "false" ) ]]; then
                    PARAMS["$param_name"]="$2"
                    shift 2
                  else
                    PARAMS["$param_name"]="true"
                    shift 1
                  fi
                else
                  # ğŸ¦† duck say â® regular param expects value
                  if [[ " ${concatMapStringsSep " " (p: p.name) script.parameters} " =~ " $param_name " ]]; then
                    PARAMS["$param_name"]="$2" # ğŸ¦† duck say â® assignz da value
                    shift 2
                  else # ğŸ¦† duck say â® unknown param? duck say fuck
                    echo -e "\033[1;31m ğŸ¦† duck say â® fuck âŒ $1\033[0m Unknown parameter: $1"
                    exit 1
                  fi
                fi
                ;;
              *) # ğŸ¦† duck say â® none of the above matchez? i guezz itz a positional param yo
                POSITIONAL+=("$1")
                shift
                ;;
            esac
          done

            # ğŸ¦† duck say â® PHASE 3: assign dem' parameterz!
            ${concatStringsSep "\n" (lib.imap0 (idx: param: '' # ğŸ¦† duck say â® match positional paramz to script paramz by index
              if (( ${toString idx} < ''${#POSITIONAL[@]} )); then
                ${param.name}="''${POSITIONAL[${toString idx}]}" # ğŸ¦† duck say â® assign positional paramz to variable
              fi
            '') script.parameters)}
          # ğŸ¦† duck say â® assign named paramz! PARAMS â® their variable
          ${concatStringsSep "\n" (map (param: ''
            if [[ -n "''${PARAMS[${param.name}]:-}" ]]; then
              ${param.name}="''${PARAMS[${param.name}]}"
            fi
          '') script.parameters)}

          # ğŸ¦† duck say â® count da paramz you cant bring too many to da party yo
          ${optionalString (script.parameters != []) ''
            if [ ''${#POSITIONAL[@]} -gt ${toString (length script.parameters)} ]; then
              echo -e "\033[1;31m ğŸ¦† duck say â® fuck âŒ Too many arguments (max ${toString (length script.parameters)})\033[0m" >&2
              exit 1
            fi
          ''}

          # ğŸ¦† duck say â® param type validation quuackidly quack yo
          ${concatStringsSep "\n" (map (param: 
            optionalString (param.type != "string") ''
              if [ -n "''${${param.name}:-}" ]; then
                case "${param.type}" in
                  int)
                    if ! [[ "''${${param.name}}" =~ ^[0-9]+$ ]]; then
                      echo -e "\033[1;31m ğŸ¦† duck say â® fuck âŒ ${param.name} must be integer\033[0m" >&2
                      exit 1
                    fi
                    ;;
                  path)
                    if ! [ -e "''${${param.name}}" ]; then
                      echo -e "\033[1;31m ğŸ¦† duck say â® fuck âŒ Path not found: ''${${param.name}}\033[0m" >&2
                      exit 1
                    fi
                    ;;
                  bool)
                    if ! [[ "''${${param.name}}" =~ ^(true|false)$ ]]; then
                      echo -e "\033[1;31m ğŸ¦† duck say â® fuck âŒ ${param.name} must be true or false\033[0m" >&2
                      exit 1
                    fi
                    ;;
                esac
              fi
            ''
          ) script.parameters)}

          # ğŸ¦† duck say â® boolean defaults - false if not provided
          ${concatStringsSep "\n" (map (param: 
            optionalString (param.type == "bool" && param.default != null) ''
              if [[ -z "''${${param.name}:-}" ]]; then
                ${param.name}=${if param.default then "true" else "false"}
              fi
            '') script.parameters)}


          ${concatStringsSep "\n" (map (param: 
            optionalString (param.default != null) ''
              if [[ -z "''${${param.name}:-}" ]]; then
                ${param.name}=${
                  if param.type == "string" then 
                    "'${lib.escapeShellArg (toString param.default)}'" 
                  else if param.type == "int" then
                    "${toString param.default}"
                  else if param.type == "bool" then
                    (if param.default then "true" else "false")
                  else if param.type == "path" then
                    "'${lib.escapeShellArg (toString param.default)}'"
                  else
                    "'${lib.escapeShellArg (toString param.default)}'"
                }
              fi
            '') script.parameters)}
            
          # ğŸ¦† duck say â® checkz required param yo - missing? errorz out 
          ${concatStringsSep "\n" (map (param: ''
            ${optionalString (!param.optional && param.default == null) ''
              if [[ -z "''${${param.name}:-}" ]]; then
                echo -e "\033[1;31m ğŸ¦† duck say â® fuck âŒ Missing required parameter: ${param.name}\033[0m" >&2
                exit 1
              fi
            ''}
          '') script.parameters)}

          # ğŸ¦† duck say â® EXECUTEEEEEAAAOO ğŸ¦†quackğŸ¦†quackğŸ¦†quackğŸ¦†quackğŸ¦†quackğŸ¦†quackğŸ¦†quackğŸ¦†quackğŸ¦†quackğŸ¦†quackğŸ¦†quackğŸ¦†yo
          ${script.code}
        '';
        # ğŸ¦† duck say â® generate da entrypoint
        mainScript = pkgs.writeShellScriptBin "yo-${script.name}" scriptContent;
      in # ğŸ¦† duck say â® letz wrap diz up already  
        pkgs.runCommand "yo-script-${script.name}" {} ''
          mkdir -p $out/bin  # ğŸ¦† duck say â® symlinkz da main script
          ln -s ${mainScript}/bin/yo-${script.name} $out/bin/yo-${script.name} 
          ${concatMapStrings (alias: '' # ğŸ¦† duck say â® dont forget to symlinkz da aliases too yo!
            ln -s ${mainScript}/bin/yo-${script.name} $out/bin/yo-${alias}
          '') script.aliases}
        ''
    ) cfg.scripts; # ğŸ¦† duck say â® apply da logic to da yo scriptz
  }; 

  githubBaseUrl = let
    matches = builtins.match ".*github.com[:/]([^/]+)/([^/\\.]+).*" config.this.user.me.repo;
  in if matches != null then
    "https://github.com/${builtins.elemAt matches 0}/${builtins.elemAt matches 1}/blob/main"
  else "";

  # ğŸ¦† duck say â® build scripts for da --help command
  terminalScriptsTableFile = pkgs.writeText "yo-helptext.md" terminalScriptsTable;
  # ğŸ¦† duck say â® markdown help text
  terminalScriptsTable = let # ğŸ¦† duck say â® categorize scripts
    groupedScripts = lib.groupBy (script: script.category) (lib.attrValues cfg.scripts);
    # ğŸ¦† duck say â® sort da scriptz by category
    visibleScripts2 = lib.filterAttrs (_: script: script.visibleInReadme) cfg.scripts;
    groupedScripts2 = lib.groupBy (script: script.category) (lib.attrValues visibleScripts2);
    sortedCategories2 = lib.sort (a: b: 
      # ğŸ¦† duck say â® system management goes first yo
      if a == "ğŸ–¥ï¸ System Management" then true
      else if b == "ğŸ–¥ï¸ System Management" then false
      else a < b # ğŸ¦† duck say â® after dat everything else quack quack
    ) (lib.attrNames groupedScripts2);
  
    # ğŸ¦† duck say â® create table rows with category separatorz 
    rows = lib.concatMap (category:
      let  # ğŸ¦† duck say â® sort from A to Ã–  
        scripts = lib.sort (a: b: a.name < b.name) groupedScripts.${category};
      in
        [ # ğŸ¦† duck say â® add **BOLD** header table row for category
          "| **${escapeMD category}** | | |"
        ] 
        ++ # ğŸ¦† duck say â® each yo script goes into a table row
        (map (script:
          let # ğŸ¦† duck say â® format list of aliases
            aliasList = if script.aliases != [] then
              concatStringsSep ", " (map escapeMD script.aliases)
            else "";
            # ğŸ¦† duck say â® generate CLI parameter hints, with [] for optional/defaulted
            paramHint = concatStringsSep " " (map (param:
              if param.optional || param.default != null
              then "[--${param.name}]"
              else "--${param.name}"
            ) script.parameters);
            # ğŸ¦† duck say â® render yo script syntax with param
            syntax = "\\`yo ${escapeMD script.name} ${paramHint}\\`";
          in # ğŸ¦† duck say â® write full md table row - command | aliases | description
            "| ${syntax} | ${aliasList} | ${escapeMD script.description} |"
        ) scripts)
    ) sortedCategories2;

  in concatStringsSep "\n" rows;

  # ğŸ¦† duck say â® we build da scripts again but diz time for the READNE and diz time script names > links 
  helpTextFile = pkgs.writeText "yo-helptext.md" helpText;
  # ğŸ¦† duck say â® markdown help text
  helpText = let 
    # ğŸ¦† duck say â® URL escape helper for GitHub links
    escapeURL = str: builtins.replaceStrings [" "] ["%20"] str;
  
    # ğŸ¦† duck say â® categorize scripts
    visibleScripts = lib.filterAttrs (_: script: script.visibleInReadme) cfg.scripts;
    groupedScripts = lib.groupBy (script: script.category) (lib.attrValues visibleScripts);
    sortedCategories = lib.sort (a: b: 
      # ğŸ¦† duck wants â® system management to be listed first yo
      if a == "ğŸ–¥ï¸ System Management" then true
      else if b == "ğŸ–¥ï¸ System Management" then false
      else a < b # ğŸ¦† duck say â® after dat everything else quack quack
    ) (lib.attrNames groupedScripts);
  
    # ğŸ¦† duck say â® create table rows with category separatorz 
#    rows = lib.concatMap (category:
#      let  # ğŸ¦† duck say â® sort from A to Ã–  
#        scripts = lib.sort (a: b: a.name < b.name) groupedScripts.${category};
#      in
#        [ # ğŸ¦† duck say â® add **BOLD** header table row for category
#          "| **${escapeMD category}** | | |"
#        ] 
#        ++ # ğŸ¦† duck say â® each yo script goes into a table row
#        (map (script:
#          let 
            # ğŸ¦† duck say â® format list of aliases
#            aliasList = if script.aliases != [] then
#              concatStringsSep ", " (map escapeMD script.aliases)
#            else "";          
            # ğŸ¦† duck say â® generate CLI parameter hints, with [] for optional/defaulted
#            paramHint = concatStringsSep " " (map (param:
#              if param.optional || param.default != null
#              then "[--${param.name}]"
#              else "--${param.name}"
#            ) script.parameters);         
            # ğŸ¦† duck say â® render yo script name as link + parameters as plain text
#            syntax = 
#              if githubBaseUrl != "" then
#                "[yo ${escapeMD script.name}](${githubBaseUrl}/${escapeURL script.filePath}) ${paramHint}"
#              else
#                "yo ${escapeMD script.name} ${paramHint}";
#          in 
            # ğŸ¦† duck say â® write full md table row - command | aliases | description
#            "| ${syntax} | ${aliasList} | ${escapeMD script.description} |"
#        ) scripts)
#    ) sortedCategories;  
#  in concatStringsSep "\n" rows;
  
    # ğŸ¦† duck say â® create table rows with category separatorz 
    rows = lib.concatMap (category:
      let # ğŸ¦† duck say â® sort from A to Ã–  
        scripts = lib.sort (a: b: a.name < b.name) groupedScripts.${category};
      in
        [ # ğŸ¦† duck say â® add **BOLD** header table row for category
          "| **${escapeMD category}** | | | |"
        ] 
        # ğŸ¦† duck say â® each yo script goes into a table row
        ++ (map (script:
          let  # ğŸ¦† duck say â® format list of aliases
            aliasList = if script.aliases != [] then
              concatStringsSep ", " (map escapeMD script.aliases)
            else "";
            # ğŸ¦† duck say â® generate CLI parameter hints, with [] for optional/defaulted
            paramHint = concatStringsSep " " (map (param:
              if param.optional || param.default != null
              then "[--${param.name}]"
              else "--${param.name}"
            ) script.parameters);
            # ğŸ¦† duck say â® render yo script name as link + parameters as plain text
            syntax = 
              if githubBaseUrl != "" then
                "[yo ${escapeMD script.name}](${githubBaseUrl}/${escapeURL script.filePath}) ${paramHint}"
              else
                "yo ${escapeMD script.name} ${paramHint}";
              
            # ğŸ¦† duck say â® add voice ready indicator
            voiceIndicator = if script.voiceReady then "âœ…" else "ğŸ“›";
          in 
            # ğŸ¦† duck say â® voice indicator to the row
            "| ${syntax} | ${aliasList} | ${escapeMD script.description} | ${voiceIndicator} |"
        ) scripts)
    ) sortedCategories;

  in concatStringsSep "\n" rows;

  # ğŸ¦† duck say â® count GENERATED regex patterns (the ~800 count)
  countGeneratedPatterns = script:
    if script.voice == null then
      0
    else
      let # ğŸ¦† duck say â® expand sentence variants with optional wordz
        expandedSentences = lib.concatMap expandOptionalWords script.voice.sentences;
      in
        lib.length expandedSentences;
  
  # ğŸ¦† duck say â® count phrase coverage  
  countUnderstoodPhrases = script:
    if script.voice == null then
      0
    else
      let # ğŸ¦† duck say â® expand sentence variants with optional wordz
        expandedSentences = lib.concatMap expandOptionalWords script.voice.sentences;   
        # ğŸ¦† duck say â® extract parameter names from sentences
        extractParamNames = sentence:
          let # ğŸ¦† duck say â® split by { to find parameters
            parts = lib.splitString "{" sentence;
            paramNames = lib.concatMap (part:
              let
                paramPart = lib.splitString "}" part;
              in
                if lib.length paramPart > 1 then
                  [ (lib.elemAt paramPart 0) ]
                else
                  []
            ) (lib.tail parts); # Skip the first part (before first {)
          in
            paramNames; 
        # ğŸ¦† duck say â® count parameter combinations for each expanded sentence
        countPhrasesForSentence = sentence:
          let
            paramNames = extractParamNames sentence;
          in
            if paramNames == [] then
              1
            else
              let # ğŸ¦† duck say â® count possible values for each parameter
                paramValueCounts = map (paramName:
                  let
                    list = script.voice.lists.${paramName} or null;
                  in
                    if list == null then 1
                    else lib.length list.values
                ) paramNames;           
                # ğŸ¦† duck say â® multiply counts for all parameters
                totalCombinations = lib.foldl (a: b: a * b) 1 paramValueCounts;
              in
                totalCombinations; 
        # ğŸ¦† duck say â® sum phrases across all expanded sentences
        totalPhrases = lib.foldl (total: sentence:
          total + countPhrasesForSentence sentence
        ) 0 expandedSentences;
      in
        totalPhrases;
  
  # ğŸ¦† duck say â® count generated patterns
  countTotalGeneratedPatterns = scripts:
    lib.foldl (total: script: 
      total + countGeneratedPatterns script
    ) 0 (lib.attrValues scripts);
  
  # ğŸ¦† duck say â® count phrases across all scriptz  
  countTotalUnderstoodPhrases = scripts:
    lib.foldl (total: script: 
      total + countUnderstoodPhrases script
    ) 0 (lib.attrValues scripts);
  
  # ğŸ¦† duck say â® quack! da duck take a list of listz and duck make all da possible combinationz
  cartesianProductOfLists = lists:
    # ğŸ¦† duck say â® if da listz iz empty .. 
    if lists == [] then
      [ [] ] # ğŸ¦† duck say â® .. i gib u empty listz of listz yo got it?
    else # ğŸ¦† duck say â® ELSE WAT?!
      let # ğŸ¦† duck say â® sorry.. i gib u first list here u go yo
        head = builtins.head lists;
        # ğŸ¦† duck say â® remaining listz for u here u go bro!
        tail = builtins.tail lists;
        # ğŸ¦† duck say â® calculate combinations for my tail - yo calc wher u at?!
        tailProduct = cartesianProductOfLists tail;
      in # ğŸ¦† duck say â® for everyy x in da listz ..
        lib.concatMap (x:
          # ğŸ¦† duck say â® .. letz combinez wit every tail combinationz ..  
          map (y: [x] ++ y) tailProduct
        ) head; # ğŸ¦† duck say â® dang! datz a DUCK COMBO alright!
  
  # ğŸ¦† duck say â® here i duckie help yo out! makin' yo life eazy sleazy' wen declarative sentence yo typin'    
  expandOptionalWords = sentence: # ğŸ¦† duck say â® qucik & simple sentences we quacky & hacky expandin'
    let # ğŸ¦† duck say â® CHOP CHOP! Rest in lil' Pieceez bigg sentence!!1     
      tokens = lib.splitString " " sentence;      
      # ğŸ¦† duck say â® definin' dem wordz in da (braces) taggin' dem' wordz az (ALTERNATIVES) lettin' u choose one of dem wen triggerin' 
      isRequiredGroup = t: lib.hasPrefix "(" t && lib.hasSuffix ")" t;
      # ğŸ¦† duck say â® puttin' sentence wordz in da [bracket] makin' em' [OPTIONAL] when bitchin' u don't have to be pickin' woooho 
      isOptionalGroup = t: lib.hasPrefix "[" t && lib.hasSuffix "]" t;   
      expandToken = token: # ğŸ¦† duck say â® dis gets all da real wordz out of one token (yo!)
        if isRequiredGroup token then
          let # ğŸ¦† duck say â® thnx 4 lettin' ducklin' be cleanin' - i'll be removin' dem "()" 
            clean = lib.removePrefix "(" (lib.removeSuffix ")" token);
            alternatives = lib.splitString "|" clean; # ğŸ¦† duck say â® use "|" to split (alternative|wordz) yo 
          in  # ğŸ¦† duck say â® dat's dat 4 dem alternativez
            alternatives
        else if isOptionalGroup token then
          let # ğŸ¦† duck say â® here we be goin' again - u dirty and i'll be cleanin' dem "[]"
            clean = lib.removePrefix "[" (lib.removeSuffix "]" token);
            alternatives = lib.splitString "|" clean; # ğŸ¦† duck say â® i'll be stealin' dat "|" from u 
          in # ğŸ¦† duck say â® u know wat? optional means we include blank too!
            alternatives ++ [ "" ]
        else # ğŸ¦† duck say â® else i be returnin' raw token for yo
          [ token ];      
      # ğŸ¦† duck say â® now i gib u generatin' all dem combinationz yo
      expanded = cartesianProductOfLists (map expandToken tokens);      
      # ğŸ¦† duck say â® clean up if too much space, smush back into stringz for ya
      trimmedVariants = map (tokenList:
        let # ğŸ¦† duck say â® join with spaces then trim them suckers
          raw = lib.concatStringsSep " " tokenList;
          # ğŸ¦† duck say â® remove ALL extra spaces
          cleaned = lib.replaceStrings ["  "] [" "] (lib.strings.trim raw);
        in # ğŸ¦† duck say â® wow now they be shinin'
          cleaned 
      ) expanded; # ğŸ¦† duck say â® and they be multiplyyin'!      
      # ğŸ¦† duck say â® throwin' out da empty and cursed ones yo
      nonEmpty = lib.filter (s: s != "") trimmedVariants;
      hasFixedText = v: builtins.match ".*[^\\{].*" v != null; # ğŸ¦† duck say â® no no no, no nullin'
      validVariants = lib.filter hasFixedText nonEmpty;
    in # ğŸ¦† duck say â® returnin' all unique variantz of da sentences â€“ holy duck dat'z fresh 
      lib.unique validVariants;
  
in { # ğŸ¦† duck say â® options options duck duck
  options = { # ğŸ¦† duck say â® 
    yo = {
      pkgs = mkOption {
        type = types.package;
        readOnly = true;
        description = "The final yo scripts package";
      }; # ğŸ¦† duck say â® yo scriptz optionz yo
      scripts = mkOption {
        type = types.attrsOf scriptType;
        default = {};
        description = "Attribute set of scripts to be made available";
      }; # ğŸ¦† duck say â® generated regex patterns count
      generatedPatterns = mkOption {
        type = types.int;
        readOnly = true;
        description = "Number of regex patterns generated at build time";
      }; # ğŸ¦† duck say â® count nlp phrases understood  
      understandsPhrases = mkOption {
        type = types.int;
        readOnly = true;
        description = "Theoretical number of unique spoken phrases the system can understand";
      };
    };
  };  
  
  # ğŸ¦† â® CONFIG  ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†ğŸ¦†#    
  config = {  # ğŸ¦† duck say â® expose diz module and all yo.scripts as a package  
    yo.pkgs = yoScriptsPackage; # ğŸ¦† duck say â® reference as: ${config.pkgs.yo}/bin/yo-<name>
    # ğŸ¦† duck say â® set global counterz
    yo.generatedPatterns = countTotalGeneratedPatterns cfg.scripts;
    yo.understandsPhrases = countTotalUnderstoodPhrases cfg.scripts;
    
    assertions = let # ğŸ¦† â® safety first
      scripts = cfg.scripts;
      scriptNames = attrNames scripts;      
      # ğŸ¦† duck say â® quackin' flappin' mappin' aliasez â® script dat belong to it 
      aliasMap = lib.foldl' (acc: script:
        lib.foldl' (acc': alias:
          acc' // { 
            ${alias} = (acc'.${alias} or []) ++ [script.name]; # ğŸ¦† duck say â® mmerge or start a list yo
          }
        ) acc script.aliases
      ) {} (attrValues scripts);
      # ğŸ¦† duck say â® find conflicts between script names & script aliases
      scriptNameConflicts = lib.filterAttrs (alias: _: lib.elem alias scriptNames) aliasMap;  
      # ğŸ¦† duck say â® find dem' double aliasez 
      duplicateAliases = lib.filterAttrs (_: scripts: lib.length scripts > 1) aliasMap;
      # ğŸ¦† duck say â® build da alias conflict error msg
      formatConflict = alias: scripts: 
        "Alias '${alias}' conflicts with script name (used by: ${lib.concatStringsSep ", " scripts})";       
      # ğŸ¦† duck say â® build da double rainbowz error msg yo
      formatDuplicate = alias: scripts: 
        "Alias '${alias}' used by multiple scripts: ${lib.concatStringsSep ", " scripts}";
      # ğŸ¦† duck say â® find auto-start scriptz - if i find i make sure it haz default values for all required paramz
      autoStartErrors = lib.mapAttrsToList (name: script:
        if script.autoStart then
          let # ğŸ¦† duck say â® filter dem not optional no defaultz
            missingParams = lib.filter (p: !p.optional && p.default == null) script.parameters;
          in
            if missingParams != [] then
              "ğŸ¦† duck say â® fuck âŒ Cannot auto-start '${name}' - missing defaults for: " +
              lib.concatMapStringsSep ", " (p: p.name) missingParams
            else null
        else null
      ) scripts;    
      
      nonInteractiveErrors = lib.mapAttrsToList (name: script:
        if script.autoStart || script.runEvery != null then
          let
            missingParams = lib.filter (p: !p.optional && p.default == null) script.parameters;
          in
            if missingParams != [] then
              "ğŸ¦† duck say â® fuck âŒ Cannot schedule '${name}' - missing defaults for: " +
              lib.concatMapStringsSep ", " (p: p.name) missingParams
            else null
        else null
      ) scripts;      
      # ğŸ¦† duck say â® clean out dem' nullz! no nullz in ma ASSertionthz! ... quack
      actualAutoStartErrors = lib.filter (e: e != null) autoStartErrors;   
    in [
      { # ğŸ¦† duck say â® assert no alias name cpmflict with script name 
        assertion = scriptNameConflicts == {};
        message = "ğŸ¦† duck say â® fuck âŒ Alias/script name conflicts:\n" +
          lib.concatStringsSep "\n" (lib.mapAttrsToList formatConflict scriptNameConflicts);
      }
      { # ğŸ¦† duck say â® make sure dat aliases unique are yo
        assertion = duplicateAliases == {};
        message = "ğŸ¦† duck say â® fuck âŒ Duplicate aliases:\n" +
          lib.concatStringsSep "\n" (lib.mapAttrsToList formatDuplicate duplicateAliases);
      }
      { # ğŸ¦† duck say â® autoStart scriptz must be fully configured of course!
        assertion = actualAutoStartErrors == [];
        message = "Auto-start errors:\n" + lib.concatStringsSep "\n" actualAutoStartErrors;
      }
    ];
    # ğŸ¦† duck say â® TODO replace with: system.activationScripts.update-readme.text = "${updateReadme}/bin/update-readme";
    system.build.updateReadme = pkgs.runCommand "update-readme" {
      helpTextFile = helpTextFile;
    } ''
      mkdir -p $out
      cp ${toString ./../README.md} $out/README.md
      ${pkgs.gnused}/bin/sed -i '/<!-- YO_DOCS_START -->/,/<!-- YO_DOCS_END -->/c\
    <!-- YO_DOCS_START -->\
    ## ğŸ¦† **Yo Commands Reference**\
    *Automagiduckically generated from module definitions*\
    \
    '"$(cat ${helpTextFile})"'\
    <!-- YO_DOCS_END -->' $out/README.md
    '';
    environment.systemPackages = [
      config.yo.pkgs
      pkgs.glow # ğŸ¦† duck say â® For markdown renderin' in da terminal
      updateReadme # ğŸ¦† duck say â® to update da readme of course ya non duck
      (pkgs.writeShellScriptBin "yo" ''
        #!${pkgs.runtimeShell}
        script_dir="${yoScriptsPackage}/bin" 
        # ğŸ¦† duck say â® help command data (yo --help
        show_help() {
          #width=$(tput cols) # ğŸ¦† duck say â® Auto detect width
          width=130 # ğŸ¦† duck say â® fixed width
          cat <<EOF | ${pkgs.glow}/bin/glow --width $width -
        ## â”€â”€â”€â”€â”€â”€â‹†â‹…â˜†â˜†â˜†â‹…â‹†â”€â”€â”€â”€â”€â”€ ##
        ## ğŸ¦†ğŸš€ **yo CLI** ğŸ¦†ğŸ¦† 
        ## ğŸ¦† duck say â® quack! i help with scripts yo
        **Usage:** \`yo <command> [arguments]\`
        ## â”€â”€â”€â”€â”€â”€â‹†â‹…â˜†â˜†â˜†â‹…â‹†â”€â”€â”€â”€â”€â”€ ##
        ## ğŸ¦†âœ¨ Available Commands
        Parameters inside brackets are [optional]
        | Command Syntax               | Aliases    | Description |
        |------------------------------|------------|-------------|
        ${terminalScriptsTable}
        ## â”€â”€â”€â”€â”€â”€â‹†â‹…â˜†â˜†â˜†â‹…â‹†â”€â”€â”€â”€â”€â”€ ##
        ## ğŸ¦†â“ Detailed Help
        For specific command help: \`yo <command> --help\`
        \`yo do --help\` will list all defined voice intents.
        \`yo zigduck --help\` will display a battery status report for your deevices.
        ğŸ¦†ğŸ¦†
        EOF
          exit 0
        } # ğŸ¦† duck say â® handle zero args           
        if [[ $# -eq 0 ]]; then
          show_help
          exit 1
        fi
        # ğŸ¦† duck say â® parse da command
        case "$1" in # ğŸ¦† duck say â® handle zero args "-h" & "--help" to show da help
          -h|--help) show_help; exit 0 ;;
          *) command="$1"; shift ;;
        esac

        script_path="$script_dir/yo-$command"
        if [[ -x "$script_path" ]]; then
          exec "$script_path" "$@"
        else
          # ğŸ¦† duck say â® TODO FuzzYo commands! but until then..
          echo -e "\033[1;31m ğŸ¦† duck say â® fuck âŒ $1\033[0m Error: Unknown command '$command'" >&2
          show_help
          exit 1
        fi
      '')
      yoScriptsPackage
      updateReadme
    ];

    # ğŸ¦† duck say â® buildz systemd services    
    systemd.services = lib.mkMerge [
      # ğŸ¦† duck say â® if `autoStart` is set
      (lib.mapAttrs' (name: script:
        lib.nameValuePair "yo-${name}" (mkIf script.autoStart {
          enable = true;
          wantedBy = ["multi-user.target"];
          after = ["sound.target" "network.target" "pulseaudio.socket" "sops-nix.service"];
    
          serviceConfig = {
            ExecStart = let
              args = lib.concatMapStringsSep " " (param:
                "--${param.name} ${lib.escapeShellArg param.default}"
              ) (lib.filter (p: p.default != null) script.parameters);
            in "${yoScriptsPackage}/bin/yo-${name} ${args}";
            User = config.this.user.me.name;
            Group = "audio";
            RestartSec = 45;
            Restart = "on-failure";
            Environment = [
              "XDG_RUNTIME_DIR=${
                if config.this.host.hostname == "desktop" then "/run/user/1000"
                else if config.this.host.hostname == "homie" then "/run/user/1002"
                else if config.this.host.hostname == "nasty" then "/run/user/1000"
                else "/run/user/1000"
              }"
              "PULSE_SERVER=unix:%t/pulse/native"
              "HOME=/home/${config.this.user.me.name}"
              "PATH=/run/current-system/sw/bin:/bin:/usr/bin:${pkgs.binutils-unwrapped}/bin:${pkgs.coreutils}/bin"
            ];
          };
        })
      ) cfg.scripts)
    
      # ğŸ¦† duck say â® if `runEvery` is set 
      (lib.mapAttrs' (name: script:
        lib.nameValuePair "yo-${name}-periodic" (mkIf (script.runEvery != null) {
          enable = true;
          description = "Periodic execution of yo script ${name}";
          serviceConfig = {
            Type = "oneshot";
            User = config.this.user.me.name;
            Group = config.this.user.me.name;
            Environment = [                        
              "HOME=/home/${config.this.user.me.name}"
              "PATH=/run/current-system/sw/bin:/bin:/usr/bin:${pkgs.binutils-unwrapped}/bin:${pkgs.coreutils}/bin"
            ];  
            ExecStart = let
              args = lib.concatMapStringsSep " " (param:
                "--${param.name} ${lib.escapeShellArg param.default}"
              ) (lib.filter (p: p.default != null) script.parameters);
            in "${yoScriptsPackage}/bin/yo-${name} ${args}";
          };
        })
      ) cfg.scripts)
    ];
    
    # ğŸ¦† duck say â® systemd timer configuration if `runEvery` is configured for a script 
    systemd.timers = lib.mapAttrs' (name: script:
      lib.nameValuePair "yo-${name}-periodic" (mkIf (script.runEvery != null) {
        enable = true;
        wantedBy = ["timers.target"];
        timerConfig = {
          OnCalendar = "*-*-* *:0/${script.runEvery}";
          Unit = "yo-${name}-periodic.service";
          Persistent = true;
        };
      })
    ) cfg.scripts;    
  };} # ğŸ¦† duck say â® 2 long script 4 jokez.. nao bai bai yo
# ğŸ¦† says â® QuackHack-McBLindy out!
# ... ğŸ›ŒğŸ¦†ğŸ’¤
