# dotfiles/modules/networking/wg-client.nix â®ž https://github.com/quackhack-mcblindy/dotfiles
{ # ðŸ¦† say â®ž  Simple WireGuardâ„¢ client configuration
  config,
  lib,
  pkgs,
  self,
  ...
} : let # ðŸ¦† duck say â®ž get'z all potential servers 
  potentialServers = lib.filter (cfg:
    lib.elem "wg-server" (cfg.config.this.host.modules.networking or [])
  ) (lib.attrValues self.nixosConfigurations);

  # ðŸ¦† duck say â®ž select'z first found server with safety check
  wgServer = if potentialServers != [] 
    then lib.head potentialServers 
    else throw "No WireGuard server configuration found";

  # ðŸ¦† duck say â®ž helper functionz
  serverPublicKey = wgServer.config.this.host.keys.publicKeys.wireguard or "";
  serverIP = wgServer.config.this.host.ip or (throw "WireGuard server IP not found");
  serverPort = wgServer.config.networking.wireguard.interfaces.wg0.listenPort or 51820;

  # ðŸ¦† duck say â®ž client configuration
  clientWgIP = config.this.host.wgip or (throw "Client WireGuard IP not configured");
  clientPrivateKeySecret = "${config.networking.hostName}_wireguard_private";
in { # ðŸ¦† duck say â®ž activate client service by exposing `"wg-client"` at `config.this.host.modules.networking`
  config = lib.mkIf (lib.elem "wg-client" config.this.host.modules.networking) {
    # ðŸ¦† duck say â®ž secret keepin'
    sops.secrets.${clientPrivateKeySecret} = lib.mkIf (!config.this.installer) {
      sopsFile = ../../secrets/hosts/${config.networking.hostName}/${clientPrivateKeySecret}.yaml;
      owner = "wgUser";
      group = "wgUser";
      mode = "0440";
    };
    
    # ðŸ¦† duck say â®ž network keepin'
    networking.wireguard.interfaces.wg0 = {
      ips = [ "${clientWgIP}/24" ];
      privateKeyFile = config.sops.secrets.${clientPrivateKeySecret}.path;      
      peers = [
        {
          publicKey = serverPublicKey;
          allowedIPs = [ "10.0.0.0/24" "192.168.1.0/24" ];
          endpoint = "${serverIP}:${toString serverPort}";
          persistentKeepalive = 25;
        }
      ];
    };
    # ðŸ¦† duck say â®ž NixOS user configuration 
    users.users.wgUser = {
      group = "wgUser";
      home = "/home/wgUser";
      createHome = true;
      isSystemUser = true;
    };

    users.groups.wgUser = {};
  };}
