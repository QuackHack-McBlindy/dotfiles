# dotfiles/modules/networking/wg-server.nix â®ž https://github.com/quackhack-mcblindy/dotfiles
{ # ðŸ¦† duck say â®ž A duckz dynamic approach to configuring a WireGuardâ„¢ server declaratively
  config,
  lib,
  pkgs,
  self,
  ...  
} : let
# ðŸ¦† says â®ž Place your encrypted private keys in `dotfiles/secrets/hosts/<device>/<device>_wireguard_private.yaml` 
# ðŸ¦† says â®ž Define your NIxOS clients like so: 
# config.this.host.wgip = "<ip>";
# config.this.host.keys.publicKeys = { wireguard = "<pubkey>": };
# ðŸ¦† says â®ž Define your mobile device clients like this: 
# config.this.user.me.mobileDevices = { <device> = { wgip = "<ip>"; pubkey = "<pubkey>"; }; };

  # ðŸ¦† says â®ž WireGuardâ„¢ User home directory
  wgUserHome = "/home/wgUser";
  
  # ðŸ¦† says â®ž WireGuardâ„¢ tunnel'z allowed IP'z
  defaultAllowedIPs = [ "10.0.0.0/24" "192.168.1.0/24" ];
  # ðŸ¦† says â®ž can also be defined per device at `config.this.user.me.mobileDevices.<device.allowedIPs` but this is optional
  
  # ðŸ¦† says â®ž Server configuration
  serverCfg = config.this.host;
  
  # ðŸ¦† says â®ž Get NixOS host peers
  peerHosts = lib.filterAttrs (_: cfg:
    lib.elem "wg-client" (cfg.config.this.host.modules.networking or [])
  ) self.nixosConfigurations;

  # ðŸ¦† says â®ž Mobile devices configuration
  mobileDevices = config.this.user.me.mobileDevices or {};

  # ðŸ¦† says â®ž Helper functions
  peerPublicKey = host: host.config.this.host.keys.publicKeys.wireguard;
  peerWgIP = host: host.config.this.host.wgip;

  # ðŸ¦† says â®ž SOPS configuration generator
  mkSopsSecret = name: {
    sopsFile = ../../secrets/hosts/${name}/${name}_wireguard_private.yaml;
    owner = "wgUser";
    group = "wgUser";
    mode = "0440";
  };
in { # ðŸ¦† says â®ž choose server host by exposing `"wg-server"` in `this.host.modules.networking`
  config = lib.mkIf (lib.elem "wg-server" serverCfg.modules.networking) {
    sops.secrets = (
      { "${config.networking.hostName}_wireguard_private" = mkSopsSecret config.networking.hostName; }
      //
      (lib.mapAttrs' (n: _: lib.nameValuePair "${n}_wireguard_private" (mkSopsSecret n)) peerHosts)
      //
      (lib.listToAttrs (map (d: lib.nameValuePair "${d}_wireguard_private" (mkSopsSecret d)) (lib.attrNames mobileDevices)))
      //
      { # ðŸ¦† says â®ž domain/ip to run da server on
        domain = { 
          sopsFile = ../../secrets/domain.yaml;
          owner = "wgUser";
          group = "wgUser";
          mode = "0440";
        };
      }
    );
   
    # ðŸ¦† says â®ž network configuration 
    networking = { # ðŸ¦† says â®ž open UDP firewall port
      firewall.allowedUDPPorts = [ 51820 ];
      # ðŸ¦† says â®ž WireGuardâ„¢ interface config 
      wireguard.interfaces.wg0 = {
        ips = [ "${serverCfg.wgip}/24" ];
        listenPort = 51820;
        privateKeyFile = config.sops.secrets."${config.networking.hostName}_wireguard_private".path;
        peers = 
          # ðŸ¦† says â®ž NixOS host peers
          (lib.mapAttrsToList (_: host: {
            publicKey = peerPublicKey host;
            allowedIPs = [ "${peerWgIP host}/32" ];
          }) peerHosts)
          ++
          # ðŸ¦† says â®ž mobile device peers
          (lib.mapAttrsToList (name: cfg: {
            publicKey = cfg.pubkey;
            allowedIPs = [ "${cfg.wgip}/32" ];
          }) mobileDevices);
      };
    };

    # ðŸ¦† says â®ž secret readin' before yo! 
    systemd.services.wireguard-wg0.after = [ "sops-nix.service" ];

    # ðŸ¦† says â®ž systemd service dat generates random colored quacky QR codez for mobile devicez yo
    systemd.services.generate-wg-qr = (let
      qrDependencies = with pkgs; [ qrencode imagemagick ];
      path = lib.makeBinPath ([ pkgs.coreutils pkgs.gnused ] ++ qrDependencies);
    in {
      serviceConfig = {
        Type = "oneshot";
        User = "wgUser";
        Group = "wgUser";
        Environment = "PATH=${path}";
      };
      # ðŸ¦† says â®ž da script that daz everything, yo!
      script = let
        # ðŸ¦† says â®ž removes old QR files
        deleteOld = lib.concatMapStringsSep "\n" (d: ''
          rm -f "${wgUserHome}/${d}.conf" "${wgUserHome}/${d}.png"
        '') (lib.attrNames mobileDevices);
        # ðŸ¦† says â®ž create new QR
        generateQR = device: let
          allowed = lib.concatStringsSep ", " (mobileDevices.${device}.allowedIPs or defaultAllowedIPs);
        in ''
          TEMP_DIR=$(mktemp -d)
          PRIVATE_KEY=$(cat ${config.sops.secrets."${device}_wireguard_private".path})
          # ðŸ¦† says â®ž insert data into QR
          cat > "$TEMP_DIR/${device}.conf" <<EOF
[Interface]
PrivateKey = $PRIVATE_KEY
Address = ${mobileDevices.${device}.wgip}/24
DNS = ${serverCfg.ip}

[Peer]
PublicKey = ${serverCfg.keys.publicKeys.wireguard}
AllowedIPs = ${allowed}
Endpoint = $(cat ${config.sops.secrets.domain.path}):51820
PersistentKeepalive = 25
EOF
          ${pkgs.coreutils}/bin/cp "$TEMP_DIR/${device}.conf" "${wgUserHome}/${device}.conf"
          ${config.yo.pkgs}/bin/yo-qr --input "$TEMP_DIR/${device}.conf" --output "${wgUserHome}/${device}.png"
          rm -rf "$TEMP_DIR" # ðŸ¦† says â®ž cleanup
        '';
      in ''
        mkdir -p "${wgUserHome}"        
        ${deleteOld}
        ${lib.concatMapStringsSep "\n" generateQR (lib.attrNames mobileDevices)}
      '';   
      after = [ "sops-nix.service" ];
      wantedBy = [ "multi-user.target" ];
    });

    # ðŸ¦† says â®ž create da user
    users = {
      groups.wgUser = {};
      users.wgUser = {
        group = "wgUser";
        home = "${wgUserHome}";
        createHome = true;
        isSystemUser = true;
      };
    }; 
  };} # ðŸ¦† says â®ž zimple az dat, yo!
# ðŸ¦† says â®ž QuackHack-McBlindy out!
