# dotfiles/modules/home.nix â®ž https://github.com/quackhack-mcblindy/dotfiles
{ # ðŸ¦† duck say â®ž if u expect home man - you are out of duck
  config,
  lib,
  pkgs,
  ... # ðŸ¦† duck say â®ž create a file like diz:  file = { ".config/myfile.txt" = "hello world"; };     
} : with lib;
let # ðŸ¦† duck say â®ž big ducks build their own home

  # ðŸ¦† duck say â®ž Create a file, yo!
  homeBase = config.this.user.me.dotfilesDir + "/home";
  sanitize = path: 
    replaceStrings ["/"] ["-"] (removePrefix "/" (removePrefix "./" path));
  
  # ðŸ¦† duck say â®ž Create a home, yo!
  mkUserLinks = user: baseDir: let
    userHome = config.users.users.${user}.home;
    storePath = builtins.path {
      path = baseDir;
      name = "home-manifest";
    };
  in ''
    echo "ðŸ¦† duck say â®ž Mirroring home directory for ${user}"
    find ${storePath} -type f -print0 | while IFS= read -r -d $'\0' src; do
      rel_path="''${src#${storePath}/}"
      target="${userHome}/''${rel_path}"
    
      # ðŸ¦† duck say â®ž Skip if symlink already correct
      # if [[ -L "$target" && "$(readlink -f "$target")" == "$src" ]]; then
      #  continue
      # fi
    
      echo "ðŸ¦† duck say â®ž Linking: $rel_path"
      mkdir -vp "$(dirname "$target")"
      
      dir="$(dirname "$target")"
      if [[ ! -d "$dir" ]]; then
        chown ${user}:users "$dir"
      fi

      [[ -e "$target" && ! -L "$target" ]] && mv -f "$target" "$target.backup"

      ln -vsfn "$src" "$target"
      chown -h ${user}:users "$target"
    done
  '';

in {  

  options = {
    file = mkOption {
      type = types.attrsOf types.lines;
      default = {};
      description = "Files to create directly under ${homeBase}";
    };
    this.home = mkOption {
      type = types.path;
      description = "Directory to mirror to home directory";
    };
  };

  config = mkMerge [
    # ðŸ¦† duck say â®ž Create the file, yo!
    {
      system.activationScripts.simpleFiles = let
        files = config.file;

      in {
        text = concatStringsSep "\n" (mapAttrsToList (path: content:
          let
            storeName = "file-${sanitize path}";
            storePath = pkgs.writeText storeName content;
            fullPath = "${homeBase}/${path}";
            dir = dirOf fullPath;
            username = config.this.user.me.name;
          in ''
            mkdir -p "${dir}"
            cp -f "${storePath}" "${fullPath}"
            chown "${username}:users" "${fullPath}"
            chmod 600 "${fullPath}"
            echo "ðŸ¦† duck say â®ž Created file: ${fullPath}"
          '') files);
        deps = [];  
      };
    }
    
    {
      file."README.md" = ''
        # ðŸ¦†ðŸ§‘â€ðŸ¦¯ **QuackHack-McBLindy'z â®ž home directory yay** ðŸ¦†ðŸ§‘â€ðŸ¦¯

        > [!CAUTION]
        > **THIS IS NOT HOME-MANAGER!**  
        > **Ducks don't use home-manager.** ðŸ¦†

        **Why?** I don't like it.  
        
        **ðŸ¦† duck say â®ž quack - diz iz my directory**
        **ðŸ¦† duck say â®ž i handle filez**  
        
        ```nix
          file."ducks.md" = "ðŸ¦† duck say â®ž like diz yay";
          
        ```
        
      '';
    }
    
    # ðŸ¦† duck say â®ž symlink the home, yo!
    (mkIf (config.this.home != null) {
      system.activationScripts.home-mirror = {
        text = ''
          #!/usr/bin/env bash
          set -euo pipefail
          ${mkUserLinks config.this.user.me.name config.this.home}
        '';
        deps = [ "users" ];
      };

      # ðŸ¦† duck say â®ž Set user variiables quack
      environment.variables = {
        BROWSER = "firefox";
        EDITOR = "nano";
        TERMINAL = "ghostty";
        QT_QPA_PLATFORMTHEME = "gtk3";
        QT_SCALE_FACTOR = "1";
        MOZ_ENABLE_WAYLAND = "1";
        QT_WAYLAND_DISABLE_WINDOWDECORATION = "1";
        QT_AUTO_SCREEN_SCALE_FACTOR = "1";
        CLUTTER_BACKEND = "wayland";
        XDG_CURRENT_DESKTOP = "gnome";
        XDG_SESSION_DESKTOP = "gnome";
        XDG_SESSION_TYPE = "wayland";
        GTK_USE_PORTAL = "1";
        XDG_CACHE_HOME = "\${HOME}/.cache";
        XDG_CONFIG_HOME = "\${HOME}/.config";
        XDG_BIN_HOME = "\${HOME}/dotfiles/home/bin";
        XDG_DATA_HOME = "\${HOME}/.local/share";
        NIX_PATH = lib.mkForce "nixpkgs=flake:nixpkgs";
      };
    })
  ];}
